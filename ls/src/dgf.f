      SUBROUTINE DGF(DIAM)
      IMPLICIT NONE
C----------
C  **DGF--LS    DATE OF LAST REVISION:   12/17/12C----------
C  THIS SUBROUTINE COMPUTES THE VALUE OF DDS (CHANGE IN SQUARED
C  DIAMETER) FOR EACH TREE RECORD, AND LOADS IT INTO THE ARRAY
C  WK2.  DIAMETER GROWTH IS PREDICTED FROM DBH, SITE INDEX, AND
C  CROWN RATIO.  DIAMETER GROWTH IS THEN MULTIPLIED BY A MODIFIER
C  FUNCTION WHICH IS COMPUTED FROM DBH, STAND'S AVERAGE DBH, MAX
C  BASAL AREA EXPECTED FOR THE SPECIES, AND TREE'S BASAL AREA.
C  AN ADJUSTMENT FACTOR, COMPUTED FROM DBH, IS THEN ADDED.  THE
C  SET OF TREE DIAMETERS TO BE USED IS PASSED AS THE ARGUMENT TO
C  DIAM.  THE PROGRAM THUS HAS THE FLEXIBILITY TO PROCESS DIFFERENT
C  CALIBRATION OPTIONS.  THIS ROUTINE IS CALLED BY **DGDRIV** DURING
C  CALIBRATION AND WHILE CYCLING FOR GROWTH PREDICTION.  ENTRY
C  **DGCONS** IS CALLED BY **RCON** TO LOAD SITE DEPENDENT COEFFICIENTS
C  THAT NEED ONLY BE RESOLVED ONCE.
C
C
C
C  DIAMETER GROWTH EQUATIONS ARE FROM LS-TWIGS VERSION 3.01
C  OR FOR DIAMETER GROWTH SEE: HAHN AND LEARY, 'POTENTIAL DIAMETER
C  GROWTH FUNCTIONS'. GEN. TECH. REP. NC-49. SEE PPS. 22-26.
C  FOR THE MODIFIER EQUATION SEE: HOLDAWAY. 1984. 'MODELING THE EFFECT
C  OF COMPETITION ON TREE DIAMETER GROWTH AS APPLIED IN STEMS.' RES.
C  PAP. NC-49.
C  FOR THE DIAMETER ADJUSTMENT FACTOR, SEE: HOLDAWAY. 1985. 'ADJUSTING
C  STEMS GROWTH MODEL FOR WISCONSIN FORESTS.' RES. PAP. NC-267.
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CALCOM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C----------
C  VARIABLES DEFINED:
C  PADG  -- POTENTIAL ANNUAL DIAMETER GROWTH
C  OMEGA -- FUNCTION CHARACTERIZING THE INDIVIDUAL TREE'S RELATIVE
C           DIAMETER EFFECT ON DIAMETER GROWTH FUNCTION
C  BETA  -- FUNCTION CHARACTERIZING AVERAGE STAND DIAMETER EFFECT OF
C           MODIFIER
C  GM    -- DIAMETER GROWTH MODIFIER FUNCTION
C  DELD  -- TREE'S DIAMETER GROWTH FOR ONE YEAR
C  ADJ   -- DIAMETER ADJUSTMENT FACTOR
C	      PER SPECIES).
C  CHECK -- MINIMUM VALUES OF D/RMSQD THAT CAN BE USED IN THE EXPRESSION
C           FOR OMEGA WITHOUT CAUSEING A NUMERICAL UNDERFLOW ERROR
C           THESE HAVE BEEN MOVED TO SUBROUTINE **BALMOD**
C  COEFFICIENTS DEFINED:
C  A1-A5 -- COEFFICIENTS FOR PADG EQUATION
C  B1-B4 -- COEFFICIENTS FOR OMEGA EQUATION
C           THESE HAVE BEEN MOVED TO SUBROUTINE **BALMOD**
C  C1,C2 -- COEFFICIENTS FOR BETA EQUATION
C           THESE HAVE BEEN MOVED TO SUBROUTINE **BALMOD**
C  D1-D3 -- COEFFICIENTS FOR ADJ EQUATION
C  BAMAX -- MAXIMUM BASAL AREA EXPECTED FOR THE SPECIES
C           THESE HAVE BEEN MOVED TO SUBROUTINE **BALMOD**
C----------
      LOGICAL DEBUG
C
      REAL A1(MAXSP),A2(MAXSP),A3(MAXSP),A4(MAXSP),A5(MAXSP),
     &   D1(MAXSP),D2(MAXSP),D3(MAXSP),DIAM(MAXTRE)
      INTEGER OBSERV(MAXSP)
      REAL TEMD(MAXTRE)
      INTEGER I,ISPC,I1,I2,I3,ILOOP
      REAL SI,CR,D,TRBA,PADG,GM,DELD,DA,ADJ,BARK,BRATIO,DDS,X1,XPPDDS
      REAL DIAGR
C----------
C  OBSERV CONTAINS THE NUMBER OF OBSERVATIONS BY SPECIES
C  FOR THE UNDERLYING MODEL (THIS DATA IS ACTUALLY
C  USED BY **DGDRIV** FOR CALIBRATION.
C----------
      DATA  OBSERV/
     & 2*4625, 2*11663, 845, 2*1772, 1277, 1006, 165, 1070,
     & 140, 2*4625, 2*240, 4*937, 3*898, 343,
     & 1092, 3*3753, 866, 4*517, 1555, 2*290, 3*349,
     & 261, 2*2124, 748, 25*190/
C----------
C  COEFFICIENTS FOR GROWTH EQUATION.
C----------
      DATA A1/
     % 2*.16062,2*.09446,.25578,2*.17056,.122,.10713,.11147,.13403,
     % .16872,2*.16062,2*.058807,4*.10948,3*.28496,.15155,
     % .25402,3*.18772,.21167,4*.12654,.15535,2*.17358,3*.16471,
     % .23490,2*.21645,.10971,5*.37510,20*.37510/
      DATA A2/
     % 2*.000009,2*.000120,.000880,2*.014516,.0008,.00107,.000010,
     % .000001,.0000003,2*.000009,2*.000000006,4*.000040,
     % 3*.00182,.000003,.000004,3*.000007,.000003,4*.000004,.0000001,
     % 2*.000070,3*.002161,.009420,2*.000091,.000320,25*.024790/
      DATA A3/
     % 2*3.6245,2*2.0596,1.7263,2*1.066,1.989,2.0017,3.0685,3.688,
     % 3.5738,2*3.6245,2*5.0559,4*2.2226,3*1.5297,3.3104,
     % 2.9396,3*2.5839,3.3131,4*2.7538,3.5367,2*2.4451,3*1.3949,
     % 1.1041,2*2.6030,2.0236,25*0.96288/
      DATA A4/
     % 2*.00004,2*.00035,.00004,2*.000520,.000060,.000060,.000030,
     % .000020,.00001,2*.000040,2*.00024,4*.000500,
     % 3*.000030,.000070,.000010,3*.00028,.00001,4*.00005,.00018,
     % 2*.00003,3*.00004,.000360,2*.000044,.000340,25*.000040/
      DATA A5/
     % 2*1.0,2*0.24225,1.0,2*0.27298,1.0,0.91127,1.0000,1.0000,
     % 3*1.0000,2*.31553,4*0.06626,3*1.0000,0.57302,
     % 1.0,3*0.08385,0.1243,4*0.62086,.259,2*.95222,3*.71628,.15386,
     % 2*1.0,0.21288,25*1.0000/
C----------
C  COEFFICIENTS FOR DIAMETER ADJUSTMENT EQUATION.
C----------
      DATA D1/
     %  2*-.0174,2*.0,-.0043,2*.0,-.0112,-.0248,-.0093,-.0050,-.0043,
     %  2*.0,2*-.0115,.0,.0109,2*.0008,3*-.0102,-.0021, .0,3*.0014,
     %  -.0113,4*-.0079,.0015,2*.0026,3*.0,.0124,2*-.0132,-.0066,
     %  5*.0085,20*-.0046/
      DATA D2/
     %  2*.00065,2*-.00017,.00011,2*.0,.00040,.00162,.00042,0.0,
     %  .00022,2*.0,2*.00032,2*.00000,2*.00004,3*.00023,0.00010,
     %  .00007,3*.00002,.00066,4*.00039,-.00003,5*.00000,-.00022,
     %  2*.00079,0.00038,5*-.00026,20*.0/
      DATA D3/
     %  2*.069,2*.018,.029,2*.000,.030,.037,.048,-.004,.039,2*.0,
     %  2*.051,.0,-.106,2*-.017,3*.028,.001,-.037,3*-.024,-.002,4*.048,
     %  -.026,2*-.088,3*.000,-.079,2*.031,0.011,5*-.088,
     %  20*-.070/
C-----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C-----------
      CALL DBCHK (DEBUG,'DGF',3,ICYC)
      IF(DEBUG) WRITE(JOSTND,3)ICYC
    3 FORMAT(' ENTERING SUBROUTINE DGF  CYCLE =',I5)
C----------
C  STORE DIAMETERS FOR ITERATIVE PROCESSING
C----------
      DO 4 I=1,MAXTRE
      TEMD(I)=DIAM(I)
    4 CONTINUE
C----------
C  BEGIN SPECIES LOOP.  ASSIGN VARIABLES WHICH ARE SPECIES DEPENDENT
C----------
      DO 20 ISPC=1,MAXSP
      I1=ISCT(ISPC,1)
      IF(I1.EQ.0) GO TO 20
      I2=ISCT(ISPC,2)
      SI=SITEAR(ISPC)
C----------
C  BEGIN TREE LOOP WITHIN SPECIES ISPC.
C----------
      DO 10 I3=I1,I2
      I=IND1(I3)
      CR=FLOAT(ICR(I))/10.
C----------
C  ITERATE 10 TIMES, SINCE DG EQUATION IS AN ANNUAL BASIS
C----------
      DO 1000 ILOOP=1,10
      D=TEMD(I)
      WK2(I)=0.0
      IF(D .LE. 0.0) GO TO 10
C----------
C  COMPUTE POTENTIAL ANNUAL DIAMETER GROWTH (PADG)
C----------
      TRBA = D*D*0.0054542
      PADG = A1(ISPC)-A2(ISPC)*D**A3(ISPC)+A4(ISPC)*SI*CR*
     &         (D**A5(ISPC))
      IF (PADG .LT. 0.0) PADG = 0.0
      CALL BALMOD(ISPC,D,BA,RMSQD,GM,DEBUG)
C----------
C  COMPUTE ACTUAL DIAMETER GROWTH FOR ONE YEAR (DELD)
C----------
      DELD = PADG*GM
      IF (DEBUG)WRITE(JOSTND,*)' GM=',GM,' PADG=',PADG,' DELD=',DELD
C----------
C  COMPUTE DIAMETER ADJUSTMENT
C
C   FIRST SET MAXIMUM DBH TO USE IN THE ADJUSTMENT FUNCTION:
C    10 INCHES FOR BLACK SPRUCE AND NON-COMMERCIAL
C    20 INCHES FOR ALL OTHER SPECIES
C----------
      DA=D
      IF ((ISPC .EQ. 7) .OR. (ISPC .EQ. 31)) THEN
         IF (D .GT. 10.0) DA = 10.0
      ELSE
         IF (D .GT. 20.0) DA = 20.0
      ENDIF
      ADJ = D1(ISPC)*DA+D2(ISPC)*DA*DA+D3(ISPC)
      DELD = DELD + ADJ
      IF(DEBUG)WRITE(JOSTND,*)' ILOOP=',ILOOP,' ADJ=',ADJ,' DELD=',DELD
      IF (DELD .LE. 0.0) DELD = 0.000001
C      QTRBA=DELD+TRBA
C      QDBH=(QTRBA/.0054542)**.5
C      TEMD(I)=QDBH
      TEMD(I)=TEMD(I)+DELD
 1000 CONTINUE
C
      BARK=BRATIO(ISPC,TEMD(I),HT(I))
      DIAGR=(TEMD(I)-DIAM(I))*BARK
      IF(DEBUG)
     &WRITE(JOSTND,*)' I,TEMD,DIAM,BARK,DIAGR= ',
     &I,TEMD(I),DIAM(I),BARK,DIAGR
      IF(LDCOR2 .AND. COR2(ISPC) .GT. 0.0)DIAGR= DIAGR*COR2(ISPC)
      IF(DIAGR.LE. .0001) DIAGR=.0001
      IF(DEBUG)WRITE(JOSTND,*)' I,ISPC,COR2,DIAGR,LDCOR2= ',
     &I,ISPC,COR2(ISPC),DIAGR,LDCOR2
      DDS=DIAGR*(2.0*DIAM(I)*BARK+DIAGR)
      IF(DEBUG)WRITE(16,*)' I,DIAGR,DIAM(I),BARK,DDS= ',
     &I,DIAGR,DIAM(I),BARK,DDS
C---------
C     CALL PPDGF TO GET A MODIFICATION VALUE FOR DDS THAT ACCOUNTS
C     FOR THE DENSITY OF NEIGHBORING STANDS.
C
      X1=0.
      XPPDDS=0.
      CALL PPDGF (XPPDDS,X1,X1,X1,X1,X1,X1)
C
C---------
      IF (DDS .GT. 0.0) WK2(I)=ALOG(DDS)+COR(ISPC)+XPPDDS
   10 CONTINUE
C----------
C  END OF SPECIES LOOP.
C----------
   20 CONTINUE
      IF(DEBUG)WRITE(JOSTND,100)ICYC
  100 FORMAT(' LEAVING SUBROUTINE DGF  CYCLE =',I5)
      RETURN
      ENTRY DGCONS
      DO 120 ISPC=1,MAXSP
      DGCON(ISPC)=0.
      ATTEN(ISPC)=OBSERV(ISPC)
      SMCON(ISPC)=0.
  120 CONTINUE
      RETURN
      END

