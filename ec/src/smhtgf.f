      SUBROUTINE SMHTGF (MODE,ICYC,I,H,DTIME,HHT,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **SMHTGF--EC  DATE OF LAST REVISION:  03/24/08
C----------
C  THIS ROUTINE CALCULATES THE HEIGHT GROWTH OF SMALL TREES (D<3.0 IN).
C  WHEN CALLED FROM ESSUBH (MODE=0), DTIME= TOTAL TREE AGE.
C  IF CALLED FROM REGENT (MODE=1), DTIME= FINT AND THE  
C  RETURN VARIABLE (HHT) IS THE HEIGHT INCREMENT.
C  CALLED FROM **ESSUBH
C  CALLED FROM **REGENT
C
C  INPUT VARIABLES
C  I      - SPECIES SEQUENCE NUMBER
C  H      - BEGINING HEIGHT
C  SI     - SITE INDEX FOR SPECIES
C  MODE   - = 0 IF CALL IS FROM ESSUBH, = 1 IF CALL IS FROM REGENT 
C  DTIME  - DTIME=TOTAL SEEDLING AGE (MODE= 0), DTIME=FINT (MODE= 1)
C
C  RETURN VARIABLES
C  HHT    - HEIGHT GROWTH OVER TIME INCREMENT DTIME (REGENT)
C  HHT    - HEIGHT 5 YEARS INTO CYCLE, OR END OF CYCLE FINT<5 (ESSUBH)
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
      INCLUDE 'PLOT.F77'
C
COMMONS
C
      LOGICAL DEBUG
      INTEGER I,ICYC,JOSTND,MODE
      REAL    AGEPDT,C1,C2,C3,C4,EFFAGE,DTIME,H,HHT,SI,HHT1,HHT2
C----------
C  IF NO GROWTH IS TO BE CALCULATED ON ESTAB TREES, I.E. PLANTING
C  IS MORE THAN 5 YEARS INTO THE SIMULATION, THEN RETURN AND
C  LEAVE THE SMALL TREE GROWTH, DURING THE PLANTING CYCLE, TO
C  THE REGENT ROUTINE.
C----------
      IF(DTIME .LE. 0.0) GO TO 120
C
      SI= SITEAR(I)
C
      GO TO (10,20,30,40,50,60,70,80,90,100,110),I
C----------
C  HEIGHT OF TALLEST SUBSEQUENT SPECIES 1 (WP) **********
C----------
   10 CONTINUE
C----------
C  FOR NON-LINEAR HEIGHT GROWTH MODEL SOLVE FIRST FOR EFFECTIVE
C  AGE BASED ON INPUT HEIGHT.
C----------
      C1= 0.375045
      C2= 0.92503
      C3= -0.020796
      C4= 2.48811
C
      IF(MODE .EQ. 0) THEN
        EFFAGE= 0.
        AGEPDT= EFFAGE + DTIME
      ELSEIF(MODE .EQ. 1) THEN
        EFFAGE= ALOG( (1.0 - (C1/SI * H)**(1/C4))/C2 ) / C3
        AGEPDT= EFFAGE + DTIME
      ENDIF
C
      HHT1 = (SI/C1)*(1.0-C2*EXP(C3*EFFAGE))**C4
      HHT2 = (SI/C1)*(1.0-C2*EXP(C3*AGEPDT))**C4
      HHT= HHT2 - HHT1
      GO TO 120
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 2 (WL) **********
C----------
   20 CONTINUE
C
      HHT = ((-3.9725 + 0.50995*SI)/(28.1168 - 0.05661*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 3 (DF) **********
C----------
   30 CONTINUE
C
      HHT = ((2.0 + 0.420*SI)/(28.5 - 0.05*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 4 (SF) **********
C----------
   40 CONTINUE
C
      HHT = ((-0.6667 + 0.4333*SI)/(28.5 - 0.05*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 5 (RC) **********
C----------
   50 CONTINUE
      C1= 0.752842
      C2= 1.0
      C3= -0.0174
      C4= 1.4711
      IF(MODE .EQ. 0) THEN
        EFFAGE= 0.
        AGEPDT= EFFAGE + DTIME
      ELSEIF(MODE .EQ. 1) THEN
        EFFAGE= ALOG( (1.0 - (C1/SI * H)**(1/C4))/C2 ) / C3
        AGEPDT= EFFAGE + DTIME
      ENDIF
C
      HHT1 = (SI/C1)*(1.0-C2*EXP(C3*EFFAGE))**C4
      HHT2 = (SI/C1)*(1.0-C2*EXP(C3*AGEPDT))**C4
      HHT= HHT2 - HHT1
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 6 (GF) **********
C----------
   60 CONTINUE
C
      HHT = ((-1.0470 + 0.4220*SI)/(28.7739 - 0.0597*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 7 (LP) **********
C----------
   70 CONTINUE
C
      HHT = (0.3277 + 0.01296*SI)*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 8 (ES) **********
C----------
   80 CONTINUE
C
      HHT = ((-8.0 + 0.35*SI)/(53.72545 - 0.274509*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 9 (AF) **********
C----------
   90 CONTINUE
C
      HHT = ((6.0 + 0.14*SI)/(33.882 - 0.06588*SI))*DTIME
      GO TO 120
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 10 (PP) **********
C----------
  100 CONTINUE
C
      HHT = ((-1.0 + 0.32857*SI)/(28.0 - 0.042857*SI))*DTIME
      GO TO 120
C----------
C  HT OF TALLEST SUBS. SPECIES 11 (OT) **********
C  IN METERS CONVERTED TO FEET
C----------
  110 CONTINUE
C
      HHT = ((0.965758 + 0.082969*SI)/(55.249612 - 1.288852*SI))*DTIME
     &      *3.280833
  120 CONTINUE
      IF (DEBUG)
     & WRITE(JOSTND,*)' LEAVING SMHTGF, ICYC,I,HHT,DTIME,SI= ',
     &ICYC,I,HHT,DTIME,SI
C
      RETURN
      END
