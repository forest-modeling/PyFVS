      SUBROUTINE HTCALC (SINDX,ISPC,AG,HGUESS,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **HTCALC--EC   DATE OF LAST REVISION:  01/12/11
C----------
C THIS ROUTINE CALCULATES A POTENTIAL HT GIVEN AN SPECIES SITE AND AGE
C IT IS USED TO CAL POTHTG AND SITE.
C GO TO A DIFFERENT POTENTIAL HEIGHT CURVE DEPENDING ON THE SPECIES
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'VARCOM.F77'
C
C
COMMONS
C----------
      LOGICAL DEBUG
      INTEGER ISPC,JOSTND
      REAL HGUESS,AG,SINDX,B0,B1,B2,B3,B4,B5,B6,B7,B8,B9,B10,B11
      REAL B12,B13,X2,X3
C----------
C  SEE IF WE NEED TO DO SOME DEBUG.
C----------
      IF(DEBUG) WRITE(JOSTND,10)
   10 FORMAT(' ENTERING HTCALC')
C----------
C LOAD THE BETA COEFFICIENTS
C----------
      B0=BB0(ISPC)
      B1=BB1(ISPC)
      B2=BB2(ISPC)
      B3=BB3(ISPC)
      B4=BB4(ISPC)
      B5=BB5(ISPC)
      B6=BB6(ISPC)
      B7=BB7(ISPC)
      B8=BB8(ISPC)
      B9=BB9(ISPC)
      B10=BB10(ISPC)
      B11=BB11(ISPC)
      B12=BB12(ISPC)
      B13=BB13(ISPC)
C----------
C SPECIES ORDER:
C 1=WP  2=WL  3=DF  4=SF  5=RC  6=GF  7=LP  8=ES  9=AF 10=PP 11=OT
C----------
C
      SELECT CASE (ISPC)
C----------
C WHITE PINE USE BRICKELL EQUATIONS
C----------
      CASE(1)
        HGUESS=SINDX/(B0*(1.0-B1*EXP(B2*AG))**B3)
C----------
C WESTERN LARCH USE COCHRAN PNW 424
C----------
      CASE(2)
        HGUESS=4.5 + B1*AG + B2*AG*AG + B3*AG**3  +
     &         B4*AG**4 + (SINDX -4.5)*(B5 + B6*AG
     &         + B7*AG*AG + B8*AG**3)-
     &         B9*(B10 + B11*AG + B12*AG*AG + B13*AG**3)
C----------
C DOUGLAS FIR USE COCHRAN PNW 251
C----------
      CASE(3)
        IF(DEBUG)WRITE(JOSTND,30)B1,B2,B3,B4,B5,B6,B7,B8,AG,SINDX
   30   FORMAT(' IN HTCALC 30F B1-8,AB,SINDX =',/,1H ,10F13.8)
        HGUESS=4.5 + EXP(B1 + B2*ALOG(AG) + B3*(ALOG(AG))**4)
     &         + B4*(B5 + B6*(1.0 - EXP(B7*AG))**B8)
     &         + (SINDX - 4.5)*(B5 + B6*(1.0 - EXP(B7*AG)**B8))
C----------
C SILVER FIR, SURROGATE IS WHITE FIR USE COCHRAN PNW 252
C GRAND FIR ---- USE SILVER FIR
C----------
      CASE(4,6)
        X2= B0 + B1*ALOG(AG) + B2*(ALOG(AG))**4
     &      + B3*(ALOG(AG))**9 + B4*(ALOG(AG))**11
     &      + B5*(ALOG(AG))**18
        X3= B6 + B7*ALOG(AG) + B8*(ALOG(AG))**2
     &      + B9*(ALOG(AG))**7 + B10*(ALOG(AG))**16
     &      + B11*(ALOG(AG))**24
        HGUESS=EXP(X2) + B12*EXP(X3) + (SINDX - 4.5)*EXP(X3)
     &       + 4.5
        IF(DEBUG)WRITE(JOSTND,9500)X2,X3,SINDX,AG,HGUESS
 9500   FORMAT(' X2,X3,SINDX,AG',5E12.4)
C----------
C RED CEDAR ---- USE HEGYI, JELINEK, VISZLAI,
C & CARPENTER 1981 FOR SITE REFERENCE
C----------
      CASE(5)
        IF(DEBUG)WRITE(JOSTND,9501)ISPC,AG,SINDX,B0,B1,B2,B3,B4,B5
 9501   FORMAT(' HTCALC RED CEDAR ISPC,AG,SI,B0-B5= ',I5,8E10.4)
        HGUESS = B1*SINDX * ((1.0 - EXP(B2*AG))**B3)
C----------
C LODGEPOLE PINE USE ALEXANDER TACKLE AND DAHMS FOR CCF 100
C----------
      CASE(7)
        HGUESS = B0 + B1*AG + B2*AG*AG + B3*  0.0*SINDX
     &    + B4*AG*SINDX + B5*AG*AG*SINDX
        IF(DEBUG)WRITE(JOSTND,9701)AG,SINDX,HGUESS
 9701   FORMAT(' HTCALC LPP AG SI HGUESS ',3F13.8)
C----------
C ENGLEMANN SPRUCE USE ALEXANDER
C----------
      CASE(8)
        HGUESS = 4.5+((B0*SINDX**B1)*(1.0-EXP(-B2*AG))**(B3*SINDX**B4))
        IF(DEBUG)WRITE(JOSTND,*)B0,B1,B2,B3,B4,AG,SINDX,HGUESS
C----------
C SUB ALPINE FIR AND NOBLE FIR USE JOHNSON'S EQUIV OF DEMARS
C----------
      CASE(9)
        HGUESS=SINDX*(B0 + B1*AG + B2*AG*AG)
C----------
C PONDEROSA PINE USE BARRETT
C----------
      CASE(10)
        IF(DEBUG)WRITE(JOSTND,20)B0,B1,AG,B2,B3,B4,B5,B6,B7,SINDX
   20   FORMAT(' HTCALC 20F B0-1,AG,B2-7,SINDX = ',/,1H ,10F13.8)
        HGUESS =( B0*(1.0 -EXP(B1*AG))**B2)
     &           - ((B3 + B4*(1.0-EXP(B5*AG))**B6)*B7)
     &           + ((B3 + B4*(1.0 - EXP(B5*AG))**B6)*(SINDX - 4.5))
     &           + 4.5
C----------
C MISC SPECIES. USE MEANS (UNPUBL) METRIC
C SINCE MEANS USES HEIGHT ABOVE B.H., ADD IN 1.37 METERS BEFORE
C CONVERTING TO ENGLISH UNITS.
C----------
      CASE(11)
        IF(DEBUG)WRITE(JOSTND,9502)ISPC,AG,SINDX,B0,B1,B2,B4,B5
 9502   FORMAT(' HTCALC HEMLOC SP AG,SI B S ',I5,7E12.4)
        HGUESS = (B0 + B1*SINDX)*(1.0 - EXP(B2*SQRT(SINDX)*AG))
     &           ** (B4 + B5/SINDX)
        HGUESS=(HGUESS+1.37)*3.281
C
      END SELECT
C
      RETURN
      END
