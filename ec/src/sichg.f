      SUBROUTINE SICHG(ISISP,SSITE,SIAGE)
      IMPLICIT NONE
C----------
C  **SICHG--EC  DATE OF LAST REVISION:  03/24/08
C----------
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
COMMONS
C  THIS ROUTINE TAKES THE SITE FROM THE STDINFO CARD AND/OR
C  THE DEFAULT VALUE FROM SITSET, AND DETERMINES IF IT IS A 
C  BHA OR TTA SITE CURVE.  THE RETURN VARIABLE SIAGE REPRESENTS THE
C  SPECIES BASE AGE + AGE2BH IF THE SITE SPECIES SITE CURVE
C  IS A TTA CURVE AND SPECIES CURVE A BHA CURVE, AND SO ON.
C  THEN, THE SITE FOR EACH SPECIES MAY BE ESTIMATED USING
C  THE SITE SPECIES CURVE IN HTCALC.
C  DATA STATEMENTS FOR A AND B CONTAIN COEFFICIENTS FOR A LINEAR
C  RELATION OF AGE TO BREAST HEIGHT AS A FUNCTION OF SITE INDEX.
C  THE LINEAR RELATION IS AN ESTIMATE FROM THE LOWER END POINT OF
C  A HEIGHT GROWTH CURVE TO THE ORIGIN (0 AGE; 0 TOT TREE HT.)
C  THESE COEFFICIENTS SHOULD BE COMPATIBLE WITH SIMILAR VALUES USED
C  IN THE SMHTGF SUBROUTINE.
C
C  CALLED FROM **SITSET
C----------
      INTEGER DIFF,REFAGE(MAXSP)
      CHARACTER*1 ISILOC,REFLOC(MAXSP)
      REAL A(MAXSP),B(MAXSP),
     &          SIAGE(MAXSP)
      REAL SSITE,AGE2BH
      INTEGER ISISP,I
C
      DATA A /18.43316,  8.11680,  8.50000,  9.01840, 24.62857,
     &         9.01840,  7.78447, 33.72545, 13.88200,  8.00000,
     &        45.24960/
      DATA B /-0.13399, -0.05661, -0.05000, -0.05700, -0.20465,
     &        -0.05700, -0.05013, -0.274509, -0.06590, -0.04286,
     &        -1.28885/ 
      DATA REFLOC/
     &  'T',3*'B','T','B','T',4*'B'/
      DATA REFAGE/
     & 4*50,100,50  ,4*100,100/
C----------
C ISILOC IS THE PLACE THE AGE FOR THE SITE SPECIES.
C----------
      ISILOC = REFLOC(ISISP)
      DO 100 I = 1,MAXSP
C----------
C  SET UP THE ARRAY TO TELL WHETHER YOU NEED TO SLIDE UP OR DOWN THE SIT
C  LINE TO ADJUST FOR TOTAL AGE OR BREAST HIGH AGE
C----------
      IF(ISILOC .EQ. 'T' .AND. REFLOC(I) .EQ. 'B')DIFF=-1
      IF(ISILOC .EQ. REFLOC(I))DIFF=0
      IF(ISILOC .EQ. 'B' .AND. REFLOC(I) .EQ. 'T')DIFF=1
      AGE2BH=0.0
      IF(DIFF .LT. 0 .OR. DIFF .GT. 0)THEN
        AGE2BH=A(I) + B(I)*SSITE
        IF((ISISP .NE. 11) .AND. (I .EQ. 11))
     &  AGE2BH=A(I) + B(I)*(SSITE/3.281)
        IF((ISISP .EQ. 11) .AND. (I .NE. 11))
     &  AGE2BH=A(I) + B(I)*(SSITE*3.281)
      END IF
      SIAGE(I) = REFAGE(I) + AGE2BH*DIFF
C
  100 CONTINUE
      RETURN
      END
