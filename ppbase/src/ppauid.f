      SUBROUTINE PPAUID (LAUT,CAUID)
      IMPLICIT NONE
C----------
C  **PPAUID  DATE OF LAST REVISION:  07/31/08
C----------
C
C     CREATE THE MANAGEMENT ID'S FOR THE AUTOID OPTION IN THE PPE.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--SEPT 1987
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      CHARACTER FRMT*6,CAUID*4
      LOGICAL LAUT
      INTEGER I,ILOC,IVAL,NEWVAL,NEWLOC
C
C     IF AUTO ID PROCESSING IS TURNED OFF, THEN BRANCH TO RETURN.
C
      LAUT=NAUTID.NE.0
      IF (NAUTID.EQ.0) GOTO 50
      IF (NAUTID.LE.1) THEN
         NAUTID=2
         GOTO 40
      ENDIF
C
C     FIND THE NUMERIC PART OF THE AUTO ID.
C
    5 CONTINUE
      DO 10 I=4,1,-1
      IF (INDEX('0123456789',CAUTID(I:I)).GT.0) GOTO 10
      ILOC=4-I
      IF (ILOC.LT.1) THEN
         CAUTID(4:4)='0'
         GOTO 40
      ENDIF
      GOTO 20
   10 CONTINUE
      ILOC=4
   20 CONTINUE
C
C     CREATE THE FORMAT TO READ THE NUMBER FROM THE STRING...AND READ
C     THE NUMERIC PART OF THE STRING.
C
      WRITE (FRMT,'(''(I'',I1,'')  '')') ILOC
      READ  (CAUTID(5-ILOC:),FRMT) IVAL
C
C     INCREMENT IVAL...FORMING NEWVAL.
C
      NEWVAL=IVAL+1
C
C     IF THE ORDER OF MAGNITUDE HAS CHANGED, ADJUST THE OUTPUT FORMAT.
C
      NEWLOC=ILOC
      IF (NEWVAL.GE.10000) THEN
         NEWVAL=0
      ELSEIF (NEWVAL.GE.1000) THEN
         NEWLOC=4
      ELSEIF (NEWVAL.GE.100) THEN
         NEWLOC=3
      ELSEIF (NEWVAL.GE.10) THEN
         NEWLOC=2
      ENDIF
C
C     WRITE THE NEW NUMBER INTO THE STRING USING A NEW FORMAT.
C
      WRITE (FRMT,'(''(I'',I1,''.'',I1,'')'')') NEWLOC,NEWLOC
      WRITE (CAUTID(5-NEWLOC:4),FRMT) NEWVAL
   40 CONTINUE
      CAUID=CAUTID
   50 CONTINUE
      RETURN
      END
