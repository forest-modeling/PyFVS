      SUBROUTINE HVFXLB
      IMPLICIT NONE
C----------
C  **HVFXLB DATE OF LAST REVISION:  07/31/08              
C----------
C
C     CALLED FROM PPMAIN.  WHEN POLICY HIERARCHIES ARE BEING
C     SIMULATED, THE MSPLABEL(S) FOR THE POLICIES THAT
C     THE STAND WAS "NOT SELECTED" UNDER MUST BE ADDED BACK INTO
C     THE STAND LABEL SET.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - DEC 1989
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
COMMONS
C
C
C
      INTEGER IHV,LENWK1,IRC
C
C
      IF (LHVDEB) THEN
         WRITE (JOPPRT,'(/'' IN HVFXLB.1 LHIER= '',L2/T14,
     >         ''SLSET='',A)') LHIER,SLSET(1:LENSLS)
         WRITE (JOPPRT,'(T14,''IHVSTA='',15I3)')
     >         (IHVSTA(ISTND,IHV),IHV=1,IXHRVP)
      ENDIF
C
C     NOTHING TO DO IF HIERARCHIES ARE NOT BEING USED.
C
      IF (.NOT.LHIER) RETURN
C
C     IF THE STAND WAS NOT SELECTED UNDER THE FIRST POLICY OF THE
C     HIERARCHY, THEN ADD THE FIRST MSPLABEL BACK INTO THE SPLABEL.
C
      IF (IHVSTA(ISTND,1).EQ.2) THEN
         WKSTR1=SLSET
         LENWK1=LENSLS
         WKHPLB=HVPLAB(1)(1:LNHPLB(1,2))
         CALL LBUNIN (LENWK1,WKSTR1,LNHPLB(1,2),WKHPLB,LENSLS,
     >                SLSET,IRC)
         IHVSTA(ISTND,1)=1
         IF (IRC.GT.0) THEN
            LHVDEB=.TRUE.
            WRITE (JOPPRT,'( '' IN HVFXLB, IRC='',I2)') IRC
            GOTO 20
         ENDIF
C
C     IF "SELECTED" UNDER THE FIRST POLICY, THEN ADD IN THE
C     LABELS FROM THE OTHERS, IF THEY STILL APPLY (STATUS CODES=1).
C
      ELSEIF (IHVSTA(ISTND,1).EQ.3) THEN
         DO 10 IHV=2,IXHRVP
         IF (IHVSTA(ISTND,IHV).EQ.1) THEN
            WKSTR1=SLSET
            LENWK1=LENSLS
            WKHPLB=HVPLAB(IHV)(1:LNHPLB(IHV,2))
            CALL LBUNIN (LENWK1,WKSTR1,LNHPLB(IHV,2),WKHPLB,LENSLS,
     >                   SLSET,IRC)
            IF (IRC.GT.0) THEN
               LHVDEB=.TRUE.
               WRITE (JOPPRT,'( '' IN HVFXLB, IHV,IRC='',I2)') IHV,IRC
            ENDIF
         ENDIF
   10    CONTINUE
         IHVSTA(ISTND,1)=1
      ENDIF
   20 CONTINUE
C
C     INSURE THAT THE PPE'S STAND LABEL SET TABELS CONTAIN THE
C     NEW STAND LABEL SET.
C
      SLSETS(ISTND)=SLSET
      LNSETS(ISTND)=LENSLS
C
      IF (LHVDEB) THEN
         WRITE (JOPPRT,'( '' IN HVFXLB.2 LHIER= '',L2/T14,
     >         ''SLSET='',A)') LHIER,SLSET(1:LENSLS)
         WRITE (JOPPRT,'(T14,''IHVSTA='',15I3)')
     >         (IHVSTA(ISTND,IHV),IHV=1,IXHRVP)
      ENDIF
      RETURN
      END
