      SUBROUTINE GPNEW (KODE,IDATE,IACTK,NPRMS,PRMS)
      IMPLICIT NONE
C----------
C  **GPNEW   DATE OF LAST REVISION:  07/31/08
C----------
C
C     CREATE A NEW GLOBAL ACTIVITY DURING STAND INPUT PHASE.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION TO PROGNOSIS.
C     NL CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1987
C
C     PARAMETERS:
C
C     KODE  = RETURN CODE, 0 IF ALL WENT WELL.
C     IDATE = DATE TO SCHEDULE GLOBAL ACTIVITY.
C     IACTK = GLOBAL ACTIVITY TO SCHEDULE.
C     NPRMS = NUMBER OF PARAMETERS.
C     PRMS  = ARRAY OF PARAMETERS.
C
COMMONS
C
C
      INCLUDE 'PPGPCM.F77'
C
C
COMMONS
C
      INTEGER IACTK,IDATE,KODE,NPRMS,I
      REAL PRMS (NPRMS)
C
      KODE=0
C
C     IF THERE IS NOT ENOUGH ROOM FOR THE NEW ACTIVITIY, RETURN
C
      IF (MIMGL.LT.MGPMAX) THEN
         MIMGL=MIMGL+1
      ELSE
         KODE=1
         CALL PPERRP (.TRUE.,2)
         GOTO 200
      ENDIF
C
C     IF THERE IS NOT ENOUGH ROOM TO STORE THE PARMS, EXIT.
C
      IF (MLPARM+NPRMS.GT.MXGPRM) THEN
         KODE=3
         CALL PPERRP (.TRUE.,2)
         GOTO 200
      ENDIF
C
C     GLOBAL ACTIVITIES MUST USE A VALID IDATE
C
      IF (IDATE.LE.0) THEN
         KODE=2
         CALL PPERRP (.TRUE.,3)
         GOTO 200
      ENDIF
C
C     STORE THE DATE, THE SEQUENCE NUMBER, SET THE SWITCH FOR INCOMMING
C     STANDS TO BE PART OF THIS GLOBAL OPTION, STORE THE ACTIVITY CODE
C     AND STORE THE PARAMETERS.
C
      MIDATE(MIMGL)=IDATE
      IGPSEQ(MIMGL)=MIMGL
      LMOPEN(MIMGL)=.TRUE.
      MIACT(1,MIMGL)=IACTK
C
C     STORE THE PARAMETERS FOR THE ACTIVITY.
C
      IF (NPRMS.LE.0) THEN
         MIACT(2,MIMGL)=0
         MIACT(3,MIMGL)=0
      ELSE
         MIACT(2,MIMGL)=MLPARM+1
         MIACT(3,MIMGL)=MLPARM+NPRMS
         DO 40 I=1,NPRMS
         MLPARM=MLPARM+1
         GPARMS(MLPARM)=PRMS(I)
   40    CONTINUE
      ENDIF
C
C     SET UP A LINKED LIST BY INITIALIZING THE
C     BEGINNING AND ENDING POINTERS TO NULL.
C
      CALL LLSETP (MIACT(4,MIMGL),MIACT(5,MIMGL))
C
C     SET THE ACTIVITY STATUS CODE
C
      MIACT(6,MIMGL)=0
  200 CONTINUE
      RETURN
      END
