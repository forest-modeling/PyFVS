      SUBROUTINE SPRDRD (SCDIST,INPUT,NRECS,IRC)
      IMPLICIT NONE
C----------
C  **SPRDRD--PPBASE   DATE OF LAST REVISION:  07/31/08
C----------
C     READS THE ROAD DISTANCE DATA.
C
C     SCDIST= SCALING OF THE AREA DATA.
C     INPUT = INPUT DATA SET REFERENCE NUMBER
C     NRECS = INPUT RECORD COUNT
C     IRC   = RETURN CODE: 0=OK, 1=ACTUAL END-OF-FILE WAS REACHED,
C             2=TOO MANY INPUT RECORDS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPSPRD.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      INTEGER IRC,NRECS,INPUT,IP
      REAL RD,SCDIS,SCDIST
      CHARACTER ID*26,CA*10
C
C     CHECK THE SCALING VALUES
C
      SCDIS=SCDIST
      IF (SCDIST.LE.0.0) SCDIS=1.0
C
C     IF STANDS ARE ALREADY LOADED, ONLY SAVE THE
C     ROAD ACCESS RECORDS FOR LOADED STANDS.  FIRST
C     CREATE AN INDEX SORT FOR FAST SEARCHING.
C
      IF (NOSTND.GT.0) CALL C26SRT (NOSTND,STDIDS,IRDORD,.TRUE.)
C
C     READ THE DATA...COUNT THE INPUT RECORDS.
C
      NRDS=0
   10 CONTINUE
      READ (INPUT,'(A26,T30,F10.0,T30,A10)',END=30) ID,RD,CA
      IF (INDEX(ID,'-999').GT.0) GOTO 20
      IF (ID.EQ.' ') GOTO 10
C
C     IF THE STANDS ARE LOADED, SAVE MATCHING ID'S ONLY
C
      IF (NOSTND.GT.0) THEN
         CALL C26BSR(NOSTND,STDIDS,IRDORD,ID,IP)
         IF (IP.EQ.0) GOTO 10
      ENDIF
      NRDS=NRDS+1
      IF (NRDS.GT.MXSTND) THEN
         IRC=2
         GOTO 40
      ENDIF
      CRDID(NRDS)=ID
      IF (CA.NE.' ') THEN
         RDDIST(NRDS)=RD*SCDIS
      ELSE
         RDDIST(NRDS)=-99999.0
      ENDIF
      GOTO 10
   20 CONTINUE
      IRC=0
      GOTO 40
   30 CONTINUE
      IRC=1
   40 CONTINUE
C
C     SORT THE INPUT LIST ON THE IDS.
C
      CALL C26SRT (NRDS,CRDID,IRDORD,.TRUE.)
      NRECS=NRDS
      RETURN
      END
