      SUBROUTINE STASH (BUFFER,ILIMIT)
      IMPLICIT NONE
C----------
C  **STASH   DATE OF LAST REVISION:  07/31/08
C----------
C
C     STASH A BUFFER FULL OF INFO IN DIRECT ACCESS FILE IDAIO.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     P.W. THOMAS--FORESTRY SCIENCES LAB, MOSCOW, ID--JULY 1982
C
C     CALLED FROM BFWRIT - WRITE A VARIABLE TO DIRECT ACCESS FILE IDAIO.
C
C     BUFFER= BUFFER FULL OF DATA TO BE STASHED IN DIRECT ACCESS FILE.
C     ILIMIT= POINTER TO LAST WORD IN BUFFER.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
C
      REAL BUFFER (ILIMIT)
      INTEGER ILIMIT,ISUB,I
      IF (PDEBUG) WRITE (JOPPRT,20) IWTREC,ILIMIT
   20 FORMAT (' IN STASH:  IWTREC= ',I5,' ILIMIT= ',I6)
C
C     FIND OUT WHICH ACTUAL RECORD AND FILE WE ARE GOING TO USE.
C
      ISUB=0
      DO 10 I=1,NDAFL
      IF (IWTREC.LE.NDACUM(I)) THEN
         WRITE (IDAIO(I),REC=IWTREC-ISUB) BUFFER
         IOSTRC=IWTREC
         CALL SPASGN (IWTREC)
         INEXR(IOSTRC)=IWTREC
         RETURN
      ELSE
         ISUB=NDACUM(I)
      ENDIF
   10 CONTINUE
C
C     IF THE LOOP EXITS, THEN FILE SPACE DOES NOT EXIST.
C
      CALL PPERRP (.FALSE.,1)
      RETURN
      END
