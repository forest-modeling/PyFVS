      SUBROUTINE PPEOPN
      IMPLICIT NONE
C----------
C  **PPEOPN--PPBASE   DATE OF LAST REVISION:  07/31/08
C----------
C
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
C     THIS ROUTINE OPENS THE PPE FILES.
C     SUBROUTINE MYOPEN CONTAINS THE MACHINE DEPENDENT CODE.
C
C     TO PROMPT FOR FILE NAMES, SET LPT TRUE,
C     IF PROMPTS ARE NOT WANTED, SET LPT FALSE.
C
      INTEGER I,MEGS,KODE
      CHARACTER*40 CNAME
      LOGICAL LPT
      DATA LPT/.TRUE./
C
C     ALL-DATA DIRECT ACCESS FILE.
C
      IF (LPT) THEN
         WRITE (*,'('' ENTER ALL-DATA DIRECT ACCESS FILE NAME ('',
     >                     I2.2,''): '')') IDAIO(1)
         READ (*,'(A)',END=100) CNAME
C
C        IF THE FIRST 2 CHARS OF THE NAME ARE EQUAL SIGNS, THEN
C        THE ALTERNATE FILE SPECIFICATION SYSTEM IS USED THAT ALLOWS
C        FOR UP TO 'MXDAFL' FILES OF VARIOUS LENGTHS TO BE SPECIFIED.
C
         IF (CNAME(1:2).EQ.'==') THEN
            READ (CNAME(3:),'(I2)') NDAFL
            IF (NDAFL.LT.0) NDAFL=1
            IF (NDAFL.GT.MXDAFL) STOP 'TOO MANY FILES REQUESTED.'
C
C           LOOP OVER THE NUMBER OF FILES.  OPEN THE FILES AND
C           STORE THE MAX FILE SIZE IN NDACUM.
C
            DO 10 I=1,NDAFL
            READ (*,'(I4,1X,A)',END=100) MEGS,CNAME
            CALL MYOPEN (IDAIO(I),CNAME,1,IRECLN*4,0,2,2,0,KODE)
            IF (KODE.GT.0) WRITE (*,'('' OPEN FAILED FOR '',A)') CNAME
C
C           CONVERT THE FILE SIZES (IN MEGS) TO RECORD COUNTS.
C
            NDACUM(I)=(MEGS*1048576)/(IRECLN*4)
   10       CONTINUE
C
C           SET UP THE CUMULATIVE ARRAYS.
C
            IF (NDAFL.GE.2) THEN
               DO 30 I=2,NDAFL
               NDACUM(I)=NDACUM(I)+NDACUM(I-1)
   30          CONTINUE
            ENDIF
         ELSE
            CALL MYOPEN (IDAIO(1),CNAME,1,IRECLN*4,0,2,2,0,KODE)
            IF (KODE.GT.0) WRITE (*,'('' OPEN FAILED FOR '',A)') CNAME
         ENDIF
      ELSE
         CNAME=' '
         CALL MYOPEN (IDAIO(1),CNAME,1,IRECLN*4,0,2,2,0,KODE)
         IF (KODE.GT.0) WRITE (*,'('' OPEN FAILED FOR '',A)') CNAME
      ENDIF
C
C     PRINT DATA FILE.
C
      IF (LPT) THEN
         WRITE (*,'('' ENTER PRINT-DATA DIRECT ACCESS FILE NAME ('',
     >                 I2.2,''): '')') IDAPRT
         READ (*,'(A)',END=100) CNAME
      ELSE
         CNAME=' '
      ENDIF
C
C     IF FILE NAME IS NULL (TYPICAL UNIX STUFF), THEN DON'T
C     PLACE A LIMIT ON THE NUMBER OF RECORDS OUTPUT.
C
      IF (INDEX(CNAME,'/dev/null ').EQ.1) NPTREC=0
      CALL MYOPEN (IDAPRT,CNAME,1,448,0,2,2,0,KODE)
      IF (KODE.GT.0) WRITE (*,'('' OPEN FAILED FOR '',A)') CNAME
C
C     HISTORY FILE.
C
      IF (LPT) THEN
         WRITE (*,
     >   '('' ENTER HISTORY FILE NAME ('',I2.2,''): '')') JOHIST
         READ (*,'(A)',END=100) CNAME
      ELSE
         CNAME=' '
      ENDIF
      CALL MYOPEN (JOHIST,CNAME,1,40,0,1,1,0,KODE)
      IF (KODE.GT.0) WRITE (*,'('' OPEN FAILED FOR '',A)') CNAME
  100 CONTINUE
      RETURN
      END
