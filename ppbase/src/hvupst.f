      SUBROUTINE HVUPST (JOPPRT,IORGST,INEWST)
      IMPLICIT NONE
C----------
C  **HVUPST DATE OF LAST REVISION:  07/31/08              
C----------
C
C     CALLED FROM PPEVMI.  COPY THE POLICY STATUS CODES FROM THE
C     ORIGINIAL STAND (IORGST) TO THE NEW REPLICATE STAND (INEWST)
C     AND MODIFY THEM BASED ON THE NEW STAND LABEL SET AND THE
C     MULTISTAND POLICY LABEL.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
COMMONS
C
      LOGICAL LBMEMR
      INTEGER INEWST,IORGST,JOPPRT,IHV,LENWKH
C
      IF (LHVDEB) WRITE (JOPPRT,'(/'' IN HVUPST, LHIER= '',L2,
     >    '' IORGST='',I5,'' INEWST='',I5)') LHIER,IORGST,INEWST
      IF (LHVDEB) WRITE (JOPPRT,'('' IN HVUPST.1 IHVSTA='',15I3)')
     >   (IHVSTA(IORGST,IHV),IHV=1,IXHRVP)
C
C     LOOP OVER POLICY STATUS CODES.
C
      DO 10 IHV=1,IXHRVP
      IF (IHVTAB(IHV,1).EQ.0) THEN
         IHVSTA(INEWST,IHV)=0
      ELSE
         IHVSTA(INEWST,IHV)=IHVSTA(IORGST,IHV)
C
C        IF PROCESSING HIERARCHIES, DO NOT RESET NOR TEST ON THE
C        FIRST POLICY STATUS CODE.  SEE HVFXLB.
C
         IF (.NOT.LHIER .OR. IHV.GT.1) THEN
C
C           PROCESS STATUS CODES THAT ARE 1 OR GREATER
C
            IF (IHVSTA(INEWST,IHV).GE.1) THEN
C
C              IF THE POLICY LABEL (UP TO THE PERIOD) IS AN
C              ELEMENT OF THE STAND LABEL SET, THEN:
C
               LENWKH=LNHPLB(IUNLAB(IHV),2)
               WKHPLB=HVPLAB(IUNLAB(IHV))(1:LENWKH)
               IF (LBMEMR(LENWKH,WKHPLB,LENSLS,SLSET)) THEN
C
C                 SET THE POLICY STATUS CODE FOR THE REP TO 1
C                 WHICH MEANS THE REP DOES APPLY TO THE POLICY.
C
                  IHVSTA(INEWST,IHV)=1
               ELSE
C
C                 SET THE POLICY STATUS CODE TO 0...WHICH
C                 MEANS THE REP DOES NOT APPLY TO THE POLICY.
C
                  IHVSTA(INEWST,IHV)=0
               ENDIF
            ELSE
C
C              INSURE THAT THE POLICY STATUS CODE IS 0.
C
               IHVSTA(INEWST,IHV)=0
            ENDIF
         ENDIF
      ENDIF
   10 CONTINUE
      IF (LHVDEB) WRITE (JOPPRT,'('' IN HVUPST.2 IHVSTA='',15I3)')
     >   (IHVSTA(INEWST,IHV),IHV=1,IXHRVP)
      RETURN
      END
