      SUBROUTINE SCHED
      IMPLICIT NONE
C----------
C  **SCHED--PPBASE   DATE OF LAST REVISION:  07/31/08
C----------
C
C     INSURES THAT THE CURRENT STAND'S CYCLING CONTROL VARIABLES (IY,
C     NCYC, & ICYC) ARE DEFINED SUCH THAT:
C
C        IN ENTRY SCHED - THE PROJECTION OF THE STAND INCLUDES THE
C                         MASTER CYCLING, AND
C       IN ENTRY SCHED2 - THE INTERMEDIATE CYCLING OF THE STAND
C                         ENDS CONCURRENTLY WITH THE END
C                         OF THE PRESENT MASTER CYCLE.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--APRIL 1982
C
C     CALLED FROM:
C
C       PPMAIN - MULTI-STAND PROGNOSIS MODEL (PPE) MAIN PROGRAM.
C       INSTND - INPUT AND INITIATE ONE PPE STAND.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C
C
      INTEGER NC2,IC2,MC2,I,IM2,NYRS,ISPOT,MYR,IDEL
C
C     NC2  = POINTS TO THE LAST USER DEFINED MEMBER OF IY
C     IC2  = INCREMENTS THRU IY
C     MC2  = POINTS TO THE LAST USER DEFINED MEMBER OF MIY
C     IM2  = INCREMENTS THRU MIY
C
      INCYC=0
      NC2=NCYC+1
      IC2=1
      MC2=MNCYC+1
C
      IF (PDEBUG) THEN
         WRITE (JOPPRT,5) MNCYC,NCYC,MICYC,INCYC,ICYC
    5    FORMAT(/' IN SCHED: MNCYC=',I3,'; NCYC=',I3,'; MICYC=',
     >         I3,'; INCYC= ',I3,'; ICYC= ',I3)
         WRITE (JOPPRT,6) (MIY(I),I=1,MC2)
    6    FORMAT (' MIY=',20I5)
         WRITE (JOPPRT,7) ( IY(I),I=1,NC2)
    7    FORMAT ('  IY=',20I5)
      ENDIF
C
C     DO FOR EACH MASTER CYCLE YEAR.
C
      DO 50 IM2=1,MC2
C
C     INCREMENT IC2
C
   10 CONTINUE
      IC2=IC2+1
C
C     IF IC2 GREATER THAN MAXCY1, THEN EXIT VIA ERROR ROUTINE.
C
      IF (IC2.GT.MAXCY1) CALL PPERRP (.FALSE.,6)
C
C     COMPUTE THE NUMBER OF YEARS BETWEEN THE LAST CYCLE AND THE MASTER
C     CYCLE.
C
   15 CONTINUE
      NYRS=MIY(IM2)-IY(IC2-1)
C
C     IF THE NUMBER OF YEARS IS ZERO, GO ON TO THE NEXT MASTER CYCLE.
C
      IF (NYRS .EQ. 0) GOTO 50
C
C     IF THE NUMBER OF YEARS IS LESS THAN ZERO, THEN DECREMENT IC2
C     AND BRANCH BACK TO RECOMPUTE NYRS.
C
      IF (NYRS.LT.0) THEN
         IC2=IC2-1
         IF (IC2.LT.2) STOP 'ERROR IN SCHED'
         GOTO 15
      ENDIF
C
C     IF IC2 GREATER THAN THE NUMBER OF USER DEFINED CYCLE YEARS; THEN:
C     ADD A CYCLE.
C
      IF (IC2.LE.NC2) GOTO 40
C
C     IF NYRS IS OVER 16 YEARS, ADD A 10-YR CYCLE (IN THE NEXT PASS A
C     7-YEAR OR LONGER CYCLE WILL BE ADDED).
C
      IF (NYRS.LE.16) GOTO 20
      NYRS=10
      GOTO 30
   20 CONTINUE
C
C     IF NYRS IS LESS THAN OR EQUAL TO 12, ADD A NYRS-LONG CYCLE.
C
      IF (NYRS.LE.12) GOTO 30
C
C     DIVIDE NYRS BY 2 (IN THE NEXT PASS THE REMAINDER WILL BE ADDED).
C
      NYRS=NYRS/2
   30 CONTINUE
      IY(IC2)=IY(IC2-1)+NYRS
   40 CONTINUE
C
C     IF THE CURRENT STAND CYLE IS EQUAL TO THE CURRENT MASTER CYCLE;
C     THEN: LOOP TO NEXT CYCLES.
C
      IF (IY(IC2).EQ.MIY(IM2)) GOTO 50
C
C     IF THE CURRENT STAND CYCLE IS LESS THAN THE CURRENT MASTER CYCLE;
C     THEN GO INCREMENT IC2
C
      IF (IY(IC2).LT.MIY(IM2)) GOTO 10
C
C     AT THIS POINT, THE CURRENT STAND CYCLE IS GREATER THAN THE MASTER
C     CYCLE; THEN: INSERT A CYCLE WHICH IS COINCIDENT WITH THE MASTER
C     CYCLE.
C
      CALL INSCYC (IC2-1,NYRS,ISPOT,.FALSE.,.FALSE.,JOPPRT,PDEBUG)
C
C     ADJUST UPWARD THE NUMBER OF CYCLES DEFINED BY THE USER (NOTE
C     THAT IT DOESN'T MATTER IF THE USER HASN'T DEFINED ANY CYCLES).
C
      NC2=NC2+1
   50 CONTINUE
C
C     SET THE NUMBER OF CYCLES.
C
      MYR=MIY(MC2)
      DO 60 IC2=2,MAXCY1
      IF (IY(IC2).EQ.MYR) GOTO 70
   60 CONTINUE
      IC2=MAXCY1
   70 CONTINUE
      NCYC=IC2-1
C
C     BRANCH TO DEFINE THE END OF THE INITIAL CYCLING
C
      MYR=MIY(1)
      GOTO 100
C
C
      ENTRY SCHED2
C
C     INSURE THE STAND'S CURRENT CYCLING ENDS ON THE CURRENT
C     MASTER CYCLE.
C
      MYR=MIY(MICYC)
      CALL INSCYC(ICYC,MYR-IY(ICYC),ISPOT,.FALSE.,.FALSE.,JOPPRT,PDEBUG)
C
C     SET INCYC SUCH THAT IT POINTS END OF THE STAND'S CYCLE WHICH
C     IS CONCURRENT WITH THE END OF THE PRESENT MASTER CYCLE.
C
      IF (ISPOT .GT. 0) GO TO 120
  100 CONTINUE
      DO 110 I=ICYC,MAXCYC
      ISPOT=I
      IF (IY(I) .EQ. MYR) GO TO 120
  110 CONTINUE
  120 CONTINUE
      INCYC=ISPOT-1
C
C     IF THE MASTER CYCLE IS DESIGNATED AS A MODE 2 CYCLE (ALLOWING
C     FOR WITHIN CYCLE STAND INTERACTIONS), THEN INSURE
C     THERE ARE NO STAND CYCLES WITHIN THE MASTER CYCLE.
C
      IF (IPMODI.LE.1) GOTO 140
      IDEL=ISPOT-ICYC-1
      IF (IDEL.LE.0) GOTO 140
      NCYC=NCYC-IDEL
      DO 130 I=ICYC,NCYC
      IY(I+1)=IY(I+IDEL+1)
  130 CONTINUE
C
C     WRITE MSG INDECATING THAT CYCLE(S) WAS(WERE) DELETED.
C
      WRITE (JOPPRT,135) IDEL,CISNUM(ISTND),NPLT,MGMID,MICYC,
     >                   MIY(MICYC-1),MIY(MICYC)
  135 FORMAT (/' NOTE:',I3,' CYCLE(S) WAS(WERE) DELETED FROM INTERNAL',
     >       ' STAND NUMBER ',A11,'; STAND ID: ',A26,'  MGMTID: ',A4/
     >        T9,'MASTER CYCLE NUMBER',I3,' COVERS YEARS',I5,' TO',I5)
C
C     RESET THE ACTIVITIY SCHEDULE FOR THE STAND.
C
      CALL OPCYCL (NCYC,IY)
      CALL OPCSET (ICYC)
  140 CONTINUE
C
      IF (PDEBUG) THEN
         WRITE (JOPPRT,150) MNCYC,NCYC,MICYC,INCYC,ICYC,IPMODI
  150    FORMAT(/' IN SCHED2: MNCYC=',I2,' NCYC=',I2,' MICYC=',I2,
     >          ' INCYC= ',I2,' ICYC= ',I2,' IPMODI=',I2)
         WRITE (JOPPRT,151) (MIY(I),I=1,MNCYC+1)
  151    FORMAT (' MIY=',20I5/5X,21I5)
         IF (NCYC.GT.0) WRITE (JOPPRT,152) (IY(I),I=1,(NCYC+1))
  152    FORMAT ('  IY=',20I5/5X,21I5)
      ENDIF
      RETURN
      END
