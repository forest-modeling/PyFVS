      SUBROUTINE SNDTBS (KODE)
      IMPLICIT NONE
C----------
C  **SNDTBS  DATE OF LAST REVISION:  07/31/08
C----------
C
C     PRINTS 1) THE STAND COMPOSTION TABLE, AND/OR
C            2) THE SAMPLE TREE AND STAND ATTRIBUTE TABLE.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1982
C
C     CALLED FROM PPIN
C
C     KODE  = 0, NO ERRORS FOUND
C             1, ONERPT COULD NOT TRACE A BRANCH THRU ALL OF ITS NODES.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'PPCISN.F77'
C
C
COMMONS
C
C     STORAGE ALLOCATION:
C
      INTEGER KODE,IONER(42),ICBRK(42),IENISN(MXSTND),NENISN,I,JISN,ILST
      INTEGER KSTND,INDXR,NRPTS,NCYC2,IRPT
      REAL ALL(15000)
      INTEGER ISNUMS(3000),ICYCS(3000),LOCS(3000)
      INTEGER IELTS(3000),IPTS(3000)
      EQUIVALENCE (ALL(1),BFV(1)),(ALL(1),ISNUMS(1)),
     >            (ALL(3001 ),ICYCS(1)),
     >            (ALL(6001 ),LOCS (1)),
     >            (ALL(9001 ),IELTS(1)),
     >            (ALL(12001),IPTS (1))
C
      LOGICAL L8,LCHKED,LPRINT
C
C     SORT THE PRINT-DATA POINTERS.
C
      CALL SRTISN (NMPRTD,CISNS,IPTS,MXSTND,NENISN,IENISN)
C
C     WRITE DEBUG DATA
C
      IF (PDEBUG) THEN
         WRITE (JOPPRT,1) NMPRTD,NENISN
    1    FORMAT (/' IN SNDTBS; NMPRTD=',I5,'; NENISN=',I5/
     >        ' ISNUMS, CISNS, ICYCS, LOCS, IELTS, AND IPTS.')
         WRITE (JOPPRT,10) (ISNUMS(IPTS(I)),CISNS(IPTS(I)),
     >         ICYCS(IPTS(I)),LOCS(IPTS(I)),IELTS(IPTS(I)),IPTS(I),
     >         I=1,NMPRTD)
   10    FORMAT (I5,2X,A10,4I5)
      ENDIF
C
C     SET L8 TRUE. IT WILL BE SET FALSE WHEN DATA ARE ACTUALLY PRINTED
C     IF THE ROUTINE ENDS WITH L8=TRUE, THEN WE HAVE AN ERROR.
C
      L8=.TRUE.
C
C     ACCESS THE STAND-TABLE DATA IN ORDER OF STAND PROCESSING
C     (VIA ASSENDING ISN'S).
C
      DO 30 JISN=1,NENISN
C
C     NOTE THAT IENISN MAY CONTAIN NEGETIVE MEMBERS, THESE ARE CAUSED
C     BY THE USE OF THE SIGN BIT AS A FLAG IN SUBROUTINE ONEISN; HERE
C     WE ONLY NEED THE ABSOLUTE VALUE OF THE SUBJECT IENISN.
C
      ILST=IENISN(JISN)
      IF (ILST.LT.0) ILST=-ILST
C
      KSTND=IPTS(ILST)
      INDXR=ISNUMS(KSTND)
C
C     CHECK THE MANAGEMENT AND STAND ID'S FOR THE SUBJECT ROOT ISN
C     AGAINST THE SELSTAND AND SELMGMID CONDITIONS SET BY THE USER.
C     IF THE ROOT STAND IS NOT SELECTED FOR PRINTING; THEN SKIP
C     FUTHER PROCESSING.
C
      CALL SLCND1 (LPRINT,STDIDS(INDXR),MGMIDS(INDXR))
      IF (PDEBUG) WRITE (JOPPRT,11) LPRINT,INDXR,STDIDS(INDXR),
     >                              MGMIDS(INDXR)
   11 FORMAT (/' IN SNDTBS: LPRINT=',L2,' INDXR=',I5,' STDIDS=',A26,
     >         ' MGMIDS=',A4)
      IF (.NOT.LPRINT) GOTO 30
C
C     THE INITIAL SCREEN IS COMPLETE.  THE NEXT STEP IS TO SORT WITHIN
C     THE ROOT-ISN AND ESTABLISH POINTERS TO BREAKS BETWEEN CYCLES.
C
      CALL ONEISN (JISN,CISNS,IPTS,ICYCS,IENISN,ICBRK,NCYC2,NRPTS)
C
C     WRITE DEBUG OUTPUT, IF REQUESTED.
C
      IF (PDEBUG) THEN
         WRITE (JOPPRT,12) JISN,NCYC2,NRPTS
   12    FORMAT (/' IN SNDTBS: JISN=',I4,'; NCYC2=',I4,'; NRPTS=',I4,
     >           ' -- IPTS, ICYCS, THEN ICBRK')
         WRITE (JOPPRT,13) (IPTS(I),I=1,NMPRTD)
         WRITE (JOPPRT,13) (ICYCS(I),I=1,NMPRTD)
         WRITE (JOPPRT,13) (ICBRK(I),I=1,NCYC2)
   13    FORMAT (1X,20I6)
      ENDIF
C
C     ONEISN RETURNS NRPTS, WHICH IS THE NUMBER OF REPORTS PRESENT
C     FOR THE SUBJECT ROOT-ISN. IN OTHER WORDS ONE ROOT-ISN MAY
C     HAVE SEVERAL BRANCHES;  THE NUMBER OF LEAVES IS DEFINED BY
C     NRPTS.  ONE REPORT WILL BE PRINTED FOR EACH BRANCH END, OR
C     LEAF.  (THE END OF THE BRANCH IS A LEAF).
C
C     LOOP THRU ALL REPORTS.
C
      DO 20 IRPT=1,NRPTS
C
C     CREATE A LIST OF OUTPUT POINTERS WHICH MAKE UP THE CURRENT REPORT
C
      CALL ONERPT (JOPPRT,KODE,IRPT,NCYC2,IONER,ICBRK,IPTS,CISNS)
C
C     PRINT REQUESTED DEBUG
C
      IF (PDEBUG) WRITE (JOPPRT,16) (IONER(I),I=1,NCYC2)
   16 FORMAT (/' IN SNDTBS, IONER: ',22I5/19X,22I5)
C
C     IF KODE=1, THE REPORT IS INCOMPLETE (ONERPT WRITES ERROR MSG)
C     AND CANNOT BE WRITTEN; THEN: RETURN
C
      IF (KODE.EQ.1) RETURN
C
C     LET ISTND BE A POINTER TO CISNS.  A NEG ISTND SIGNIFIES TO PPISN
C     THAT THE CORRECT CISN IS IN CISNS, OTHERWISE PPISN USES CISNUM
C
      ISTND=-IONER(NCYC2)
C
C     FIND OUT IF THIS REPORT WILL BE PRINTED/PROCESSED; THAT IS CHECK
C     THE SELISN, SELSPLAB, AND SELHPLAB CONDITIONS SET BY THE USER.
C
C     IF THE REPORT IS NOT GOING TO BE PRINTED/PROCESSED; THEN: GO ON
C     TO THE NEXT REPORT.
C
      CALL SLCND2 (LPRINT,CISNS(IONER(NCYC2)))
      IF (.NOT.LPRINT) GOTO 20
      ISTND=ISNUMS(IONER(NCYC2))
      CALL SLCND3 (LPRINT,ISTND)
      IF (.NOT.LPRINT) GOTO 20
      CALL SLCND4 (LPRINT,ISTND)
      IF (.NOT.LPRINT) GOTO 20
C
C     WRITE THE REQUESTED REPORTS AND SIGNAL THAT ERROR 8 IS NOT
C     COMMITTED.
C
      CALL RPTWTR (NCYC2,IONER)
      L8=.FALSE.
   20 CONTINUE
   30 CONTINUE
C
C     IF NOTHING HAS BEEN PRINTED-PROCESSED, WRITE AN ERROR MSG.
C
      IF (L8) CALL PPERRP (.TRUE.,8)
      RETURN
      END
