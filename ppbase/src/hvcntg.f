      SUBROUTINE HVCNTG (II,IHV,CONTIG,IRC)
      IMPLICIT NONE
C----------
C  **HVCNTG--PPBASE   DATE OF LAST REVISION:  07/31/08              
C----------
C     FINDS CONTIGUOUS ACRES.
C
C     II    = INDEX POINTER TO MEMBER OF ISNSRT FOR THE CURRENT
C             STAND.
C     IHV   = THE CURRENT MULTISTAND POLICY BEING PROCESSED.
C     NNEIG = LENGTH OF "TOP" PORTION OF IDXLST, SET BY THIS ROUTINE
C     IRC   = RETURN CODES: 0=OK, 1=MISSING DATA.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
C
      INTEGER IRC,IHV,II,IST,NLST,I,NNEIG
      REAL CONTIG
C
C
C
C     BUILD A LIST OF INDEX POINTERS TO STANDS THAT WILL BE CLEAR CUT
C     FOR THIS POLICY.  INCLUDE THE SUBJECT STAND AS THE FIRST STAND.
C
      IST=ISNSRT(II)
      IF (IHVSTA(IST,IHV).EQ.-1) THEN
         NLST=1
         ISDWK1(1)=ISNSRT(II)
C
C        IF THIS IS NOT THE ONLY STAND, THEN ADD OTHER STANDS.
C
         IF (II.GT.1) THEN
            DO 10 I=1,II-1
            IST=ISNSRT(I)
            IF (IHVSTA(IST,IHV).EQ.-3) THEN
               NLST=NLST+1
               ISDWK1(NLST)=IST
            ENDIF
   10       CONTINUE
         ENDIF
C
C        CALL SPCNTG TO FIND OUT WHICH STANDS ARE NEIGHBORS OF THE
C        CURRENT STAND AND HOW MANY CONTIGUOUS CLEAR CUT ACRES WILL BE
C        CREATED IF THE SUBJECT STAND IS HARVESTED.
C
         CALL SPCNTG (ISDWK1,NLST,NNEIG,CONTIG,IRC)
C
      ELSE
         CONTIG=0.0
         NLST=0
         IRC=0
         NNEIG=0
      ENDIF
      IF (LHVDEB) THEN
         WRITE (JOPPRT,30) STDIDS(ISNSRT(II)),CONTIG,NLST,NNEIG,IRC
   30    FORMAT (/' IN HVCNTG, SUBJECT STAND=',A,' CONTIG=',F8.2,
     >            ' NLST=',I5,' NNEIG=',I5,' IRC=',I2)
         WRITE (JOPPRT,40) NNEIG,(STDIDS(ISDWK1(I)),I=1,NLST)
   40    FORMAT (' IN HVCNTG, CLEAR CUT STAND SET (FIRST',I3,
     >           ' ARE CONTIGUOUS)...'/((4(2X,A26))))
      ENDIF
      RETURN
      END
