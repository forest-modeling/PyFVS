      SUBROUTINE HVALID (NHRVP,JOPPRT)
      IMPLICIT NONE
C----------
C  **HVALID DATE OF LAST REVISION:  07/31/08
C----------
C
C     CALLED FROM HVIN...CHECK ON THE VALIDITY THE CURRENT POLICY.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'PPEXCM.F77'
C
C
COMMONS
C
      INTEGER JOPPRT,NHRVP,IHV,I,ICD
      LOGICAL LVALID,LVAL2
C
      IF (LHVDEB) WRITE (JOPPRT,'(/'' IN HVALID, NHRVP='',I3)') NHRVP
C
C     INITIALIZE LVALID
C
      LVALID=.TRUE.
C
C     CHECK ON THE PRESENCE OF A POLICY LABEL.
C
      IF (HVPLAB(NHRVP).EQ.' ') THEN
         LVALID=.FALSE.
         WRITE (JOPPRT,'(/'' MSPLABEL IS BLANK.'')')
      ELSE
C
C        MAKE SURE THAT THE LABEL IS UNIQUE
C
         DO 10 IHV=1,IXHRVP
         IF (IHV.EQ.NHRVP) GOTO 10
C
C        IF HIERARCHIES ARE BEING USED, THEN LABEL MUST BE UNIQUE
C        UP TO THE PERIOD WHEN COMPARED TO POLICY ONE.
C
         IF (LHIER.AND.IHV.EQ.1) THEN
            I=2
         ELSE
            I=1
         ENDIF
         IF(HVPLAB(NHRVP)(1:LNHPLB(NHRVP,I)).EQ.
     >      HVPLAB(IHV  )(1:LNHPLB(IHV  ,I))) THEN
            LVALID=.FALSE.
            WRITE (JOPPRT,'(/'' THE MSPLABEL MATCHES'',
     >          '' THE ONE SPECIFIED FOR POLICY '',I2)') IHV
         ENDIF
   10    CONTINUE
      ENDIF
C
C     INITIALIZE LVAL2
C
      LVAL2=.TRUE.
C
C     CHECK ON THE TARGET SPECIFICATION...IS THERE ANY OPCODE?
C
      IF (IHVTAB(NHRVP,1).EQ.0) THEN
         LVAL2=.FALSE.
         WRITE (JOPPRT,'(/'' THE TARGET EXPRESSION IS MISSING OR'',
     >         '' WAS INCORRECTLY SPECIFIED.'')')
      ELSE
C
C        DOES THE OPCODE ACCESS ILLEGAL VARIABLES AND/OR OPCODES?
C
         DO 20 I=IHVTAB(NHRVP,1),MPEXCD
         ICD=IPEXCD(I)
         IF (ICD.EQ.0) GOTO 25
         IF (ICD.GT.9 .AND. ICD.LT.101) GOTO 20
         IF (ICD.EQ.111 .OR. ICD.EQ.112) GOTO 20
         IF (ICD.GT.7000 .AND. ICD.LT.10000) GOTO 20
         ICD=ICD/100
         IF (ICD.NE.104 .AND. ICD.NE.105 .AND. ICD.NE.106 .AND.
     >       ICD.NE.112 .AND. ICD.NE.113) GOTO 20
         LVAL2=.FALSE.
   20    CONTINUE
   25    CONTINUE
         IF (.NOT.LVAL2) WRITE (JOPPRT,'(/'' TARGET EXPRESSION '',
     >      '' USES ILLEGAL VARIABLES AND/OR OPERATORS.'')')
      ENDIF
C
C     RESET LVALID AND LVAL2
C
      LVALID=LVALID.AND.LVAL2
      LVAL2=.TRUE.
C
C     CHECK ON THE PRIORITY SPECIFICATION...IS THERE ANY OPCODE?
C
      IF (IHVTAB(NHRVP,2).EQ.0) THEN
         LVAL2=.FALSE.
         WRITE (JOPPRT,'(/'' THE PRIORITY EXPRESSION IS MISSING OR'',
     >         '' WAS INCORRECTLY SPECIFIED.'')')
      ELSE
C
C        DOES THE OPCODE ACCESS ILLEGAL VARIABLES AND/OR OPCODES?
C
         DO 30 I=IHVTAB(NHRVP,2),MPEXCD
         ICD=IPEXCD(I)
         IF (ICD.EQ.0) GOTO 35
         IF ((ICD.GT.9   .AND. ICD.LT.200 ) .OR.
     >       (ICD.GT.300)) GOTO 30
         LVAL2=.FALSE.
   30    CONTINUE
   35    CONTINUE
         IF (.NOT.LVAL2) WRITE (JOPPRT,'(/'' PRIORITY EXPRESSION'',
     >      '' USES ILLEGAL VARIABLES AND/OR OPERATORS.'')')
      ENDIF
C
C     RESET LVALID AND LVAL2
C
      LVALID=LVALID.AND.LVAL2
      LVAL2=.TRUE.
C
C     CHECK ON THE CREDIT SPECIFICATION...IS THERE ANY OPCODE?
C
      IF (IHVTAB(NHRVP,3).EQ.0) THEN
         LVAL2=.FALSE.
         WRITE (JOPPRT,'(/'' THE CREDIT EXPRESSION IS MISSING OR'',
     >         '' WAS INCORRECTLY SPECIFIED.'')')
      ELSE
C
C        DOES THE OPCODE ACCESS ILLEGAL VARIABLES AND/OR OPCODES?
C
         DO 40 I=IHVTAB(NHRVP,3),MPEXCD
         ICD=IPEXCD(I)
         IF (ICD.EQ.0) GOTO 45
         IF ((ICD.GT.9 .AND. ICD.LT.7000) .OR. ICD.GT.8000) GOTO 40
         LVAL2=.FALSE.
   40    CONTINUE
   45    CONTINUE
         IF (.NOT.LVAL2) WRITE (JOPPRT,'(/'' CREDIT EXPRESSION'',
     >      '' USES ILLEGAL VARIABLES AND/OR OPERATORS.'')')
      ENDIF
C
C     RESET LVALID.
C
      LVALID=LVALID.AND.LVAL2
      IF (.NOT.LVALID) THEN
         WRITE (JOPPRT,'(/'' ******** ERROR: MULTISTAND TREATMENT'',
     >         '' POLICY IS IGNORED.'')')
         CALL RCDSET (3,.TRUE.)
         DO I=1,3
            IHVTAB(NHRVP,I)=0
         ENDDO
         HVPLAB(NHRVP)=' '
         LNHPLB(NHRVP,1)=0
         LNHPLB(NHRVP,2)=0
      ENDIF
      RETURN
      END
