      SUBROUTINE SPNBRD (SCALE,INPUT,NRECS,IRC)
      IMPLICIT NONE
C----------
C  **SPNBRD--PPBASE   DATE OF LAST REVISION:  07/31/08
C----------
C     READS THE NEIGHBORS INFORMATION
C
C     SCALE = SCALING VALUE USED TO SCALE THE INPUT DATA.
C     INPUT = INPUT DATA SET REFERENCE NUMBER
C     NRECS = INPUT RECORD COUNT
C     IRC   = RETURN CODE: 0=OK, 1=ACTUAL END-OF-FILE WAS REACHED,
C             2=TOO MANY INPUT RECORDS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPSPNB.F77'
C
C
COMMONS
C
      INTEGER IRC,NRECS,INPUT
      REAL SCALE,SCL,X
      CHARACTER*26 ID1,ID2
C
C     CHECK THE SCALING VALUE
C
      SCL=SCALE
      IF (SCALE.LE.0.0) SCL=1.0
C
C     READ THE DATA...COUNT THE INPUT RECORDS.
C
      NNBS=0
   10 CONTINUE
      READ (INPUT,'(A26,T29,A26,T55,F10.0)',END=30) ID1,ID2,X
      IF (INDEX(ID1,'-999').GT.0) GOTO 20
      IF (ID1.EQ.' ' .OR. ID2.EQ.' ') GOTO 10
      NNBS=NNBS+1
      IF (NNBS.GT.MXNBS) THEN
         IRC=2
         GOTO 40
      ENDIF
      IF (ID1.LE.ID2) THEN
         CNBID1(NNBS)=ID1
         CNBID2(NNBS)=ID2
      ELSE
         CNBID1(NNBS)=ID2
         CNBID2(NNBS)=ID1
      ENDIF
      BORDER(NNBS)=X*SCL
      GOTO 10
   20 CONTINUE
      IRC=0
      GOTO 40
   30 CONTINUE
      IRC=1
   40 CONTINUE
      NRECS=NNBS
      RETURN
      END
