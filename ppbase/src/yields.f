      SUBROUTINE YIELDS
      IMPLICIT NONE
C----------
C  **YIELDS--PPBASE   DATE OF LAST REVISION:  07/31/08
C----------
C
C     TASKS:
C     1) INDIVIDUAL STAND SUMMARY YIELD TABLES ARE PRINTED AND/OR
C        WRITTEN WITHOUT HEADINGS TO THE DATA SET REFERENCED BY JOCOPY.
C     2) INDIVIDUAL STAND ACTIVITY SUMMARIES ARE PRINTED.
C     3) STAND SUMMARY YIELD TABLES ARE COMBINED INTO A COMPOSITE
C        YIELD TABLE WHICH IS PRINTED AND/OR WRITTEN WITHOUT HEADINGS
C        TO THE DATA SET REFERENCED BY JOCOPY.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1982.
C
C     CALLED FROM PPIN - PROCESS PPE KEYWORDS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
COMMONS
C
      INTEGER LSTYLD(MXSTND),I20,JPRT,NSEL,NOUT,IOUT,I,IKNT
      REAL WEIGHT
      LOGICAL LPROC,LINIT,LSFLAG,LAUT,LDONE
      DATA I20/20/
C
C     START THE OUTPUT OPTIONS
C
      JPRT=0
      IF (LSUM) JPRT=JOPPRT
C
C     INITIALIZE NSEL, WHICH COUNTS THE NUMBER OF STANDS ACTUALLY
C     SELECTED.
C
      NSEL=0
C
C     CREATE A LIST OF STANDS THAT PASS THE SELECT CONDITIONS.
C
      DO 10 ISTND=1,NOSTND
C
C     TEST THE STAND AND MANAGEMENT IDS FOR SELECT-CONDITIONS.
C
      CALL SLCND1 (LPROC,STDIDS(ISTND),MGMIDS(ISTND))
      IF (PDEBUG) WRITE (JOPPRT,'('' IN YIELDS AT CND1, LPROC='',L2)')
     >            LPROC
      IF (.NOT. LPROC) GOTO 10
C
C     TEST THE ISN FOR USER SPECIFIED SELECT-CONDITIONS.
C
      CALL SLCND2 (LPROC,CISNUM(ISTND))
      IF (PDEBUG) WRITE (JOPPRT,'('' IN YIELDS AT CND2, LPROC='',L2)')
     >            LPROC
      IF (.NOT. LPROC) GOTO 10
C
C     TEST THE STAND LABEL SET FOR USER SPECIFIED SELECT-CONDITIONS.
C
      CALL SLCND3 (LPROC,ISTND)
      IF (PDEBUG) WRITE (JOPPRT,'('' IN YIELDS AT CND3, LPROC='',L2)')
     >            LPROC
      IF (.NOT. LPROC) GOTO 10
C
C     TEST THE MULTISTAND POLICY FOR USER SPECIFIED SELECT-CONDITIONS.
C
      CALL SLCND4 (LPROC,ISTND)
      IF (PDEBUG) WRITE (JOPPRT,'('' IN YIELDS AT CND4, LPROC='',L2)')
     >            LPROC
      IF (.NOT. LPROC) GOTO 10
C
C     COUNT THE NUMBER OF ACCEPTED STANDS, SAVE THE STAND POINTER
C     IN A LIST (ISNSRT).
C
      NSEL=NSEL+1
      ISNSRT(NSEL)=ISTND
      IF (PDEBUG) WRITE (JOPPRT,5) NSEL,ISTND
    5 FORMAT (' IN YIELDS: NSEL=',I5,' ISTND=',I5)
   10 CONTINUE
C
C     IF NO STANDS WERE SELECTED FOR PROCESSING, THEN WRITE AN ERROR
C     MSG, AND RETURN.
C
      IF (NSEL.LE.0) THEN
         CALL PPERRP (.TRUE.,8)
         RETURN
      ENDIF
C
C     PROCESS THE LIST OF POINTERS FOR THE BYCCCCCC CONDITIONS.
C
C     OPEN A LOOP..."DO WHILE LDONE=.FALSE." ... LET LDONE=.TRUE.
C     ON FIRST PASS TO INDICATE TO BYGRPS THAT THIS IS THE FIRST CALL.
C
      LDONE=.TRUE.
   20 CONTINUE
      CALL BYGRPS (NSEL,NOUT,LSTYLD,LDONE)
C
C     IF LDONE IS TRUE, BRANCH TO EXIT.
C
      IF (LDONE) GOTO 250
C
C     LOOP OVER THE STANDS IN THE ORDER SPECIFIED IN THE LSTYLD...
C
      LINIT=.TRUE.
      DO 200 IOUT=1,NOUT
      ISTND=LSTYLD(IOUT)
C
C     SAVE THE VALUE OF LFLAG (HEADING CONTROL).
C
      LSFLAG=LFLAG
      CALL GETSTD
C
C     RESTORE LFLAG TO THE VALUE IT HAD BEFORE THE GETSTD (WHICH MAY
C     HAVE RESULTED IN LFLAG BEING RESET).
C
      LFLAG=LSFLAG
C
C     CONTROL THE PRINTING.  IF THE TABLES ARE BEING PRINTED, THEY
C     WILL BE PRINTED UNDER A NEW HEADING; IF NOT, INFORMATIONAL
C     MESSAGES WILL BE WRITTEN TO THE PRINTER AND CONTAINED IN THE
C     KEYWORD TABLE.
C
C     IF THE ACTIVITY SUMMARY AND THE SUMMARY YIELD TABLES ARE
C     GOING TO BE PRINTED; THEN:  CLOSE THE KEYWORD TABLE.
C
      IF (LACTS.OR.LSUM) THEN
         CALL PPCLOP (JOPPRT)
         CALL GROHED (JOPPRT)
      ENDIF
C
C     WRITE REQUESTED SUMMARY YIELD TABLE DATA.
C
C     IF NOT END-OF-RUN, THEN: ZERO OUT ITEMS 7 TO 16 IN IOSUM AND
C     SET IKNT EQUAL TO THE CYCLE COUNT
C
      IF (.NOT.LEORUN) THEN
         DO 30 I=7,16
         IOSUM(I,ICYC)=0
   30    CONTINUE
      ENDIF
      IKNT=ICYC
C
C     GET THE STAND WEIGHTING FACTOR.
C
      CALL PPWEIG (ISTND,WEIGHT)
      WRITE (JOPPRT,40) CISNUM(ISTND)(1:4),NPLT,MGMID,WEIGHT
   40 FORMAT (/' NOTE:  PROCESSING STAND NUMBER: ',A4,' STAND ID: ',
     >        A26,'  MGMT ID (AS INPUT): ',A4,/,
     >        '        PPE STAND WEIGHTING FACTOR: ',E15.7)
C
C     IF THE SUMMARY STATS OR ACTIVITY SUMMARY IS BEING PRINTED,
C     WRITE THE BRANCHING NUMBER AND MULTISTAND AND STAND LABELS.
C
      IF (LSUM .OR. LACTS) CALL PPLABS (JOPPRT)
C
C     IF THE MACHINE READABLE LABEL AND WEIGHTS FILE IS BEING
C     WRITTEN, THEN CALL PLBWTS TO WRITE THIS OUTPUT.
C
      IF (LBWTS) CALL PLBWTS (WEIGHT)
C
C     CALL THE AUTOMATIC MGMTID GENERATOR.
C
      IF (JPRT+JCPSUM.GT.0) CALL PPAUID (LAUT,MGMID)
C
C     WRITE THE SUMMARY STATISTICS.
C
      CALL SUMOUT (IOSUM,I20,0,JOPPRT,JPRT,JCPSUM,IKNT,MGMID,NPLT,
     >             WEIGHT,ITITLE,IPTINV)
C
C     WRITE REQUESTED ACTIVITY SUMMARY.
C
      IF (LACTS) CALL OPLIST (.FALSE.,NPLT,MGMID,ITITLE)
C
C     WRITE DEBUG OUTPUT.
C
      IF (PDEBUG) WRITE (JOPPRT,60) ISTND,IKNT,JCPCMP,LINIT,
     >                              LCMPS,LEORUN
   60 FORMAT (/' IN YIELDS: ISTND=',I4,' IKNT=',I3,' JCPCMP=',I3,
     >        ' LINIT=',L2,' LCMPS=',L2,' LEORUN=',L2)
C
C     BUILD REQUESTED COMPOSITE YIELD TABLES.
C
      IF (LCMPS .OR. JCPCMP.GT.0 .AND. LEORUN) THEN
         CALL CMADDS (WEIGHT,IKNT,LINIT)
         LINIT=.FALSE.
      ENDIF
  200 CONTINUE
C
C     WRITE THE REQUESTED COMPOSITE YIELD TABLE.
C
      IF (LCMPS .OR. JCPCMP.GT.0) THEN
         IF (LCMPS) THEN
            I=JOPPRT
            IF (LFLAG) CALL GROHED (JOPPRT)
         ELSE
            I=0
         ENDIF
         CALL CMPRT (I,JCPCMP)
      ENDIF
C
C     BRANCH BACK TO BYGRPS ... (DO WHILE LDONE=.FALSE.)
C
      GOTO 20
C
C     BRANCH HERE WHEN LDONE IS .TRUE.
C
  250 CONTINUE
      RETURN
      END
