      PROGRAM PPMAIN
      IMPLICIT NONE
C----------
C  **PPMAIN--PPBASE   DATE OF LAST REVISION:  12/15/10
C----------
C
C     PARALLEL PROCESSING EXTENSION TO THE PROGNOSIS
C     MODEL FOR STAND DEVELOPMENT.
C
C     DEVELOPED UNDER THE LEADERSHIP OF:
C
C     DR. ALBERT R. STAGE
C     FORESTRY SCIENCES LABORATORY
C     INTERMOUNTAIN FOREST AND RANGE EXPERIMENT STATION
C     USDA  --  FOREST SERVICE
C     1221 SOUTH MAIN
C     MOSCOW, IDAHO   83843    --    USA.
C
C     PHONE (208) 882-3557
C
C     PARALLEL PROCESSING EXTENSION WAS DESIGNED AND IMPLIMENTED BY:
C
C     NICHOLAS L. CROOKSTON
C     INITIAL CODING ACCOMPLISHED BETWEEN SEPT 1979 AND JAN 1990.
C     (ALONG WITH A MILLION OTHER THINGS)
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'ECON.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
C     LBWATV, LTMATV, AND LMPATV ARE TRUE IF BUDWORM, TUSSOCK MOTH,
C     AND/OR MOUNTAIN PINE BEETLE ARE ACTIVE THIS MASTER CYCLE UNDER
C     PROCESSING MODE 2 (INTERSTAND INTERACTION).
C
      INTEGER IIISTN,KODE,IMDSUM
      LOGICAL LBGCGO,LBWATV,LTMATV,LMPATV,LSAVE,LSFLAG
      LOGICAL LTMGO,LMPBGO,LBWEGO,LDFBGO,LCVATV
C
C     OPEN PPE DATA FILES.
C
      CALL PPEOPN
C
C     OPEN PROGNOSIS FILES.
C
      CALL FILOPN
C
C     INITIALIZE THE MULTIPLE REPORT ROUTINE (THIS DOES NOT OPEN A FILE)
C
      CALL GENRPT

C
C     SET FLAGS TO INITIALIZE THE PARALLEL PROCESSING SYSTEM
C
      LIN1ST = .TRUE.
      LFLAG = .TRUE.
   20 CONTINUE
C
C     READ AND PROCESS CONTROL FILE
C
      CALL PPIN
      IF (PDEBUG) WRITE (JOPPRT,22) LRESTR,LASTIN,ISTND
   22 FORMAT (/' IN MAIN: LRESTR=',L2,'; LASTIN=',L2,'; ISTND=',I6)
C
C     IF CONTINUING A RUN, BRANCH PAST ALL-STAND INITIALIZATION.
C
      IF (LRESTR) GOTO 40
C
C     IF THE LAST STAND HAS BEEN INITIATED, THEN: BRANCH TO PROJECTION
C     LOOP.
C
      IF (LASTIN) GOTO 30
C
C     ELSE: INPUT AND INITIATE NEXT STAND
C
      CALL INSTND
C
C     INSURE CURRENT STAND IS PROJECTED UP TO THE MASTER STARTING YEAR.
C
      ICYC = 1
      ICL6 = 0
      CALL GRSTND
C
C     IF THE STAND HAS GONE THRU ONE COMPLETE CYCLE; THEN:
C     LOAD SUMMARY ARRAY AS IF THIS CYCLE WERE THE LAST CYCLE.
C
      IF (INCYC.GT.0) CALL LDSUM (3)
C
C     STORE THE CURRENT STAND ON DIRECT ACCESS FILE.
C
      CALL PUTSTD
C
C     CREATE THE AUTOMATIC START-UP REPS.
C
      CALL PPARPS
C
C     WRITE A SUMMARY-TO-DATE OF THE CURRENT STAND.
C
      WRITE (JOPPRT,25)
   25 FORMAT (//' SUMMARY UP TO MASTER STARTING YEAR:')
      CALL SUMOUT (IOSUM,20,1,JOPPRT,JOPPRT,0,ICYC,MGMID,NPLT,0.,
     &             ITITLE,IPTINV)
C
C     GO TO PPIN FOR FUTHER PROCESSING INSTRUCTIONS
C
      LFLAG=.TRUE.

C     WRITE PROGRESS NOTE

      WRITE (*,23) NOSTND,TRIM(NPLT)
   23 FORMAT(' Note: Stand',I7,' (ID: ',A,
     >       ') completed initial processing.')
      GOTO 20
   30 CONTINUE
      IF (PDEBUG) WRITE (JOPPRT,35) LPAUSE
   35 FORMAT (/' IN MAIN, LPAUSE=',40L2)
C
C     INITIALIZE THE ALL STAND PROCESSING ROUTINES.
C
      CALL ALINIT
   40 CONTINUE

C
C     MAKE SURE THAT THE AREA DATA HAS BEEN CORRECTLY LOADED.
C
      CALL LDAREA
C
C     INCREMENT THE MASTER CLOCK
C
      CALL PPMCLK

C     WRITE PROGRESS MESSAGE.

      WRITE (*,'(/'' Note: Starting master cycle'',I3,'' of '',I3)')
     >       MICYC-1,MNCYC
C
C     REVIEW ALL STANDS AND MAKE NECESSARY MODIFICATIONS TO PROJECTION
C     CONTROL VARIABLES...SAVE THE CURRENT VALUE OF LFLAG
C
      LSFLAG=LFLAG
      CALL ALSTD1
      LFLAG=LSFLAG
C
C     ORGANIZE THE SEQUENCE OF STAND PROCESSING.
C
      CALL C11SRT (NOSTND,CISNUM,ISNSRT,.FALSE.)
C
C     PROCESS EACH STAND; NOTE THAT THE LENGTH OF THE LIST OF STANDS
C     MAY INCREASE DURING THE CYCLE.
C
      IIISTN=0
   50 CONTINUE
      IIISTN=IIISTN+1
      IF (PDEBUG) WRITE (JOPPRT,55) IIISTN,NOSTND,LEORUN,MNCYC,MICYC
   55 FORMAT (/' IN PPMAIN, IIISTN=',I4,'; NOSTND=',I7,
     >        '; LEORUN=',L2,'; MNCYC=',I3,'; MICYC=',I3)
C
C     IF ALL OF THE STANDS HAVE BEEN PROCESSED; THEN: BRANCH DOWN TO
C     PREPARE FOR 1) SECOND PART OF MODE 2 CYCLE, 2) NEXT MASTER CYCLE,
C     OR 3) NEXT PROCESSING INSTRUCTION FROM USER.
C
      IF (IIISTN.GT.NOSTND) GOTO 150
      ISTND=ISNSRT(IIISTN)
C
C     RETRIVE STAND 'ISTND' AND RETAIN THE VALUE OF LFLAG
C
      IF (PDEBUG) WRITE (JOPPRT,70) ISTND,CISNUM(ISTND)
   70 FORMAT (/' IN MAIN, CALLING GETSTD, CISNUM(',I6,')= ',A11)
      LSFLAG=LFLAG
      CALL GETSTD
      LFLAG=LSFLAG
C
C     IF MULTISTAND POLICY SIMULATION IS ACTIVE, CALL HVREPS TO
C     CREATE NECESSARY REPLICATE STAND AND SET THE VALUE OF SELECTED
C     IN THE EVENT MON.
C
      IF (LHVALL) CALL HVREPS (NPLT)
C
C     ADD OPTIONS THAT ARE BEING READ FROM AN EXTERNAL FILE (IF ANY).
C
      CALL OPRDAT (JEXOPT,KODE)
C
C     CALL PPINTR TO SET THE PROCESSING MODE FOR THIS STAND:
C     IPMODE=1, IF THE STAND WILL BE INDEPENTENTLY PROCESSED UP TO
C               THE END OF THE MASTER CYCLE.
C            2, IF THE STAND WILL BE ALLOWED TO INTERACT ON A
C               ANNUAL OR MORE FREQUENT BASIS.
C     THE PROCESSING MODE DEPENDS ON THE PREVIOUSLY SET MODE AND THE
C     PRESENCE OR ABSENCE OF ACTIVITIES THAT REQUIRE MODE 1.
C
      CALL PPINTR
C
C     REPORT PROGRESS TO SCREEN (AJM 9/05)
C
      WRITE (*,75) MICYC-1,MNCYC,IIISTN,TRIM(STDIDS(ISTND)),NOSTND,
     >             1,IPMODI
   75 FORMAT(' Note: Cycle',I3,' of',I3,
     > '; Stand:',I7,' (ID: ',A,') of',I7,'; Phase',I2,' of',I2)
C
C     INSURE CYCLING CONTROL VARIABLES ARE CORRECTLY DEFINED
C
      CALL SCHED2
C
C     IF THE PROCESSING MODE IS 2, BRANCH TO THE CODE WHICH ALLOWS
C     WITHIN YEAR INTERACTION.
C
      IF (IPMODI .GE. 2) GOTO 100
C
C     PROJECT STAND 'ISTND' UP TO THE END OF THE CURRENT MASTER CYCLE.
C
      CALL GRSTND
C
C     PROCESS MULTISTAND POLICY LABELS.
C
      IF (LHVALL) CALL HVFXLB
C
C     IF THE MASTER CLOCK HAS INDECATED END-OF-RUN, THEN COMPUTE AND
C     STORE THE STAND'S LAST YEAR STATISTICS, THEN GO ON TO NEXT STAND.
C     ...WRITE COVER OUTPUT.
C
      IF (LEORUN) THEN
         CALL GROEND
         GOTO 50
      ENDIF
C
C     ELSE: STORE THE STAND ON THE DA FILE, THEN GO ON TO NEXT STAND.
C
      CALL SPARLS (ISNKEY(ISTND,2),ISNKEY(ISTND,3))
      CALL PUTSTD
      GOTO 50
C
C     IF PROCESSING MODE IS EQUAL TO 2, SIMPLY PROCESS THE TREE
C     LIST UP TO AND INCLUDING COMPUTATIONS OF NORMAL MORTALITY.
C
  100 CONTINUE
      CALL GRINCR (PDEBUG,IPMODI,LTMGO,LMPBGO,LDFBGO,
     1             LBWEGO,LCVATV,LBGCGO)
C
C     INITIALIZE ONE MOUNTAIN PINE BEETLE STAND.
C
      CALL BMSDIT
C
C     STORE THE STAND ON THE DA FILE AND GO ON TO NEXT STAND.
C
      CALL SPARLS (ISNKEY(ISTND,2),ISNKEY(ISTND,3))
      CALL PUTSTD
      GOTO 50
  150 CONTINUE
C
C     SEE IF THERE ARE ANY ALL-STAND ROUTINES TO BE CALLED.
C     IF NOT, BRANCH OUT OF INTERSTAND INTERACTION PORTION OF PPE.
C
      IMDSUM=0
      DO ISTND=1,NOSTND
        IF (IPMODE(ISTND).GT.1) IMDSUM=IMDSUM+1
      ENDDO
      IF (PDEBUG) WRITE (JOPPRT,153) NOSTND,IMDSUM
  153 FORMAT (/' IN MAIN (ALSTD2) NOSTND=',I5,'; IMDSUM= ',I8)
      IF (IMDSUM.LE.0) GOTO 200
C
C     WE ARE AT MID CYCLE FOR ALL STANDS; CALL ALL-STAND
C     PROCESSOR NUMBER 2.
C
      CALL ALSTD2
C
C     ENTER A SECOND STAND LOOP TO FINISH THE MASTER CYCLE.
C
      WRITE (*,'(/'' Note: Starting phase 2 processing for cycle'',
     >           I3)') MICYC-1

      DO 190 IIISTN=1,NOSTND
      ISTND=ISNSRT(IIISTN)
      IF (IPMODE(ISTND).EQ.1) GOTO 190
C
C     REPORT PROGRESS TO SCREEN (AJM 9/05)
C
      WRITE (*,75) MICYC-1,MNCYC,IIISTN,TRIM(STDIDS(ISTND)),NOSTND,
     >             2,IPMODI
C
      IF (PDEBUG) WRITE (JOPPRT,154) IPMODI,LEORUN,ISTND
  154 FORMAT (/' IN MAIN, PHASE 2; IPMODI=',I2,'; LEORUN=',L2,
     >        '; ISTND=',I6)
      LSFLAG=LFLAG
      CALL GETSTD
      LFLAG=LSFLAG
C
C     APPLY MORTALITY ESTIMATES GENERATED BY THE MPB DISPERSAL MODEL
C
      CALL BMKILL
C
C     CALL GRADD TO UPDATE PROGNOSIS TREE LIST AND CALL GRCEND TO
C     LOAD REPORTED STAND STATISTICS.
C
      CALL GRADD (PDEBUG,IPMODI,LTMGO,LMPBGO,LDFBGO,
     &            LBWEGO,LCVATV,LBGCGO)
      CALL GRCEND
C
C     IF MULTISTAND POLICY SELECTION IS BEING DONE, PROCESS LABELS.
C
      IF (LHVALL) CALL HVFXLB
C
C     IF MASTER CLOCK HAS INDECATED END-OF-RUN, THEN: COMPUTE THE
C     STAND'S LAST YEAR STATISTICS...AND WRITE COVER OUTPUT.
C
      IF (LEORUN) THEN
         CALL GROEND
      ELSE
C
C        ELSE: STORE THE STAND ON THE DA FILE.
C
         CALL SPARLS (ISNKEY(ISTND,2),ISNKEY(ISTND,3))
         CALL PUTSTD
      ENDIF
  190 CONTINUE
  200 CONTINUE
C
C     THIS MARKS THE END OF THE STAND LOOP FOR BOTH PROCESSING MODES.
C
C     IF END-OF-RUN WAS SIGNALED; THEN:  PRINT OUTPUT CREATED BY
C     EXTENSIONS WHICH USE TEMPORARY FILES AND THEN BRANCH TO CALL
C     PPIN FOR MORE PROCESSING INSTRUCTIONS.
C
      IF (PDEBUG) WRITE (JOPPRT,209) LEORUN
  209 FORMAT (/' IN MAIN AT 209, LEORUN=',L2)
      IF (.NOT.LEORUN) GOTO 210
      CALL ESOUT (LFLAG)
      CALL MPBOUT
      CALL TMOUT
      CALL BWEOUT
      CALL RDROUT
      CALL GENPRT(JOSTND,KWDFIL)
      GOTO 20
  210 CONTINUE
C
C     IF A RUN PAUSE IS SCHEDULED, EXECUTE PAUSE LOGIC, AND
C     BRANCH TO PPIN FOR MORE PROCESSING INSTRUCTIONS.
C
      IF (PDEBUG) WRITE (JOPPRT,212) MICYC,LPAUSE(MICYC-1)
  212 FORMAT (/' IN MAIN AT 210, MICYC=',I4,' LPAUSE(MICYC-1)=',L2)
      LRESTR=LPAUSE(MICYC-1)
      IF (.NOT.LRESTR) GOTO 40
      CALL PPAUSE
      GOTO 20
      END

