      SUBROUTINE GPLOAD
      IMPLICIT NONE
C----------
C  **GPLOAD  DATE OF LAST REVISION:  07/31/08
C----------
C
C     LOAD THE MASTER MODE 2 INTERACTION LIST, IPMODE, ON A
C     STAND SPECIFIC BASIS.  ALL GLOBAL OPTIONS TRIGGER MODE 2
C     PROCESSING OF STANDS.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION TO PROGNOSIS.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPGPCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      INTEGER I,ISUB,IP,IVAL,LLVALU
      LOGICAL LLNULL
C
C     SET MODE 1 PROCESSING FOR ALL STANDS
C
      DO 40 I=1,NOSTND
      IPMODE(I)=1
   40 CONTINUE
C
C     AWAKEN GLOBAL OPTIONS FOR THIS CYCLE
C
      CALL GPCYCL
C
C     IF THERE ARE NON SCHEDULED, THEN BRANCH TO EXIT
C
      IF (MGPCUR.EQ.0) GOTO 999
C
C     LOOP THROUGH ALL OF THE GLOBAL OPTIONS
      DO 50 I=1,MGPCUR
      ISUB=MGPCYC(I)
C
C     IP POINTS TO THE FIRST PLACE ON THE LINKED LIST OF STANDS THAT
C     THIS GLOBAL OPTIONS APPLIES TO.
C
      IP=MIACT(4,ISUB)
   45 CONTINUE
C
C     IF THE LIST CONTENTS IS A 'NULL' MEMBER, THEN THE LIST IS
C     FINISHED.  BRANCH TO THE NEXT OPTION.
C
      IF (LLNULL(IP)) GOTO 50
C
C     OTHERWISE, THE MEMBER CONTAINS AN INDEX TO A STAND THAT IS TO
C     BE PROCESSED BY THE CURRENT GLOBAL OPTON.
C
      IVAL=LLVALU (IP)
C
C     SET THE PROCESSING MODE FOR THIS STAND EQUAL TO 2.
C
      IPMODE(IVAL)=2
C
C     GET THE NEXT MEMBER OF THE LINKED LIST.
C
      CALL LLNEXT(IP)
      GOTO 45
   50 CONTINUE
  999 CONTINUE
      RETURN
      END
