      SUBROUTINE PLBWTS (WEIGHT)
      IMPLICIT NONE
C----------
C  **PLBWTS--PPBASE   DATE OF LAST REVISION:  07/31/08              
C----------
C
C     WRITES THE LABELING AND WEIGHTS INFORMATION IN MACHINE READABLE
C     FORMAT TO DATA SET REFERENCE NUMBER JLBWTS.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--OCT 1987
C
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
COMMONS
C
      INTEGER J1,J2,NCNT,I
      REAL WEIGHT
C
C     IF THIS OPTION HAS BEEN TURNED OFF, OR IF THE DSRN IS ZERO,
C     THEN: RETURN
C
      IF (.NOT.LBWTS .OR. JLBWTS.LE.0) RETURN
C
C     WRITE THE STAND ID, MGMTID AND WEIGHT (RECORD TYPE 1).
C
      WRITE (JLBWTS,10) CLBWID,CISNUM(ISTND),STDIDS(ISTND),
     >                  MGMIDS(ISTND),WEIGHT
   10 FORMAT (A4,T7,A11,T19,'1',T21,'1',T23,A26,1X,A4,1X,E15.7)
C
C     CALL PPACTS (AN ENTRY IN OPLIST) TO WRITE THE ACTIVITIES.
C     THIS WILL BE RECORD TYPE 2.
C
      CALL PPACTS (JLBWTS,CLBWID,CISNUM(ISTND))
C
C     WRITE THE STAND POLICY LABELS (RECORD TYPE 3)...USE AS MANY
C     RECORDS AS NECESSARY TO WRITE THE LABELS (SPAN THE RECORDS).
C
      J1=1
      J2=LNSETS(ISTND)
      IF (J2.EQ.0) GOTO 50
      DO 30 NCNT=1,(((J2-J1)/56)+1)
      IF (J2.GT.(J1+61)) J2=J1+55
      WRITE (JLBWTS,20) CLBWID,CISNUM(ISTND),NCNT,SLSETS(ISTND)(J1:J2)
   20 FORMAT (A4,T7,A11,T19,'3',I2,T25,A)
      J1=J2+1
      J2=LNSETS(ISTND)
   30 CONTINUE
   50 CONTINUE
C
C     WRITE THE POLICY LABELS (RECORD TYPE 4)...USE AS MANY
C     RECORDS AS NECESSARY TO WRITE THE LABELS (SPAN THE RECORDS).
C
      IF (IXHRVP.EQ.0) GOTO 100
      DO 90 I=1,IXHRVP
      IF (IHVSTA(ISTND,I).LE.0) GOTO 90
      WRITE (JLBWTS,70) CLBWID,CISNUM(ISTND),I,HVPLAB(I)(1:LNHPLB(I,1))
   70 FORMAT (A4,T7,A11,T19,'4',I2,T25,A)
   90 CONTINUE
  100 CONTINUE
C
      RETURN
      END
