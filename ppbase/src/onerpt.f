      SUBROUTINE ONERPT (JOPPRT,KODE,IRPT,NCYC2,IONER,ICBRK,IPTS,CISNS)
      IMPLICIT NONE
C----------
C  **ONEPRT  DATE OF LAST REVISION:  07/31/08
C----------
C
C     GENERATE ONE STAND REPORT.
C
C     CREATES A LIST OF OUTPUT POINTERS, IONER, WHICH MAKES UP
C     REPORT IRPT OUT OF THE POSSIBLE NRPTS.  ALL OF THE NPRTS ARE
C     VARIOUS BRANCHES FORMED FROM ONE ROOT STAND.  IONER CONTAINS
C     VALUES OF IPTS.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1982
C
C     CALLED FROM: SNDTBS - PROCESS AND PRINT STAND TABLES.
C
C     JOPPRT= LOGICAL UNIT NUMBER FOR MAIN PRINTER OUTPUT.
C     KODE  = RETURN CODE FOR THE ROUTINE.
C             0.  NO ERRORS FOUND.
C             1.  A BRANCH COULD NOT BE TRACED THRU ALL OF ITS NODES.
C     IRPT  = THE ITH REPORT OUT OF A POSSIBLE N REPORTS.
C     NCYC2 = NUMBER OF CYCLES FOR THE ROOT STAND.
C     IONER = LIST OF POINTERS TO ELEMENTS OF A REPORT.
C     ICBRK = RETURNED AS POINTERS TO IPTS WHICH POINT TO BREAKS IN
C             ISNS ALONG CYCLE BOUNDARIES.  ALL OF THE VALUES OF
C             ICBRK REFER TO THE SAME ROOT STAND.
C     IPTS  = POINTER ARRAY TO ISNS.
C     CISNS = ARRAY OF INTERNAL STAND NUMBERS.
C
      CHARACTER CISNS(1)*11,CNEW*7,COLD*7,COLDL*7
C
      INTEGER IONER(1),ICBRK(1),IPTS(1),NCYC2,IRPT,KODE,JOPPRT
      INTEGER NOB,IFR,IP,IRCL,IL,IB,I,J
C
C     INITIALIZE SOME VARIALBES (MOSTLY FOR DEBUG PURPOSES).
C
      CNEW= ' '
      COLD= ' '
      COLDL=' '
C
C     SET RETURN CODE = 0, NO ERRORS
C
      KODE=0
C
C     LOAD IONER FROM THE BOTTOM TO THE TOP, THERE WILL BE NCYC2
C     MEMBERS OF IONER, AND NCYC2 WILL ALWAYS BE GREATER THAN 2.
C
      NOB=NCYC2
C
C     THE LAST CYCLE'S POINTERS ARE ALWAYS AT OFFSET IRPT WITHIN THE
C     LAST CYCLE.
C
      IFR=ICBRK(NOB)
      IONER(NOB)=IPTS(IFR+IRPT-1)
      NOB=NOB-1
      IFR=ICBRK(NOB)
      IP=IPTS(IFR+IRPT-1)
      IONER(NOB)=IP
C
C     INITIALIZE THE WITHIN ISN COLUMN POINTER AND BRANCHING COMPONENT
C     OF THE SEARCH ISN.
C
      IRCL=-1
      COLD=CISNS(IP)(5:11)
   10 CONTINUE
      NOB=NOB-1
C
C     IF NOB = 0, THE JOB IS FINISHED, RETURN.
C
      IF (NOB.LT.1) RETURN
      IFR=ICBRK(NOB)
      IL=ICBRK(NOB+1)-1
C
C     IF IFR AND IL ARE EQUAL, THERE ARE NO BRANCHES;
C     THEN: SIMPLY LOAD THE OUTPUT POINTER AND CONTINUE.
C
      IF (IFR.LT.IL) GOTO 40
      IONER(NOB)=IPTS(IFR)
      GOTO 10
   40 CONTINUE
C
C     SEARCH FOR THE OUTPUT-POINTER WHICH GOES WITH THE CURRENT LEG.
C     FIRST NARROW THE SEARCH TO THE OUTPUT-POINTERS ASSOCIATED WITH
C     THE CURRENT REPORT.
C
      IB=IFR+IRPT-1
C
C     LET CNEW EQUAL THE BRANCHING COMPONENT OF THE ISN ASSOCIATED
C     WITH THE OUTPUT-POINTER ASSOCIATED WITH IB.
C
      IP=IPTS(IB)
      CNEW=CISNS(IP)(5:11)
C
C     IF THE BRANCHING IS THE SAME FOR THIS OUTPUT (CNEW) AS FOR THE
C     LAST OUTPUT (COLD), THEN: WE HAVE FOUND THE CORRECT OUTPUT-
C     POINTER.
C
      IF (CNEW.NE.COLD) THEN
         IONER(NOB)=IP
         GOTO 10
      ENDIF
C
C     ELSE: WE NEED TO SEARCH, WITHIN THE POINTERS ASSOCIATED WITH
C     THE CURRENT CYCLE, FOR THE OUTPUT ASSOCIATED WITH THE BRANCH.
C     THE OUTPUT-POINTER WE NEED WILL EATHER HAVE EQUAL BRANCHING AS
C     THE LAST OUTPUT-POINTER, OR IT WILL BE EQUAL TO THE LAST ONE
C     WITHOUT THE LAST BRANCH NODE.  IN OTHER WORDS, IF THE STAND
C     BRANCHED DURING THE CYCLE WE ARE CURRENTLY PROCESSING, THE
C     BRANCHING COMPONENT OF THE ISN WHICH IS APPROPRIATE TO THE
C     CURRENT REPORT WILL BE THE EQUAL TO AN ISN WHICH DOES NOT
C     CONTAIN THE LAST NODE.
C
C     LET COLDL BE DEFINED AS THE SEARCH BRANCHING-COMPONENT-ISN LESS
C     THE LAST NODE.
C
C     IF THE COLUMN POINTER HAS NOT BEEN DEFINED (IRCL=-1),
C     THEN: DEFINE IT.
C
      IF (IRCL.LT.0) THEN
         COLDL=COLD
         CALL RITCOL (COLDL,IRCL)
         COLDL(IRCL:IRCL)='0'
         IRCL=IRCL-1
      ELSE
C
C        ELSE: AT THIS POINT IRCL = ZERO, THERE ARE NO NODES LEFT;
C        THEREFORE DO NOT 'DROP' ANY NODES.
C
         IF (IRCL.GT.0) THEN
            COLDL(IRCL:IRCL)='0'
            IRCL=IRCL-1
         ENDIF
      ENDIF
C
C     SEARCH FOR THE CORRECT BRANCH, THE SEARCH IS BOTTOM UP.
C
      I=-1
      DO 90 J=IFR,IB
      I=I+1
      IP=IPTS(IB-I)
      IF (J.NE.IFR) CNEW=CISNS(IP)(5:11)
      IF (CNEW.NE.COLD .AND. CNEW.NE.COLDL) GOTO 90
      IONER(NOB)=IP
      COLD=CNEW
      GOTO 10
   90 CONTINUE
C
C     IF THE DO-LOOP ENDS HERE, THE BRANCH COULD NOT BE TRACED--THE
C     REPORT IS INCOMPETE.  WRITE AN ERROR MSG, SET KODE=1, AND
C     RETURN CONTROL BACK TO CALLING ROUTINE.
C
      KODE=1
      WRITE (JOPPRT,100) IRPT,NCYC2,NOB,CNEW,COLD,COLDL,IRCL,IB,IL,
     >                   (ICBRK(I),I=1,NCYC2)
  100 FORMAT (/' IN ONERPT: IRPT=',I4,'; NCYC2=',I4,
     >        '; NOB=',I4,'; CNEW= ',A7,'; COLD=',A7,'; IRCL=',I4,
     >        '; IB=',I4,'; IL=',I4/
     >         ' ICBRK (1 TO NCYC2)...',5(/1X,10I6))
      CALL PPERRP (.TRUE.,13)
      RETURN
      END
