      SUBROUTINE HVIN (RECORD,KEYWRD,ARRAY,LNOTBK,IREAD,IRECNT)
      IMPLICIT NONE
C----------
C  **HVIN   DATE OF LAST REVISION:  07/31/08
C----------
C
C     CALLED FROM PPIN.  READS, COMPILES, AND STORES POLICIES
C     CALLS ALGCMP TO COMPILE THEM POLICY EXPRESSIONS.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
C     RECORD= CHARACTER*80 STRING.
C     KEYWRD= KEYWORD
C     ARRAY = ARRAY OF VALUES ON THE KEYWORD
C     LNOTBK= TRUE IF NOT BLANK
C     IREAD = READER DATA SET REFERENCE NUMBER.
C     IRECNT= RECORD COUNTER.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'PPEXCM.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      INTEGER IRECNT,IREAD,MXHVKY,NHRVP,IHV,IRC,LCLFT,LENCEX
      INTEGER IKEY,I,I1,IRPS,KODE,IARG
      PARAMETER (MXHVKY=5)
      LOGICAL LNOTBK(7)
      CHARACTER CLEFT*20,RECORD*80,CEXSTR*100
      CHARACTER*8 CHVKEY(MXHVKY),KEYWRD
      EQUIVALENCE (CEXPRS,CEXSTR)
      REAL ARRAY(7)
      DATA CHVKEY/'MSPLABEL','TARGET','PRIORITY','CREDIT',
     >            'UNITS'/
C
C     IF NO MULTISTAND TREATMENT POLICIES HAVE BEEN SPECIFIED, THEN
C     INITIALIZE LOGIC.
C
      IF (.NOT.LHVALL) THEN
         LHVALL=.TRUE.
         LEXPON=.TRUE.
      ENDIF
C
C     SET DEBUG SWITCH.
C
      LHVDEB=LNOTBK(5)
C
C     IF THE POLICY NUMBER IS CODED ON THE KEYWORD, USE IT IF IT
C     IS WITHIN RANGE.
C
      NHRVP=0
      IF (LNOTBK(1)) NHRVP=IFIX(ARRAY(1))
      IF (NHRVP.GT.MXHRVP) THEN
         WRITE (JOPPRT,10) NHRVP,MXHRVP
   10    FORMAT (/' ********   WARNING:  POLICY NUMBER ',I5,
     >          ' IS GREATER THAN ',I5,' AND WILL BE CHANGED.')
         CALL RCDSET (1,.TRUE.)
         NHRVP=0
      ENDIF
      IF (NHRVP.GT.0) GOTO 30
C
C     FIND THE FIRST VACANT POLICY POSITION.  IF NONE ARE
C     VACENT, THEN ISSUE AN ERROR MESSAGE.
C
      DO 20 IHV=1,MXHRVP
      IF (IHVTAB(IHV,1).EQ.0) THEN
         NHRVP=IHV
         GOTO 30
      ENDIF
   20 CONTINUE
      CALL PPERRP (.TRUE.,16)
      RETURN
   30 CONTINUE
C
C     SET UP THE SWITCH FOR USING EXTERNAL SELECTION OF HARVEST UNITS
C
      IF (LNOTBK(7)) IHVEXT=IFIX(ARRAY(7))
      IF (IHVEXT.GT.0 .AND. (LHIER.OR.NHRVP.GT.1)) THEN
         WRITE (JOPPRT,31)
   31    FORMAT (/'********   ERROR:  EXTERNAL HARVEST SELCTION CAN',
     >            ' NOT BE SIMULATED WHEN HIERARCHIES ARE BEING USED ',
     >            ' OR WHEN MORE THAN ONE POLICY IS SPECIFIED.')
         CALL RCDSET (4,.FALSE.)
      ENDIF
C
C     IF HIERARCHIES ARE ON, MAKE SURE NO MORE THAN 2 POLICIES ARE
C     PROCESSED.  (HOPEFULLY, THIS WILL BE FIXED LATER).
C
C     IF HIERARCHIES ARE OFF, THEN TURN THEM ON IF THE THIRD FIELD
C     IS NON-BLANK AND IF WE ARE PROCESSING THE FIRST POLICY.
C
      IF (LHIER) THEN
         IF (NHRVP.GT.2) THEN
            LHIER=.FALSE.
            WRITE (JOPPRT,35)
   35       FORMAT (/'********   ERROR:  ONLY 2 POLICIES CAN',
     >             ' BE SIMULATED WHEN HIERARCHIES ARE BEING USED.')
            CALL RCDSET (4,.FALSE.)
         ENDIF
      ELSE
         LHIER=NHRVP.EQ.1 .AND. ARRAY(4).NE.0.
         IF (LHIER .AND. IHVEXT.EQ.1) THEN
            WRITE (JOPPRT,31)
            CALL RCDSET (4,.FALSE.)
         ENDIF
      ENDIF
C
C     SET OUTPUT SWITCH TRUE IF SECOND FIELD IS NOT BLANK.
C
      LHVOUT(NHRVP)=LNOTBK(2)
      IF (LHVOUT(NHRVP)) THEN
         WRITE (JOPPRT,40) KEYWRD,NHRVP,' '
   40    FORMAT(/1X,A8,'   DEFINE MULTISTAND TREATMENT POLICY',I2,'. ',
     >       A,' REPORTS WILL BE GENERATED FOR THIS POLICY.')
      ELSE
         WRITE (JOPPRT,40) KEYWRD,NHRVP,' NO'
      ENDIF
      IF (LNOTBK(3)) THEN
         TRGSTS(9,NHRVP)=ARRAY(3)
         WRITE (JOPPRT,44) ARRAY(3)
   44    FORMAT (T13,'INITIAL VALUE OF OLDTARG =',E14.7)
      ENDIF
C
C     SET UP THE SWITCH FOR PRINTING MACHINE READABLE OUTPUT.
C
      IF (LNOTBK(6)) THEN
         JOHVDS(NHRVP)=IFIX(ARRAY(6))
         WRITE (JOPPRT,45) IFIX(ARRAY(6))
   45    FORMAT (T13,'MACHINE READABLE OUTPUT WILL BE WRITTEN TO ',
     >          'DATA SET REFERENCE NUMBER ',I3)
      ENDIF
      IF (IHVEXT.NE.0) THEN
         WRITE (JOPPRT,46) IHVEXT
   46    FORMAT (T13,'EXTERNAL SELECTION LOGIC SET TO METHOD ',I2)
         IF (LPRTCT) THEN
         WRITE (JOPPRT,47)
   47      FORMAT (T13,'THE "EXACT" OPTION HAS BEEN TURNED OFF.')
           LPRTCT=.FALSE.
         ENDIF 
      ENDIF
C
C     READ THE EXPRESSION AND PLACE IT INTO THE CEXPRS ARRAY.
C
      WRITE (JOPPRT,'()')
   50 CONTINUE
      CALL ALGEXP (CEXPRS,LENCEX,MXEXPR,CLEFT,LCLFT,RECORD,IRECNT,
     >             IREAD,JOPPRT,IRC)
C
C     RETURN CODES, IRC: 0=OK,1='END  ' WAS FOUND. OTHER ERRORS ARE
C     HANDLED INTERNALLY.
C
      IF (IRC.EQ.1) GOTO 3000
C
C     DECODE THE LEFT HAND TOKEN.  IT SHOULD BE A TARGET, MSPLABEL,
C     PRIORITY, OR CREDIT KEYWORD.
C
      IKEY=0
      DO 60 I=1,MXHVKY
      IF (CLEFT(1:8).EQ.CHVKEY(I)) THEN
         IKEY=I
         GOTO 80
      ENDIF
   60 CONTINUE
      IF (IKEY.EQ.0) THEN
         WRITE (JOPPRT,70)
   70    FORMAT (/' ******** ERROR: LEFT HAND SIDE OF EQUAL SIGN ',
     >           'IS NOT A MSPOLICY KEYWORD.')
         CALL RCDSET (1,.TRUE.)
         GOTO 50
      ENDIF
   80 CONTINUE
C
C     BRANCH TO EXECUTE CORRECT CODE FOR EACH KEYWORD.
C
      GOTO (1100,1200,1300,1400,1400),IKEY
C
C     OPTION 1: MSPLABEL
C
 1100 CONTINUE
C
C     STORE THE POLICY LABEL.  THE LABEL IS IN THE CEXPRS
C     ARRAY WHICH IS EQUIVALENCED TO WKSTR1...IT IS MOVED TO
C     RECORD FOR PROCESSING BY LBGET1.  THE LABELS SHOULD ALL BE
C     UNIQUE...THIS IS CHECKED LATER.
C
      I1 = 0
      DO I=1,80
         IF (I1.EQ.0) THEN
            IF (RECORD(I:I).EQ.'=') I1=1
         ELSE
            RECORD(I1:I1)=RECORD(I:I)
            I1=I1+1
         ENDIF
      ENDDO
      IF (I1+1.LE.80) RECORD(I1+1:)=' '

      IRPS=1
      CALL LBGET1 (IREAD,IRECNT,RECORD,IRPS,LNHPLB(NHRVP,1),
     >     WKSTR2,1,KODE)
      IF (KODE.EQ.1) WRITE (JOPPRT,1110) WKSTR2(1:40)
 1110 FORMAT (/' ******** ERROR: MSPLABEL WAS TOO LONG',
     >        '; THE LABEL NOW READS:'/T13,'MSPLABEL = ',A)
      IF (KODE.EQ.2) CALL ERRGRO (.FALSE.,2)
      HVPLAB(NHRVP)=' '
      HVPLAB(NHRVP)(1:LNHPLB(NHRVP,1))=WKSTR2(1:LNHPLB(NHRVP,1))
      LNHPLB(NHRVP,2)=INDEX(HVPLAB(NHRVP)(1:LNHPLB(NHRVP,1)),'.')-1
      IF (LNHPLB(NHRVP,2).LE.0) LNHPLB(NHRVP,2)=LNHPLB(NHRVP,1)
C
      IF (LHVDEB) WRITE (JOPPRT,1390) HVPLAB(NHRVP),LNHPLB(NHRVP,1),
     >                                LNHPLB(NHRVP,2)
 1390 FORMAT (' IN HVIN: HVPLAB=',A,' LNHPLB(.,1:2)=',2I5)
      GOTO 50
C
C     OPTION 2: TARGET...THE FORMULA FOR THE TARGET.
C
 1200 CONTINUE
C
C     CALL ALGCMP TO COMPILE THE EXPRESSION.
C
C     SAVE A POINTER TO THE OPCODE FOR THE TARGET.
C
      IHVTAB(NHRVP,1)=NPEXCD
      IARG=-1
      CALL ALGCMP (IRC,.FALSE.,CEXPRS,LENCEX,JOPPRT,LHVDEB,8000,
     >    IPTODO,MXPTDO,IPEXCD,NPEXCD,MPEXCD,PTSTV3,NPTST3,IARG,MPTST3)
C
C     IF WE GET A NON-ZERO RETURN CODE, ISSUE A GENERAL PURPOSE
C     ERROR MESSAGE AND BRANCH TO PROCESS THE NEXT EXPRESSION.
C
      IF (IRC.GT.0) THEN
         CALL ERRGRO (.TRUE.,12)
         IHVTAB(NHRVP,1)=0
      ENDIF
      GOTO 50
C
C     OPTION 3: PRIORITY
C
 1300 CONTINUE
C
C     CALL ALGCMP TO COMPILE THE EXPRESSION.
C
C     SAVE A POINTER TO THE OPCODE FOR THE PRIORITY.
C
      IHVTAB(NHRVP,2)=NPEXCD
      IARG=-1
      CALL ALGCMP (IRC,.FALSE.,CEXPRS,LENCEX,JOPPRT,LHVDEB,8000,
     >    IPTODO,MXPTDO,IPEXCD,NPEXCD,MPEXCD,PTSTV3,NPTST3,IARG,MPTST3)
C
C     IF WE GET A NON-ZERO RETURN CODE, ISSUE A GENERAL PURPOSE
C     ERROR MESSAGE AND BRANCH TO PROCESS THE NEXT EXPRESSION.
C
      IF (IRC.GT.0) THEN
         CALL ERRGRO (.TRUE.,12)
         IHVTAB(NHRVP,2)=0
      ENDIF
      GOTO 50
C
C     OPTION 4: UNITS OR CREDIT
C
 1400 CONTINUE
C
C     CALL ALGCMP TO COMPILE THE EXPRESSION.
C
C     SAVE A POINTER TO THE OPCODE FOR THE CUTUNITS.
C
      IHVTAB(NHRVP,3)=NPEXCD
      IARG=-1
      CALL ALGCMP (IRC,.FALSE.,CEXPRS,LENCEX,JOPPRT,LHVDEB,8000,
     >   IPTODO,MXPTDO,IPEXCD,NPEXCD,MPEXCD,PTSTV3,NPTST3,IARG,MPTST3)
C
C     IF WE GET A NON-ZERO RETURN CODE, ISSUE A GENERAL PURPOSE
C     ERROR MESSAGE AND BRANCH TO PROCESS THE NEXT EXPRESSION.
C
      IF (IRC.GT.0) THEN
         CALL ERRGRO (.TRUE.,12)
         IHVTAB(NHRVP,3)=0
      ENDIF
      GOTO 50
 3000 CONTINUE
C
C     CALL HVALID TO VALIDATE THE OPTIONS JUST ENTERED.
C
      CALL HVALID (NHRVP,JOPPRT)
C
C     COMPUTE THE MAX POLICY NUMBER SO FAR DEFINED
C
      IXHRVP=0
      DO 3010 IHV=1,MXHRVP
      IF (IHVTAB(IHV,1).EQ.0) GOTO 3010
      IXHRVP=IHV
 3010 CONTINUE
C
C     IF THE MAX MULTISTAND POLICY SO-FAR DEFINED IS ZERO, THEN
C     TURN OFF SELECTION LOGIC.
C
      LHVALL=IXHRVP.GT.0
C
C     IF MULTISTAND SELECTION IS OFF, INSURE THAT HIERARCHIES ARE
C     ALSO OFF.
C
      IF (.NOT.LHVALL) LHIER=.FALSE.
C
C     IF HIERARCHIES ARE ON, WRITE APPROPRATE MSG.
C
      IF (LHIER) THEN
         IF (IXHRVP.EQ.1) THEN
            WRITE (JOPPRT,3020) IXHRVP,
     >      'CAN NOT BE SELECTED UNDER ANY OTHER POLICY.'
         ELSE
            WRITE (JOPPRT,3020) IXHRVP,
     >      'ARE LIMITED TO STANDS NOT SELECTED UNDER POLICY 1.'
 3020       FORMAT (/' ********   NOTE:  POLICY HIERARCHY IN USE.  ',
     >              'STANDS SELECTED UNDER POLICY',I3,' ',A)
         ENDIF
      ENDIF
      RETURN
      END
