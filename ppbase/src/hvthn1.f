      SUBROUTINE HVTHN1
      IMPLICIT NONE
C----------
C  **HVTHN1--PPBASE  DATE OF LAST REVISION:  03/05/10
C----------
C
C     CALLED FROM HVALOC...COMPUTE THE YIELD FROM A STAND IF IT IS
C     NOT SELECTED.  THEN RELOAD THE STAND.
C     REMEMBER:  THIS ROUTINE IS CALLED WITHIN A DO OVER STANDS LOOP.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'HVDNCM.F77'
C
C
      INCLUDE 'PPEXCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'ECON.F77'
C
C
COMMONS
C
      LOGICAL LMORE
      INTEGER IBA
C
      INTEGER IEVLST(3,25),MAELNK(2,50),LENWK1,IRC,IC,IHV
      REAL STAGEA,STAGEB
C
C     MAKE SURE THAT THE STAND VALUE "SELECTED" IS SET "NO" (0).
C
      CALL EVSET4 (9,0.0)
C
C     IF HIERARCHIES ARE BEING USED, MAKE SURE THAT THE FIRST
C     POLICY LABEL IS REMOVED FROM THE STAND LABEL SET.
C
      IF (LHIER) THEN
         WKSTR1=SLSET
         LENWK1=LENSLS
         WKHPLB=HVPLAB(1)(1:LNHPLB(1,2))
         CALL LBRNLS (LENWK1,WKSTR1,LNHPLB(1,2),WKHPLB,
     >                LENSLS,SLSET,IRC)
      ENDIF
C
C     CALL EVMON TO MAKE SURE THAT ALL OF THE ACTIVITIES THAT WOULD
C     BE SCHEDULED UNDER THE CIRCUMSTANCES ARE SCHEDULED.  EVMON IS
C     CALLED WITH A 1 FOR PHASE ONE, AND A 2 TO SUPPRESS CALLING
C     PPEVMI...THEREBY BLOCKING ANY ATTEMPS TO CREATE BRANCHES.
C
C     IF THERE IS MORE THAN ONE ALTERNATIVE ACTIVITY GROUP FOR A GIVEN
C     LOGICAL EXPRESSION, THE EVENT MONITOR WILL PICK ONE ACTIVITY GROUP
C     IF THE ACTIVITY GROUP LABEL IS IN THE STAND LABEL AND IF IT IS
C     THE ONLY ACTIVITY GROUP WHOSE LABEL IS IN THE STAND LABEL.  IN
C     OTHERWORDS, IF THERE IS MORE THAN ONE ALTERNATIVE RESPONSE TO
C     NOT BEING SELECTED, THE PROGRAM WILL IGNORE ALL OF THE
C     ALTERNATIVES AND COMPUTE THE "THIN" YIELD WITHOUT THE IGNORED
C     ACTIVITIES...AND NO ERROR MSG IS WRITTEN.  THE ASSUMPTION IS
C     THAT NO MULTI-ACTIVITY-GROUP EVENTS WILL BE CODED FOR THE
C     "NOT SELECTED" CASE.
C
      CALL EVMON (1,2)
C
C     MAKE SURE THAT CHEAPO OUTPUT WILL NOT BE WRITTEN.
C
      LECON=.FALSE.
C
C     SAVE LAST CYCLE DENSITY STATS.
C
      OLDTPA=TPROB
      OLDAVH=AVH
      OLDBA=BA
      RELDM1=RELDEN
      ORMSQD=RMSQD
C
C     COMPUTE PRE-CUTS SDI
C
      CALL SDICAL(BTSDIX)
      CALL SDICLS(0,0.,999.,1,SDIBC,SDIBC2,STAGEA,STAGEB,0)
C
C     CALL CUTS TO CALCULATE THE REMOVAL.
C
      CALL CUTS
C
C     CALL DENSE TO COMPUTE POST THIN DENSITY STATS AND SAVE THEM.
C     (CODE SIMILAR TO THAT FOUND IN SUBROUTINE TREGRO).
C
C     STORE THE REMOVED TPA IN WK4 FOR USE IN THE SECOND CALL TO THE
C     EVENT MONITOR (SPMCDBH INVOLVING CUT TREES).
C
      DO IC=1,MAXTRE
         WK4(IC)=WK3(IC)
      ENDDO

      CALL DENSE
      ATAVD=RMSQD
      ATAVH=AVH
      ATBA=BA
      ATCCF=RELDEN
      ATTPA=TPROB
      CALL SDICAL(ATSDIX)
      CALL SDICLS(0,0.,999.,1,SDIAC,SDIAC2,STAGEA,STAGEB,0)
C
C     COMPUTE THE STRUCTURAL STAGE OF THE STAND AFTER TO CUTS.
C
      IBA = 2
      CALL SSTAGE (IBA,ICYC,.TRUE.)
C
C     RECALL THE EVENT MONITOR FOR THE POST-THIN EVALUATION.  THE
C     COMPLETE EVENT MONITOR MUST BE CALLED SO THAT USER-DEFINED
C     VARIABLES CAN BE CORRECTLY HANDELED...REMEMBER, THEN CAN BE
C     SET AS ACTIVITIES.
C
      CALL EVMON (2,2)
C
C     COMPUTE THE YIELD OF THE "NOT SELECTED" RESPONSE FOR ALL OF
C     THE POLICIES.
C
      DO 30 IHV=1,IXHRVP
C
C     IF THE POLICY IS NOT SET, OR IF THE STAND CANNOT APPLY
C     TO THIS POLICY, THEN SET THE YIELD EQUAL TO 0.0
C
      IF (IHVTAB(IHV,1).EQ.0 .OR. IHVSTA(ISTND,IHV).EQ.0) THEN
         HVTHIN(ISTND,IHV)=0.0
      ELSE
C
C        COMPUTE THE UNITS FOR THE POLICY AND STORE IN HVTHIN.
C
         CALL ALGEVL (LREG,MXLREG,XREG,MXXREG,IPEXCD(IHVTAB(IHV,3)),
     >      MPEXCD-IHVTAB(IHV,3)+1,IY(1),IY(ICYC),LHVDEB ,JOPPRT,IRC)
         IF (IRC.GT.0) THEN
            WRITE (JOPPRT,'(/'' COMPUTING CREDIT FAILED.'')')
            CALL ERRGRO (.TRUE.,21)
            CALL RCDSET (3,.TRUE.)
            IHVSTA(ISTND,IHV)=0
            HVTHIN(ISTND,IHV)=0.0
         ELSE
            HVTHIN(ISTND,IHV)=XREG(1)
         ENDIF
         IF (LHVDEB) WRITE (JOPPRT,10) ISTND,IHV,IRC,HVTHIN(ISTND,IHV)
   10    FORMAT (/' IN HVTHN1: ISTND=',I4,' IHV=',I2,' IRC=',I2,
     >            ' HVTHIN=',E15.7)
      ENDIF
   30 CONTINUE
C
C     IF THE NBDENEFF OPTION IS ON (NEIGHBOR DENSITY EFFECTS), THEN
C     SAVE SOME CRITICAL STATISTICS FOR THE "NOT SELECTED" CONDITION.
C
      IF (LNEDNG) THEN
         HVDBA (ISTND,1)=BA
         HVDAHT(ISTND,1)=AVH
         HVDCCF(ISTND,1)=RELDEN
         IF (LHVDEB) WRITE (JOPPRT,110) ISTND,BA,AVH,RELDEN
  110    FORMAT (/' IN HVTHN1: ISTND=',I4,' BA=',F8.2,'; AVH=',
     >          F8.2,'; RELDEN=',F8.2)
      ENDIF
      RETURN
      END
