      SUBROUTINE PPLDEV
      IMPLICIT NONE
C----------
C  **PPLDEV  DATE OF LAST REVISION:  07/31/08
C----------
C
C     CALLED FROM EVTSTV...LOAD THE EVENT MONITOR FOR THE CURRENT
C     STAND WITH VARIABLES THAT THE PPE IS RESPONSIBLE FOR.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1982.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
COMMONS
C
      INTEGER IRC,I
      REAL A,WEIGHT
C
C
      IF (PDEBUG) WRITE (JOPPRT,'('' IN PPLDEV, ISTND='',I5)') ISTND
C
C     THIS ROUTINE IS RESPONSIBLE FOR DEFINING THE FOLLOWING
C     VARIABLES FOR THE EVENT MONITOR (SEE GROUP 4 IN EVTSTV).
C
C     410 BRCHWT    BRAHCH WEIGHT (BRCHWT VARIABLE FROM THE PPE).
C
      TSTV4(10)=BRCHWT(ISTND)
      LTSTV4(10)=.TRUE.
C
C     411 STNDAREA  STAND AREA IN ACRES (PPE)
C
      LTSTV4(11)=LAREAS
      TSTV4(11)=AREAS(ISTND)
C
C     412 RDACCESS  DISTANCE TO ROAD (PPE)
C
      CALL SPRDIS (STDIDS(ISTND),A,IRC)
      IF (IRC.EQ.0) TSTV4 (12)=A
      LTSTV4(12)=IRC.EQ.0
C
C     413 STARTYR   MASTER STARTING YEAR (PPE)
C
      LTSTV4(13)=.TRUE.
      TSTV4(13)=FLOAT(MIY(1))
C
C     414 STANDWT   SAMPLING WEIGHT FOR THE STAND COMPUTED BY PPE
C
      CALL PPWEIG (ISTND,WEIGHT)
      LTSTV4(14)=.TRUE.
      TSTV4(14)=WEIGHT
C
C     415 NODES     THE NUMBER OF NODES IN THE DECISION TREE.
C
      CALL RITCOL (CISNUM(ISTND)(5:11),I)
      TSTV4(15)=FLOAT(I)
      LTSTV4(15)=.TRUE.
C
      RETURN
      END
