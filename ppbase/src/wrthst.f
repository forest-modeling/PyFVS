      SUBROUTINE WRTHST (JIN,JOUT)
      IMPLICIT NONE
C----------
C  **WRTHST  DATE OF LAST REVISION:  07/31/08
C----------
C
C     WRITE THE HISTORY FILE IN MEMORY TO DISK.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JULY 1982
C
C     WRITES THE CONTENTS OF MEMORY WHICH COMPRISE THE HISTORY FILE.
C     DIFFERS FROM HISTWT IN THAT IT IS DESIGNED TO WRITE ALL OF THE
C     RECORDS AT ONCE (HISTWT WRITES ONE RECORD EVERY TIME A DIRECT
C     ACCESS FILE IS UPDATED).  WRTHST IS DESIGNED TO RESTORE THE
C     HISTORY FILE AFTER IT HAS BEEN READ BY HISTRD.
C     WRTHST ATTEMPTS TO INSURE THAT ALL OF THE ESSENTIAL DATA STORED
C     IN THE DATA SET REFERENCED BY JIN ARE TRANSFERED TO THE NEW FILE
C     REFERENCED BY JOUT.  JIN AND JOUT MAY OR MAY NOT BE EQUAL.
C
C     CALLED FROM PPIN   - PROCESS PPE KEYWORDS.
C
C     JIN   = DATA SET REFERENCE NUMBER TO READ HISTORY FILE.
C     JOUT  = DATA SET REFERENCE NUMBER TO WRITE HISTORY FILE.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'PPCISN.F77'
C
C
COMMONS
C
      INTEGER JOUT,JIN,I
      REAL ALL(15000)
      INTEGER ISNUMS(3000),ICYCS(3000),LOCS(3000)
      INTEGER IELTS(3000),IPTS(3000)
      EQUIVALENCE (ALL(1),BFV(1)),(ALL(1),ISNUMS(1)),
     >            (ALL(3001 ),ICYCS(1)),
     >            (ALL(6001 ),LOCS (1)),
     >            (ALL(9001 ),IELTS(1)),
     >            (ALL(12001),IPTS (1))
C
C     PRINT DEBUG
C
      IF (PDEBUG) WRITE (JOPPRT,10) JIN,JOUT,NMPRTD,LHIST,NOSTND
   10 FORMAT (/' IN WRTHST JIN=',I3,' JOUT=',I3,
     >         ' NMPRTD=',I6,' LHIST=',L2,' NOSTND=',I5)
C
C     INSURE ALL OF THE HISTORY DATA HAS BEEN READ.
C
      CALL HISTRD (JIN)
C
C     REWIND OUTPUT HISTORY FILE.
C
      REWIND JOUT
C
C     IF THE INPUT AND OUTPUT FILES ARE EQUAL TO EACH OTHER AND
C     EQUAL TO JOHIST, INDECATE THAT THE HISTORY FILE IS OPEN FOR
C     OUTPUT.
C
      LHIST=.NOT.(JIN-JOUT.EQ.0 .AND. JIN.EQ.JOHIST)
C
C     WRITE THE PRINT-DATA RECORDS.
C
      IF (NMPRTD.EQ.0) GOTO 200
      WRITE (JOUT,110) (ISNUMS(I),CISNS(I),
     >      LOCS(I),IELTS(I),ICYCS(I),I=1,NMPRTD)
  110 FORMAT (1X,I5,A11,1X,3I5)
  200 CONTINUE
C
C     PRINT DEBUG.
C
      IF (PDEBUG) WRITE (JOPPRT,210) NOSTND,NMPRTD
  210 FORMAT (/' IN WRTHST, NOSTND=',I5,'; NMPRTD=',I5)
      RETURN
      END
