      SUBROUTINE HVPROJ(ICALL)
      IMPLICIT NONE
C----------
C  **HVPROJ--PPBASE  DATE OF LAST REVISION:  07/31/08
C----------
C
C     SIMULATES GROWTH BEYOND THE POST CUTS CONDITIONS FOR SOME
C     YEARS.  THE ROUTINE IS DESIGNED TO PROVIDE PROJECTED INFORMATION
C     BEYOND THE CONDITIONS IMMEDIATLY FOLLOWING CUTS FOR USE
C     IN COMPUTING HARVEST PICKS.
C
C     CALLED FROM HVALOC
C
C     ICALL = 1 WHEN CALLED DURING TRIAL "THIN" (NOT SELECTED)
C             2 WHEN CALLED DURING TRIAL "HARVEST" (SELECTED)
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - AUG 2003
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'HVDNCM.F77'
C
C
      INCLUDE 'PPEXCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C
      INTEGER KFMS(4)          ! FUEL MODEL CODES
      REAL FMWTS(4)            ! CORRESPONDING WEIGHTS
      REAL FMLOADS(4)          ! 1HR,10HR,100HR,1000HR LOADS
      REAL VALS(5)             ! HABTYPE, FORTYP, RTPA, FIRE, FIREYEAR FROM EVENT MONITOR
      INTEGER ICALL,IYR,IRC
      REAL TOPHT,CANCOV,CRBLKD,CRBSHT,POKL,POVK

C     IF THE EXTERNAL SELECTION LOGIC IS NOT BEING USED, THEN RETURN

      IF (LHVDEB) WRITE (JOPPRT,'(/'' IN HVPROJ ICALL IHVEXT'',2I4)')
     >                                          ICALL,IHVEXT
      IF (IHVEXT.NE.1) RETURN

C     THE FOLLOWING CODE IS AN ABSTRACTION OF THE CODE FROM GRINCR.
C     NOT EVERY PART OF GRINCR IS INCLUDED AND THIS MIGHT BE A MISTAKE...
C     THAT IS, IF THE "PROJECT AHEAD" LOGIC DIFFERES FROM "ACTUAL
C     SIMULATION" IN SOME IMPORTANT WAYS, THEN THIS CODE SHOULD BE
C     REVISED TO INCLUDE MORE ELEMENTS OF THE "ACTUAL SIMULATION".
C     ON THE OTHERHAND, PERHAPS WE CAN DO THE "PROJECT AHEAD" WITH
C     RECORD TRIPPLING TURNED OFF AND TRIM OUT OTHER FAT.

C     SET THE CYCLE LENGTH AND OPTION POINTERS FOR THE CYCLE.

      IF(ICYC .GE. 2) THEN
       OLDFNT = IY(ICYC)-IY(ICYC-1)
      ELSE
        OLDFNT = FINT
      ENDIF
      IFINT=IY(ICYC+1)-IY(ICYC)
      FINT=IFINT

      LTRIP=(ICYC.LE.ICL4.AND.ITRN.LE.(MAXTRE/3).AND..NOT.NOTRIP)

      CALL FMSDIT
      CALL EVLDX(VALS(4),1,420,IRC) ! FIRE IN LAST PERIOD

      CALL COMCUP
      CALL DGDRIV
      CALL HTGF
      CALL REGENT(.FALSE.,1)
      CALL MORTS
      IF (LTRIP.AND.ITRN.GT.0) THEN
         CALL TRIPLE
         ITRN=ITRN*3
         CALL REASS
         IREC1=IREC1*3
      ENDIF
      DO IYR = IY(ICYC),MIN(IY(ICYC)+3,IY(ICYC+1)-1)
         CALL FMPPHV(IYR,CRBSHT,CRBLKD,CANCOV,KFMS,FMWTS,POKL,POVK,
     >               FMLOADS)
         TOPHT=AVH
         CALL EVLDX(VALS(1),1,126,IRC) ! HABTYPE
         CALL EVLDX(VALS(2),1,141,IRC) ! FORTYP
         CALL EVLDX(VALS(3),1,209,IRC) ! REMOVED TPA
         CALL EVLDX(VALS(5),1,423,IRC) ! FIREYEAR
         WRITE (83,10) TRIM(NPLT),IYR,ICALL,CRBSHT,CRBLKD,CANCOV,
     >                 TOPHT,POKL,POVK,KFMS,FMWTS,FMLOADS,
     >                 INT(VALS(1)),INT(VALS(2)),VALS(3),INT(VALS(4)),
     >                 INT(VALS(5))
   10    FORMAT ('"',A,'",',I4,',',I2,',',F5.1,',',F8.5,',',F5.0,',',
     >           F5.0,',',F5.2,',',F7.0,',',4(I2,','),4(F5.3,','),
     >           4(F8.2,','),2(I4,','),F7.1,',',I2,',',I4)
      ENDDO
      IF (LHVDEB) WRITE (JOPPRT,'(/'' LEAVING HVPROJ'')')
      RETURN
      END
