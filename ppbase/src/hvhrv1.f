      SUBROUTINE HVHRV1
      IMPLICIT NONE
C----------
C  **HVHRV1--PPBASE  DATE OF LAST REVISION:  03/05/10
C----------
C
C     CALLED FROM HVALOC...COMPUTE THE YIELD(S) FROM A STAND IF IT IS
C     SELECTED FOR UNDER EACH UNIQUE POLICY.
C     REMEMBER:  THIS ROUTINE IS CALLED WITHIN A DO OVER STANDS LOOP.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'PPEXCM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'OPCOM.F77'
C
C
      INCLUDE 'ECON.F77'
C
C
      INCLUDE 'HVDNCM.F77'
C
C
COMMONS
C
      LOGICAL LMORE
      INTEGER IBA,IDELAB,IHV,INQ,IAPPLY,NDEL,K,LNWKS1,IRC,IC,IMCCUT
      REAL STAGEA,STAGEB
C
      DIMENSION IDELAB(15)
C
C     CREATE IUNLAB...SEE COMMON HVSCM1 FOR A DESCRIPTION.
C
      DO 50 IHV=1,IXHRVP
      IF (IHVTAB(IHV,1).EQ.0) THEN
         IUNLAB(IHV)=0
      ELSE
         IUNLAB(IHV)=IHV
         IF (IHV.GT.1) THEN
            DO 40 INQ=1,(IHV-1)
            IF (LNHPLB(INQ,2).EQ.LNHPLB(IHV,2)) THEN
               IF (HVPLAB(INQ)(1:LNHPLB(INQ,2)) .EQ.
     >            HVPLAB(IHV)(1:LNHPLB(IHV,2)) )  THEN
                  IUNLAB(IHV)=INQ
                  GOTO 45
               ENDIF
            ENDIF
   40       CONTINUE
   45       CONTINUE
         ENDIF
      ENDIF
   50 CONTINUE
      IF (LHVDEB) WRITE (JOPPRT,'(/'' IN HVHRV1, IUNLAB='',15I3
     >   )')  (IUNLAB(INQ),INQ=1,IXHRVP)
C
C     LOOP OVER POLICIES.
C
      IAPPLY=0
      DO 100 INQ=1,IXHRVP
C
C     IF THE CURRENT POLICY IS UNDEFINED, SKIP.
C
      IF (IUNLAB(INQ).LE.0) GOTO 100
C
C     IF THE STATUS CODE IS ZERO FOR THIS STAND, SET THE
C     YIELD FOR THIS POLICY EQUAL TO ZERO AND GO ON TO THE
C     NEXT POLICY.
C
      IF (IHVSTA(ISTND,INQ).EQ.0) THEN
         HVYLDS(ISTND,INQ)=0.0
      ELSEIF (IAPPLY.NE.IUNLAB(INQ)) THEN
         IF (IAPPLY.GT.0) CALL GETSTD
         IAPPLY=IUNLAB(INQ)
C
C        DELETE, FROM THE STAND POLICY LABEL, ALL OF THE OTHER
C        MSPLABELS.
C
         NDEL=0
         IF (LHVDEB) WRITE (JOPPRT,55) INQ,IAPPLY,ISTND,CISNUM(ISTND),
     >      HVPLAB(INQ)(1:LNHPLB(INQ,1)),SLSET(1:LENSLS)
   55    FORMAT (' IN HVHRV1: INQ=',I3,' IAPPLY=',I3,' ISTND=',I5,
     >           ' CISNUM= ',A10,' HVPLAB=',A/T13,'SLSET=',A)
         DO 70 IHV=1,IXHRVP
         IF (IAPPLY.NE.IABS(IUNLAB(IHV))) THEN
C
C           KEEP TRACK OF THE DELETIONS...SEVERAL CALLS TO LBRNLS CAN
C           BE AVOIDED.
C
            IF (NDEL.GT.0) THEN
               DO 60 K=1,NDEL
               IF (IHV.EQ.IDELAB(K)) GOTO 70
   60          CONTINUE
            ENDIF
            NDEL=NDEL+1
            IDELAB(NDEL)=IHV
            WKSTR1=SLSET
            LNWKS1=LENSLS
            WKHPLB=HVPLAB(IHV)
            CALL LBRNLS (LNWKS1,WKSTR1,LNHPLB(IHV,2),WKHPLB,
     >                   LENSLS,SLSET,IRC)
         ENDIF
   70    CONTINUE
         IF (LHVDEB) WRITE (JOPPRT,'('' IN HVHRV1: NEW SLSET='',
     >       A)')  SLSET(1:LENSLS)
C
C        MAKE SURE THAT "SELECTED" IS SET EQUAL TO "YES"
C
         CALL EVSET4 (9,1.0)
C
C        MAKE SURE THAT CHEAPO OUTPUT WILL NOT BE WRITTEN.
C
         LECON=.FALSE.
C
C        GET THE SSTAGE AND SDI INFO.
C
         IBA = 1
         CALL SSTAGE (IBA,ICYC,.TRUE.)
         CALL SDICAL(BTSDIX)
         CALL SDICLS(0,0.,999.,1,SDIBC,SDIBC2,STAGEA,STAGEB,0)

C
C        CALL THE EVENT MONITOR FOR PHASE ONE, AND IGNORE BRANCHING.
C
         CALL EVMON (1,2)
C
C        CALL CUTS TO FIND OUT WHAT THE "SELECTED" CUT IS FOR
C        THIS POLICY.
C
         CALL CUTS
C
C        STORE THE REMOVED TPA IN WK4 FOR USE IN THE SECOND CALL TO THE
C        EVENT MONITOR (SPMCDBH INVOLVING CUT TREES).
C
         DO IC=1,MAXTRE
            WK4(IC)=WK3(IC)
         ENDDO
C
C        CALL DENSE TO COMPUTE POST THIN DENSITY STATS AND SAVE THEM.
C        (CODE SIMILAR TO THAT FOUND IN SUBROUTINE GRINCR).
C
         CALL DENSE
         ATAVD=RMSQD
         ATAVH=AVH
         ATBA=BA
         ATCCF=RELDEN
         ATTPA=TPROB
         CALL SDICAL(ATSDIX)
         CALL SDICLS(0,0.,999.,1,SDIAC,SDIAC2,STAGEA,STAGEB,0)
C
C        COMPUTE THE STRUCTURAL STAGE OF THE STAND AFTER TO CUTS.
C
         IBA = 2
         CALL SSTAGE (IBA,ICYC,.TRUE.)
C
C        CALL EVMON FOR PHASE 2 CALCULATIONS.
C
         CALL EVMON (2,2)
C
C        FIND OUT IF THE STAND IS "CLEAR CUT".
C
         CALL HVCCUT (IMCCUT)
C
C        LOOP THROUGH THE REST OF THE POLICIES AND COMPUTE
C        THE YIELD FOR ALL POLICIES THAT ARE ACTIVE FOR THIS
C        STAND AND THAT MATCH THIS POLICY LABEL UP TO THE
C        OF A PERIOD.
C
         DO 80 IHV=INQ,IXHRVP
         IF (IHVSTA(ISTND,IHV).EQ.0) THEN
            HVYLDS(ISTND,IHV)=0.0
         ELSEIF (IUNLAB(IHV).EQ.IAPPLY) THEN
C
C           EVALUATE THE CREDIT UNDER THE POLICY.
C
            CALL ALGEVL (LREG,MXLREG,XREG,MXXREG,IPEXCD(IHVTAB(IHV,3)),
     >                   MPEXCD-IHVTAB(IHV,3)+1,IY(1),IY(ICYC),LHVDEB ,
     >                   JOPPRT,IRC)
            IF (IRC.GT.0) THEN
               CALL RCDSET (3,.TRUE.)
               CALL ERRGRO (.TRUE.,21)
               HVYLDS(ISTND,IHV)=0.0
            ELSE
C
C              CREDIT THE APPROPRIATE POLICY.
C
               HVYLDS(ISTND,IHV)=XREG(1)
C
C              IF THE STAND IS CLEAR CUT, THE STATUS CODE
C              SHOULD BE SET EQUAL TO -1, OTHERWISE +1.
C
               IHVSTA(ISTND,IHV)=IMCCUT
            ENDIF
            IF (LHVDEB) WRITE (JOPPRT,10) ISTND,IHV,IRC,
     >                  IHVSTA(ISTND,IHV),HVYLDS(ISTND,IHV)
   10       FORMAT (/' IN HVHRV1: ISTND=',I4,' IHV=',I2,'; IRC=',I2,
     >             '; IHVSTA=',I4,'; HVYLDS=',E15.7)
C
C           TURN THE SIGN BIT ON IN IUNLAB TO INDICATE THAT THIS
C           POLICY HAS BEEN COMPLETED.
C
            IUNLAB(IHV)=-IUNLAB(IHV)
         ENDIF
   80    CONTINUE
      ENDIF
  100 CONTINUE
C
C     IF THE NBDENEFF OPTION IS ON (NEIGHBOR DENSITY EFFECTS), THEN
C     SAVE SOME CRITICAL STATISTICS FOR THE "SELECTED" CONDITION.
C
      IF (LNEDNG) THEN
         HVDBA (ISTND,2)=BA
         HVDAHT(ISTND,2)=AVH
         HVDCCF(ISTND,2)=RELDEN
         IF (LHVDEB) WRITE (JOPPRT,110) ISTND,BA,AVH,RELDEN
  110    FORMAT (/' IN HVHRV1: ISTND=',I4,' BA=',F8.2,'; AVH=',
     >          F8.2,'; RELDEN=',F8.2)
      ENDIF
C
C     MAKE SURE THE VALUES OF IUNLAB ARE POSITIVE (IUNLAB IS USED
C     IN HVREPS)
C
      DO 120 IHV=1,IXHRVP
      IUNLAB(IHV)=IABS(IUNLAB(IHV))
  120 CONTINUE
      RETURN
      END
