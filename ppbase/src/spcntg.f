      SUBROUTINE SPCNTG (ISDWK1,NLST,NNEIG,CONTIG,IRC)
      IMPLICIT NONE
C----------
C  **SPCNTG DATE OF LAST REVISION:  07/31/08
C----------
C     FINDS CONTIGUOUS STANDS AND ACRES TO A SUBJECT STAND.
C
C     ISDWK1= A LIST OF STAND INDICIES WHERE THE FIRST MEMBER
C             IS THE SUBJECT STAND.  UPON RETURN THE LIST
C             IS REORDERED SUCH THAT ALL OF THE STANDS IN THE
C             LIST THAT FORM A CONTIGUOUS AREA TO THE SUBJECT
C             STAND ARE AT THE TOP.  INDICIES TO STANDS THAT
C             DO NOT BORDER ANY STANDS IN THE TOP OF THE LIST
C             ARE STORED IN THE BOTTOM OF THE LIST.
C     NLST  = LENGTH OF ISDWK1, SET BY CALLING ROUTINE
C     NNEIG = LENGTH OF "TOP" PORTION OF ISDWK1, SET BY THIS ROUTINE
C     CONTIG= AREA IN ACRES OF CONTIGUOUS AREA
C     IRC   = RETURN CODES: 0=OK, 1=MISSING DATA.
C
      REAL CONTIG,X
      INTEGER IRC,NNEIG,NLST,II,INEI,I,JJ,J
      INTEGER ISDWK1(NLST)
C
C     SET SIZE OF TOP PORTION OF LIST.
C
      NNEIG=1
C
C     FIND INITIAL CONTIGUOUS AREA.
C
      CALL SPLAAR (ISDWK1(1),CONTIG,IRC)
      IF (IRC.GT.0) THEN
         IRC=1
         RETURN
      ELSE
         IF (NLST.EQ.1) RETURN
      ENDIF
C
C     DO UNTIL (NNEIG REACHES THE BOTTOM OF THE LIST) OR
C              (LIST MEMBER I IS NOT A NEIGHBOR OF ANY
C               STAND FROM NNEIG+1 TO THE END OF THE LIST).
C
      DO 30 II=1,NLST
      INEI=0
      IF (NNEIG.GE.NLST) GOTO 40
      I=ISDWK1(II)
      DO 20 JJ=NNEIG+1,NLST
      J=ISDWK1(JJ)
C
C     FIND OUT IF STAND I SHARES A COMMON BORDER WITH STAND J
C
      CALL SPNBBD (I,J,X,IRC)
C
C     IF THEY DO, THEN INCREMENT NNEIG.  IF JJ IS GREATER THAN NNEIG
C     THEN EXCHANGE THE POINTER STORED IN ISDWK1(JJ) WITH THE ONE
C     STORED IN LOCATION ISDWK1(NNEIG).
C
      IF (IRC.EQ.0 .AND. X.GE.0.0) THEN
         INEI=1
         NNEIG=NNEIG+1
         IF (JJ.GT.NNEIG) THEN
            ISDWK1(JJ)=ISDWK1(NNEIG)
            ISDWK1(NNEIG)=J
         ENDIF
C
C        SUM UP THE ACRES
C
         CALL SPLAAR (J,X,IRC)
         IF (IRC.GT.0) THEN
            IRC=1
            RETURN
         ENDIF
         CONTIG=CONTIG+X
      ELSEIF (IRC.EQ.2) THEN
         IRC=1
         RETURN
      ENDIF
   20 CONTINUE
C
C     IF STAND I WAS NOT A NEIGHBOR OF ANY STANDS BELOW, EXIT.
C
      IF (INEI.EQ.0) GOTO 40
   30 CONTINUE
   40 CONTINUE
      IRC=0
      RETURN
      END
