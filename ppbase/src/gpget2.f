      SUBROUTINE GPGET2 (IACTK,ISDATE,MAXPRM,NPRMS,PRMS,MAXSTD,
     >                  NSTND,ISTDLS, LOK)
      IMPLICIT NONE
C----------
C  **GPGET   DATE OF LAST REVISION:  07/31/08
C----------
C     
c     Test version that works within the master cycle.
c
C     GET A GLOBAL ACTIVITY, FREE THE LINKED LIST OF STANDS, AND
C     SET THE ACTIVITY STATUS CODE TO 'DONE'.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION TO PROGNOSIS.
C     P.W. THOMAS--FORESTRY SCIENCES LAB, MOSCOW, ID--MARCH 1986
C
C     SUBROUTINES CALLED :
C
C       LLCOPY - COPY A LINKED LIST INTO AN ARRAY.
C       LLSCRH - SCRATCH A LINKED LIST.
C
C     PARAMETERS :
C
C       IACTK  - GLOBAL ACTIVITY NUMBER.
C       ISDATE - THE DATE (YEAR) THE ACTIVITY IS SCHEDULED TO OCCUR.
C       MAXPRM - MAXIMUM NUMBER OF PARAMETERS RETURNED.
C       NPRMS  - NUMBER OF PARAMETERS RETURNED.
C       PRMS   - ARRAY OF PARAMETERS RETURNED.
C       MAXSTD - MAXIMUM NUMBER OF STANDS RETURNED.
C       NSTND  - ACTUAL NUMBER OF STANDS RETURNED.
C       ISTDLS - ARRAY OF STANDS RETURNED.
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
      INCLUDE 'PPEPRM.F77'
      INCLUDE 'PPGPCM.F77'
      INCLUDE 'PPCNTL.F77'
C
COMMONS
C
      INTEGER ISTDLS(MAXSTD),MAXSTD,NSTND,NPRMS,ISDATE,IACTK
      INTEGER MAXPRM,I,ISUB,NGPARM,J,ISUB2
      REAL PRMS(MAXPRM)
      LOGICAL   LOK

C     IF NO GLOBAL OPTIONS, THEN EXIT

      LOK = .FALSE.

      IF (MGPCUR.LE.0) GOTO 999

C     LOOP OVER GLOBAL OPTIONS.

      DO 40 I=1,MGPCUR
      
        ISUB=MGPCYC(I)

C     IF THIS OPTION IS NOT IN THE RIGHT YEAR, SKIP. (This could be merged
C     with the subsequent IF statement.)
        
        IF(MIDATE(ISUB) .NE. ISDATE) GOTO 40
        
C     IF THIS OPTION IS NOT THE ONE WE ARE LOOKING FOR, OR IF IT HAS
C     BEEN DONE, THEN KEEP LOOKING

        
        IF (MIACT(1,ISUB).NE.IACTK .OR. MIACT(6,ISUB).EQ.1) GOTO 40
        NGPARM=MIACT(3,ISUB)-MIACT(2,ISUB)+1
        NPRMS=NGPARM
        IF (NGPARM.GT.MAXPRM) NPRMS=MAXPRM
C
C     WE FOUND THE OPTION.  LOAD THE PARMS.
C
        DO 30 J=1,NPRMS
          ISUB2=MIACT(2,ISUB)+J-1
          PRMS(J)=GPARMS(ISUB2)
   30   CONTINUE
C
C     LOAD THE DATE THE ACTIVITY WAS ACTUALLY SCHEDULED TO OCCUR
C
        ISDATE=MIDATE(ISUB)
C
C     GET THE LIST OF STANDS THIS GLOBAL OPTIONS APPLIES TO...AND
C     SCRATCH THE LINKED LIST.
C
        CALL LLCOPY (ISTDLS,MAXSTD,NSTND,MIACT(4,ISUB))
        CALL LLSCRH (MIACT(4,ISUB),MIACT(5,ISUB))
C
C     SET THE ACTIVITY STATUS CODE TO 'DONE' and the flag to TRUE.
C
        MIACT(6,ISUB)=1
        LOK = .TRUE.
        
   40 CONTINUE
  
  999 CONTINUE
  
      RETURN
      END
