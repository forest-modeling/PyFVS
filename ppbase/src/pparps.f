      SUBROUTINE PPARPS
      IMPLICIT NONE
C----------
C  **PPARPS  DATE OF LAST REVISION:  07/31/08
C----------
C
C     CREATE THE AUTOMATIC "START-UP" REPLICATIONS REQUESTED
C     ON THE ADDSTAND KEYWORD.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--DEC 1992
C
C     CALLED FROM : PPMAIN.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      INTEGER NCOL(3),I,NC,IC,I1,I2,IREP
      REAL XBRWT,A
C
C     JUST RETURN IF THE REQUESTED REPS IS ONE.
C
      IF (NAURPS.LE.1) RETURN
C
C     MAKE SURE THAT THE MAX NUMBER OF REPS IS WITHIN BOUNDS
C     OF BOTH 9**3=729 AND MXSTND.
C
      IF (NAURPS.GT.729 .OR. NAURPS+NOSTND.GT.MXSTND) THEN
         I=MIN(MXSTND-NOSTND+1,729)
         WRITE (JOPPRT,5) NAURPS,I
    5    FORMAT (/' ******** WARNING: REQUESTED AUTO REPS REQUESTED=',
     >          I4,' HAS BEEN SET=',I4)
         NAURPS=I
         IF (NAURPS.EQ.1) RETURN
      ENDIF
C
C     COMPUTE THE BRANCH WEIGHTING FACTOR AND SET THE "CURRENT" STAND
C     VALUE.
C
      XBRWT=1.0/FLOAT(NAURPS)
      BRCHWT(ISTND)=XBRWT
C
C     SET UP THE INTERNAL STAND NUMBER CREATION LOGIC AND MODIFY
C     THE ALREADY PROCESSED CISN.
C
      NC=1
      IF (NAURPS.GT. 9) NC=2
      IF (NAURPS.GT.81) NC=3
      DO 10 IC=1,3
      NCOL(IC)=0
      IF (IC.LE.NC) NCOL(IC)=1
   10 CONTINUE
      WRITE (CISNUM(ISTND),'(I4,3I1,''0000'')') ISTND,NCOL
      IF (PDEBUG) WRITE (JOPPRT,40) ISTND,STDIDS(ISTND),CISNUM(ISTND),
     >   BRCHWT(ISTND),(ISNKEY(ISTND,I),I=1,4)
C
C     LOOP OVER THE REPS.
C
      I1=ISTND+1
      I2=NOSTND+NAURPS-1
      DO 60 IREP=I1,I2
      ISTND=ISTND+1
      NOSTND=NOSTND+1
      STDIDS(ISTND)=STDIDS(I1-1)
      MGMIDS(ISTND)=MGMIDS(I1-1)
      SLSETS(ISTND)=SLSETS(I1-1)
      LNSETS(ISTND)=LNSETS(I1-1)
      ISNKEY(ISTND,1)=ISNKEY(I1-1,1)
      ISNKEY(ISTND,4)=ISNKEY(I1-1,4)
      BRCHWT(ISTND)=XBRWT
      SAMWTS(ISTND)=SAMWTS(I1-1)
      ISNSRT(ISTND)=ISTND
C
C     FIGURE OUT THE INTERNAL STAND NUMBER ID.
C
      NCOL(NC)=NCOL(NC)+1
      IF (NC.GT.1) THEN
         DO 20 IC=NC,2,-1
         IF (NCOL(IC).GE.10) THEN
            NCOL(IC)=1
            NCOL(IC-1)=NCOL(IC-1)+1
         ENDIF
   20    CONTINUE
      ENDIF
      WRITE (CISNUM(ISTND),'(I4,3I1,''0000'')') I1-1,NCOL
C
C     CALL THE BASE-MODEL AND ESTAB RANDOM NUMBER GENERATORS TO FORCE
C     A UNIQUE SEQUENCE OF NUMBER FOR EACH REPLICATION.
C
      CALL ESRANN (A)
      CALL   RANN (A)
C
C     STORE THE REPLICATION.
C
      CALL PUTSTD
C
      IF (PDEBUG) WRITE (JOPPRT,40) ISTND,STDIDS(ISTND),CISNUM(ISTND),
     >   BRCHWT(ISTND),(ISNKEY(ISTND,I),I=1,4)
   40 FORMAT(/' IN PPARPS, ISTND=',I4,'; STDIDS=',A,
     >       '; CISNUM=',A,' BRCHWT=',F6.3,
     >       '; ISNKEY(ISTND,1:4)=',4I5)
   60 CONTINUE
C
      RETURN
      END
