      SUBROUTINE GETPRT (IPLACE,JYR,IOAGE)
      IMPLICIT NONE
C----------
C  **GETPRT  DATE OF LAST REVISION:  07/31/08
C----------
C
C     READS THE PRINT-DATA FOUND AT POSITION IPLACE IN THE DIRECT
C     ACCESS FILE IDAPRT.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--FEB 1982
C
C     THE VALUE OF IPTELS IS USED TO DESCRIBE TO THIS ROUTINE WHICH
C     DATA ARE LOCATED AT POSITION IPLACE.  THE FIRST WORD OF THE
C     FIRST RECORD SHOULD ALWAYS MATCH IPTELS; IF IT DOES NOT THIS
C     ROUTINE ABENDS THE RUN VIA ERRGRO.
C
C     CALLED FROM RPTWTR.
C
C     IPLACE= RECORD NUMBER IN DIRECT ACCESS FILE IDAPRT WHERE
C             PRINT DATA IS FOUND.
C     JYR   = YEAR INPUT FROM DIRECT ACCESS RECORD.
C     IOAGE = STAND AGE INPUT FROM DIRECT ACCESS RECORD.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
      INTEGER IOAGE,JYR,IPLACE,I,JPTELS,IPSPCT
      LOGICAL LREMOV
C
C     SET THE SWITCHES WHICH CONTROL THE READ AND DISPLY USING THE
C     VALUE OF IPTELS.
C
      I=MOD(IPTELS,10)
      ICL6=0
      IF (I.EQ.0) ICL6=1
      IF (I.EQ.3) ICL6=-99
      LREMOV=I.EQ.2
      IF (.NOT.LREMOV) ONTREM(7)=0.
C
C     WRITE DEBUG, IF REQUESTED.
C
      IF (PDEBUG) WRITE (JOPPRT,5) IDAPRT,IPLACE,IPTELS,I,ICL6,
     >                             LREMOV
    5 FORMAT (/' IN GETPRT: IDAPRT=',I3,'; IPLACE=',I5,'; IPTELS=',I3,
     >        '; I=',I2,'; ICL6=',I3,' LREMOV=',L2)
C
      IF(ICL6.LE.0) GOTO 10
C
      LSTART=.TRUE.
      READ (IDAPRT,REC=IPLACE) JPTELS,IOAGE,ITITLE,NPLT,MGMID,IOSPCT,
     >       IOSPCV,IOSPMC,IOSPBV,IONSP,LCVOLS,LBVOLS,LMORT
      IF (IPTELS.NE.JPTELS) GOTO 40
      IASVPT=IPLACE+1
      READ (IDAPRT,REC=IASVPT) JYR,LCVOLS,LBVOLS,IFINT,ONTCUR,OSPCT,
     >      OCVCUR,OSPCV,OMCCUR,OSPMC,OBFCUR,OSPBV,
     >      DBHIO,HTIO,IOICR,DGIO,PCTIO,PRBIO
      IFST=0
      RETURN
   10 CONTINUE
      LSTART=.FALSE.
C
C     IF THIS IS NORMAL CYCLING, READ THE PRINT-DATA.
C
      IF(ICL6.NE.0) GOTO 30
      JYR=IY(ICYC+1)
      READ (IDAPRT,REC=IPLACE) JPTELS,IFST,IOAGE,IOSPAC,IOSPMO,
     >      IPSPCT,IOSPCV,IOSPMC,IOSPBV,IONSP
      IF (IPTELS.NE.JPTELS) GOTO 40
      IASVPT=IPLACE+1
      READ (IDAPRT,REC=IASVPT) JYR,OACC,OSPAC,OMORT,OSPMO,ONTCUR,OSPCT,
     >       OCVCUR,OSPCV,OMCCUR,OSPMC,OBFCUR,OSPBV,IOAGE,ORMSQD,
     >       OLDTPA,OLDBA,OLDAVH,RELDM1,IFINT,DBHIO,HTIO,IOICR,DGIO,
     >       PCTIO,PRBIO
C
C     IF REMOVAL OCCURED, THEN READ REMOVAL DATA.
C
      IF (.NOT.LREMOV) GOTO 20
      IASVPT=IASVPT+1
      READ (IDAPRT,REC=IASVPT) IOSPTT,IOSPTV,IOSPMR,IOSPBR,IOSPRT
      IASVPT=IASVPT+1
      READ (IDAPRT,REC=IASVPT) ONTREM,OSPTT,OCVREM,OSPTV,OMCREM,OSPMR,
     >       OBFREM,OSPBR,ONTRES,OSPRT,ATAVD,ATTPA,ATBA,ATAVH,ATCCF
   20 CONTINUE
      RETURN
   30 CONTINUE
C
C     FINAL OUTPUT FOR STAND.
C
      READ (IDAPRT,REC=IPLACE) JPTELS,IOAGE,ORMSQD,OLDTPA,OLDBA,OLDAVH,
     >       RELDM1,LREMOV,ATAVD,ATTPA,ATBA,ATAVH,ATCCF
      IF (IPTELS.NE.JPTELS) GOTO 40
      RETURN
   40 CONTINUE
C
C     BRANCH HERE IF THE PRINT-DATA DESCRIPTION WORD PASSED TO THIS
C     ROUTINE DOES NOT MATCH THE VALUE FOUND IN THE FILE.
C
      WRITE (JOPPRT,50) IPLACE,IPTELS,JPTELS
   50 FORMAT (/' DATA RECORD NUMBER=',I5,'; RECORD DESCRIPTION WORD ',
     >        'SHOULD BE ',I5,' BUT ACTUALLY IS ',I5)
      CALL PPERRP (.FALSE.,12)
C
C     THE NEXT STMT SHOULD NEVER, NEVER EXEC.
C
      STOP 100
      END
