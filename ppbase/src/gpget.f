      SUBROUTINE GPGET (IACTK,ISDATE,MAXPRM,NPRMS,PRMS,MAXSTD,
     >                  NSTND,ISTDLS)
      IMPLICIT NONE
C----------
C  **GPGET--PPBASE  DATE OF LAST REVISION:  07/31/08
C----------
C
C     GET A GLOBAL ACTIVITY, FREE THE LINKED LIST OF STANDS, AND
C     SET THE ACTIVITY STATUS CODE TO 'DONE'.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION TO PROGNOSIS.
C     P.W. THOMAS--FORESTRY SCIENCES LAB, MOSCOW, ID--MARCH 1986
C
C     SUBROUTINES CALLED :
C
C       LLCOPY - COPY A LINKED LIST INTO AN ARRAY.
C       LLSCRH - SCRATCH A LINKED LIST.
C
C     PARAMETERS :
C
C       IACTK  - GLOBAL ACTIVITY NUMBER.
C       ISDATE - THE DATE THE ACTIVITY IS SCHEDULED TO OCCUR.
C       MAXPRM - MAXIMUM NUMBER OF PARAMETERS RETURNED.
C       NPRMS  - NUMBER OF PARAMETERS RETURNED.
C       PRMS   - ARRAY OF PARAMETERS RETURNED.
C       MAXSTD - MAXIMUM NUMBER OF STANDS RETURNED.
C       NSTND  - ACTUAL NUMBER OF STANDS RETURNED.
C       ISTDLS - ARRAY OF STANDS RETURNED.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPGPCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
COMMONS
C
C
      INTEGER ISTDLS(MAXSTD),NSTND,NPRMS,ISDATE,IACTK,MAXPRM,I,ISUB
      INTEGER NGPARM,J,ISUB2,MAXSTD
      REAL PRMS(MAXPRM)
C
C     IF NO GLOBAL OPTIONS, THEN EXIT
C
      IF (MGPCUR.LE.0) GOTO 999
C
C     LOOP OVER GLOBAL OPTIONS.
C
      DO 40 I=1,MGPCUR
      ISUB=MGPCYC(I)
C
C     IF THIS OPTION IS NOT THE ONE WE ARE LOOKING FOR, OR IF IT HAS
C     BEEN DONE, THEN KEEP LOOKING
C
      IF (MIACT(1,ISUB).NE.IACTK .OR. MIACT(6,ISUB).EQ.1) GOTO 40
      NGPARM=MIACT(3,ISUB)-MIACT(2,ISUB)+1
      NPRMS=NGPARM
      IF (NGPARM.GT.MAXPRM) NPRMS=MAXPRM
C
C     WE FOUND THE OPTION.  LOAD THE PARMS.
C
      DO 30 J=1,NPRMS
      ISUB2=MIACT(2,ISUB)+J-1
      PRMS(J)=GPARMS(ISUB2)
   30 CONTINUE
C
C     LOAD THE DATE THE ACTIVITY WAS ACTUALLY SCHEDULED TO OCCUR
C
      ISDATE=MIDATE(ISUB)
C
C     GET THE LIST OF STANDS THIS GLOBAL OPTIONS APPLIES TO...AND
C     SCRATCH THE LINKED LIST.
C
      CALL LLCOPY (ISTDLS,MAXSTD,NSTND,MIACT(4,ISUB))
      CALL LLSCRH (MIACT(4,ISUB),MIACT(5,ISUB))
C
C     SET THE ACTIVITY STATUS CODE TO 'DONE'.
C
      MIACT(6,ISUB)=1
   40 CONTINUE
  999 CONTINUE
      RETURN
      END
