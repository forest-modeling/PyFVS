      SUBROUTINE HVDNSL
      IMPLICIT NONE
C----------
C  **HVDNSL DATE OF LAST REVISION:  07/31/08
C----------
C
C     CALLED BY HVALOC TO SET UP THE DENSITY STATISTICS FOR
C     REPRESENTING NEIGHBORING DENSITY EFFECTS.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - NOV 1992
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
      INCLUDE 'PPCNTL.F77'
C
C
      INCLUDE 'HVDNCM.F77'
C
C
COMMONS
C
C
      INTEGER I
C
C     IF THE NEIGHBOR DENSITY OPTION IS OFF, THEN RETURN.
C
      IF (.NOT.LNEDNG) RETURN
C
C     AT THIS TIME, THE SELECTION STATUS CODE IS KNOWN FOR ALL OF THE
C     STANDS, AND THE DENSITY OF THE STAND AS "SELECTED" VS "NOT
C     SELECTED" IS ALSO KNOWN.
C
C     ******* PROTOTYPE: ASSUME THAT THERE IS ONLY ONE MSPOLICY, THAT
C     ALL STANDS ARE BEING MANAGED UNDER THE MSPOLICY, AND THAT NO
C     STANDS WILL BE REPLICATED IN HVREPS.
C
C     LOOP OVER STANDS FOR MSPOLICY 1 AND LOAD THE APPLICABLE DENSITY
C     STATISTICS INTO THE FIRST COLUMN OF THE ARRAYS USED TO STORE THEM.
C
      AXXBA =0.0
      AXXHT =0.0
      AXXCCF=0.0
      DO 20 I=1,NOSTND
      IF (LHVDEB) WRITE (JOPPRT,10) I,IHVSTA(I,1),HVDBA(I,1),
     >  HVDBA(I,2),HVDAHT(I,1),HVDAHT(I,2),HVDCCF(I,1),HVDCCF(I,2)
   10 FORMAT (' IN HVDNSL, I=',I4,'; IHVSTA(I,1)=',I3,'; HVDBA(I,1:2)=',
     >         2F8.2,'; HVDAHT(I,1:2)=',2F8.2,'; HVDCCF(I,1:2)=',2F8.2)
      IF (IHVSTA(I,1).EQ.3) THEN
         HVDBA (I,1)=HVDBA (I,2)
         HVDAHT(I,1)=HVDAHT(I,2)
         HVDCCF(I,1)=HVDCCF(I,2)
      ENDIF
C
C     ADD UP THE TOTAL BA, AVH, CCF TO COMPUTE AN AVERAGE.
C
      AXXBA =AXXBA +HVDBA (I,1)
      AXXHT =AXXHT +HVDAHT(I,1)
      AXXCCF=AXXCCF+HVDCCF(I,1)
   20 CONTINUE
C
C     COMPUTE THE AVERAGES...THESE ARE UNWEIGHTED BECAUSE THE
C     "****** PROTOTYPE ******" ASSUMPTION IS THAT ALL STANDS ARE THE
C     SAME SIZE.
C
      AXXBA =AXXBA /FLOAT(NOSTND)
      AXXHT =AXXHT /FLOAT(NOSTND)
      AXXCCF=AXXCCF/FLOAT(NOSTND)
C
      IF (LHVDEB) WRITE (JOPPRT,'('' IN HVDNSL, AXXBA='',F8.2,
     >           ''; AXXHT='',F8.2,''; AXXCCF='',F8.2,
     >           ''; NOSTND='',I6)') AXXBA,AXXHT,AXXCCF,NOSTND
      RETURN
      END
