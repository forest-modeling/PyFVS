      SUBROUTINE LDSUM (IOPT)
      IMPLICIT NONE
C----------
C  **LDSUM--PPBASE   DATE OF LAST REVISION:  03/05/10
C----------
C
C     LOADS THE SUMMARY OUTPUT ARRAY.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--JAN 1981
C
C     CALLED FROM:
C
C       PPEMAIN - MULTI-STAND PROGNOSIS MODEL (PPE) MAIN PROGRAM.
C       GRADD   - END OF CYCLE FOR ONE PPE STAND.
C       GROEND  - END OF PROJECTION FOR ONE PPE STAND.
C       GRSTND  - PROJECT PPE STAND UP TO MASTER STARTING YEAR.
C       INSTND  - INPUT AND INITIATE ONE PPE STAND.
C
C     PARAMETERS:
C
C     IOPT = 1, INITIAL CALIBRATION STATISTICS.
C            2, NORMAL CYCLING
C            3, LAST LINE OF STATISTICS ADDED.
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'SUMTAB.F77'
C
C
COMMONS
C----------
C  DECLARATIONS
C----------
      CHARACTER*7 VVER
      INTEGER IOPT,IXF,I,IFIA,IROW,IP1,ISWT
      REAL DUM1,SUMAGE,AGEKNT,STAGEA,STAGEB,TEMSDI,DUMSDI
C----------
C  IDENTIFY VARIANT
C----------
      CALL VARVER (VVER)
C----------
C  CALCULATE FOREST TYPE (IFORTP), SIZE CLASS (ISZCL),
C  AND STOCKING CLASS (ISTCL)
C----------
      DUM1=0.
      IXF=2
      CALL FORTYP(IXF,DUM1)
C----------
C  ESTIMATE STAND AGE FROM SIZE CLASS AND ABIRTH
C  IF IT WAS MISSING FROM THE INPUT DATA, SET IT FROM THIS ESTIMATE..
C----------
      IF(LSTART)THEN
        SUMAGE=0.
        AGEKNT=0.
C*        WRITE(JOSTND,*)' IAGE,ISZCL,ITRN= ',IAGE,ISZCL,ITRN
        IF(ISZCL.EQ.0 .OR. ISZCL.GT.3)THEN
          ICAGE=0
          GO TO 42
        ELSEIF(ISZCL .EQ. 3) THEN
          DO I=1,ITRN
          IF(DBH(I).LT.5.)THEN
            SUMAGE=SUMAGE+ABIRTH(I)*PROB(I)
            AGEKNT=AGEKNT+PROB(I)
C*            WRITE(JOSTND,*)' I,D,AGE,PROB,SUMAGE,AGEKNT= ',
C*     &      I,DBH(I),ABIRTH(I),PROB(I),SUMAGE,AGEKNT
          ENDIF
          ENDDO
        ELSEIF(ISZCL .EQ. 2) THEN
          DO I=1,ITRN
          IF(FIAJSP(ISP(I))(1:1).EQ.'-')THEN
            IFIA=998
          ELSE
            READ(FIAJSP(ISP(I)),'(I4)') IFIA
          ENDIF
          IF((IFIA.LT.300. .AND. DBH(I).GE.5. .AND. DBH(I).LT.9.) .OR.
     &       (IFIA.GE.300. .AND. DBH(I).GE.5. .AND. DBH(I).LT.11.))THEN
            SUMAGE=SUMAGE+ABIRTH(I)*PROB(I)
            AGEKNT=AGEKNT+PROB(I)
C*            WRITE(JOSTND,*)' I,IFIA,D,AGE,PROB,SUMAGE,AGEKNT= ',
C*     &      I,IFIA,DBH(I),ABIRTH(I),PROB(I),SUMAGE,AGEKNT
          ENDIF
          ENDDO
        ELSE
          DO I=1,ITRN
          IF(FIAJSP(ISP(I))(1:1).EQ.'-')THEN
            IFIA=998
          ELSE
            READ(FIAJSP(ISP(I)),'(I4)') IFIA
          ENDIF
          IF((IFIA.LT.300. .AND. DBH(I).GE.9.) .OR.
     &       (IFIA.GE.300. .AND. DBH(I).GE.11.))THEN
            SUMAGE=SUMAGE+ABIRTH(I)*PROB(I)
            AGEKNT=AGEKNT+PROB(I)
C*            WRITE(JOSTND,*)' I,IFIA,D,AGE,PROB,SUMAGE,AGEKNT= ',
C*     &      I,IFIA,DBH(I),ABIRTH(I),PROB(I),SUMAGE,AGEKNT
          ENDIF
          ENDDO
        ENDIF
        IF(AGEKNT.GT.0.)THEN
C*        WRITE(JOSTND,*)' AGEKNT= ',AGEKNT
          ICAGE=IFIX(SUMAGE/AGEKNT)
        ELSE
          ICAGE=0
        ENDIF
C*        WRITE(JOSTND,*)' SUMAGE,AGEKNT,ICAGE= ',SUMAGE,AGEKNT,ICAGE
C----------
C  REMOVE THE COMMENT CHARACTERS (C**) IN THE FOLLOWING STATEMENT
C  TO ENABLE REPORTING THE INITIAL STAND AGE, AGECMP, (IF 0 IN STDINFO
C  KEYWORD) IN THE SUMMARY STATISTICS TABLE.  DO **NOT** USE FOR THE
C  FOLLOWING VARIANTS, AK, CI, CS, IE, KT, LS, NE, NI, SE, TT, UT, WS,
C  WHICH DO NOT CONSISTENTLY USE SITE CURVES FOR HEIGHT GROWTH.
C----------
C**        IF(IAGE .EQ. 0)IAGE=ICAGE
      ENDIF
   42 CONTINUE
C
C
      GOTO (10,100,200),IOPT
C
C     INITIAL STATISTICS
C
   10 CONTINUE
      IOSUM(1,1)=IY(1)
      IOSUM(2,1)=IAGE
      IOSUM(3,1)=TPROB/GROSPC+.5
      IOSUM(4,1)=OCVCUR(7)/GROSPC+.5
      IOSUM(5,1)=OMCCUR(7)/GROSPC+.5
      IOSUM(6,1)=OBFCUR(7)/GROSPC+.5
      DO 20 I=7,10
      IOSUM(I,1)=0
   20 CONTINUE
      IOSUM(11,1)=BA/GROSPC+.5
      IOSUM(12,1)=RELDEN/GROSPC+.5
      IOSUM(13,1)=AVH+.5
      IOSUM(14,1)=0
      IOSUM(15,1)=0
      IOSUM(16,1)=0
C
      IOSUM(18,1)=IFORTP
      IOSUM(19,1)=ISZCL
      IOSUM(20,1)=ISTCL
      QDBHAT(1)=RMSQD
      IOLDBA(1)=BA/GROSPC+.5
      CALL SDICLS(0,0.,999.,1,TEMSDI,DUMSDI,STAGEA,STAGEB,0)
      ISDI(1)=TEMSDI/GROSPC + 0.5
      IBTCCF(1)=RELDEN/GROSPC+.5
      IBTAVH(1)=AVH+0.5
      QSDBT(1)=RMSQD
      ISDIAT(1)=ISDI(1)
      IROW=1
      GOTO 310
C
C     NORMAL CYCLING.
C
  100 CONTINUE
      IP1=ICYC+1
      IOSUM(1,IP1)=IY(IP1)
      IOSUM(2,IP1)=IAGE+IY(IP1)-IY(1)
      IOSUM(3,IP1)=ONTCUR(7)/GROSPC+.5
      IOSUM(4,IP1)=OCVCUR(7)/GROSPC+.5
      IOSUM(5,IP1)=OMCCUR(7)/GROSPC+.5
      IOSUM(6,IP1)=OBFCUR(7)/GROSPC+.5
      IOSUM(7,ICYC)=ONTREM(7)/GROSPC+.5
      IOSUM(8,ICYC)=OCVREM(7)/GROSPC+.5
      IOSUM(9,ICYC)=OMCREM(7)/GROSPC+.5
      IOSUM(10,ICYC)=OBFREM(7)/GROSPC+.5
      IOSUM(11,ICYC)=OLDBA/GROSPC+.5
      IOSUM(12,ICYC)=RELDM1/GROSPC+.5
      IOSUM(13,ICYC)=OLDAVH+.5
      IOSUM(14,ICYC)=IFINT
      IOSUM(15,ICYC)=OACC(7)/GROSPC+.5
      IOSUM(16,ICYC)=OMORT(7)/GROSPC+.5
C
      IOSUM(18,IP1)=IFORTP
      IOSUM(19,IP1)=ISZCL
      IOSUM(20,IP1)=ISTCL
      IOLDBA(IP1)=BA/GROSPC + 0.5
      CALL SDICLS(0,0.,999.,1,TEMSDI,DUMSDI,STAGEA,STAGEB,0)
      ISDI(IP1)=TEMSDI/GROSPC + 0.5
      IBTCCF(IP1)=RELDEN/GROSPC+0.5
      IBTAVH(IP1)=AVH+0.5
      QSDBT(IP1)=RMSQD
      QDBHAT(IP1)=RMSQD
      ISDIAT(IP1)=ISDI(IP1)
C
C     IF THINNING OCCURED; THEN: USE THE POST-THIN DENSITY STATISTICS,
C     EXCEPT FOR TREES/ACRE INWHICH CASE USE THE PRE-THIN STATISTIC.
C
      IF (ONTREM(7).LE.0) GOTO 300
      IOSUM(3,ICYC)=OLDTPA/GROSPC+.5
      IOSUM(11,ICYC)=ATBA/GROSPC+.5
      IOSUM(12,ICYC)=ATCCF/GROSPC+.5
      IOSUM(13,ICYC)=ATAVH+.5
      QDBHAT(ICYC)=ATAVD
      ISDIAT(ICYC)=SDIAC/GROSPC+0.5
      GOTO 300
C
C     LOAD THE LAST LINE OF STATISTICS.
C
  200 CONTINUE
C
C  CALCUALTE LAST CYCLE MAI VALUE
C
          IF ((VVER(:2) .EQ. 'CS') .OR. (VVER(:2) .EQ. 'LS') .OR.
     1      (VVER(:2) .EQ. 'NE') .OR. (VVER(:2) .EQ. 'OZ') .OR.
     2      (VVER(:2) .EQ. 'SE') .OR. (VVER(:2) .EQ. 'SN')) THEN
            BCYMAI(ICYC)=(IOSUM(4,ICYC)+TOTREM)/IOSUM(2,ICYC)
          ELSE
            BCYMAI(ICYC)=(IOSUM(5,ICYC)+TOTREM)/IOSUM(2,ICYC)
          ENDIF
C
      DO 220 I=7,16
      IOSUM(I,ICYC)=0
  220 CONTINUE
      IOSUM(11,ICYC)=BA/GROSPC+.5
      IOSUM(12,ICYC)=RELDEN/GROSPC+.5
      IOSUM(13,ICYC)=AVH+.5
      IOSUM(18,IP1)=IFORTP
      IOSUM(19,IP1)=ISZCL
      IOSUM(20,IP1)=ISTCL
      IOLDBA(ICYC)=BA/GROSPC + 0.5
      CALL SDICLS(0,0.,999.,1,TEMSDI,DUMSDI,STAGEA,STAGEB,0)
      ISDI(IP1)=TEMSDI/GROSPC + 0.5
      IBTCCF(IP1)=RELDEN/GROSPC+0.5
      IBTAVH(IP1)=AVH+0.5
      QSDBT(IP1)=RMSQD
      QDBHAT(IP1)=RMSQD
      ISDIAT(IP1)=ISDI(ICYC)
  300 CONTINUE
C
      IROW=ICYC
  310 CONTINUE
C
C     LOAD THE STAND SAMPLING WEIGHT.
C
      IF (SAMWT.GE. .99999) ISWT=IFIX(SAMWT+.0001)
      IF (SAMWT.LT. .99999) ISWT=IFIX(SAMWT*100000.+.5)
      IOSUM(17,IROW)=ISWT
      RETURN
      END
