      SUBROUTINE HVSTSL (ISTND,JOPPRT)
      IMPLICIT NONE
C----------
C  **HVSTSL DATE OF LAST REVISION:  07/31/08
C----------
C
C     CALLED FROM HVREPS.  CALL EVTSTV (EVENT MONITOR ROUTINE THAT
C     MAINTAINS THE TEST VARIABLES) TO SET THE VALUE OF "SELECTED"
C     FOR THE STAND.  NOTE THAT THE APPLICALBLE ENTRIES IN EVTSTV
C     ARE EVUST4 AND EVSET4.
C
C     MULTISTAND POLICY ROUTINE - N.L. CROOKSTON  - JULY 1987
C     FORESTRY SCIENCES LABORATORY - MOSCOW, ID 83843
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'PPEPRM.F77'
C
C
      INCLUDE 'PPHVCM.F77'
C
C
COMMONS
C
C
      INTEGER JOPPRT,ISTND,I,IHV
C
C
C
      IF (LHVDEB) WRITE (JOPPRT,'(/'' IN HVSTSL.1 LHIER= '',L2,
     >     '' ISTND='',I5,'' IHVSTA(ISTND,1:15)='',15I2)') LHIER,ISTND,
     >    (IHVSTA(ISTND,I),I=1,IXHRVP)
C
C     USE TWO DIFFERENT METHODS TO PROCESS THE STATUS CODES, DEPENDING
C     ON THE USE OF POLICY HIERARCHIES.
C
      IF (LHIER) THEN
C
C        IF THE FIRST-LEVEL HIERARCHY IS "SELECTED", THEN SET THE
C        EVENT MONITOR CODE TO SELECTED AND SET THE STATUS
C        CODES FOR THE OTHER POLICIES TO 1 (POLICY APPLIES TO THE
C        STAND).
C
         IF (IHVSTA(ISTND,1).EQ.3) THEN
            CALL EVSET4 (9,1.0)
            DO 10 IHV=2,IXHRVP
            IF (IHVSTA(ISTND,IHV).EQ.2) IHVSTA(ISTND,IHV)=1
   10       CONTINUE
C
C        IF THE STAND IS NOT SELECTED UNDER THE FIRST-LEVEL OF
C        THE HIERARCHY, FIND OUT IF IT IS UNDER SOME OTHER POLICY.
C
         ELSEIF (IHVSTA(ISTND,1).EQ.2) THEN
            CALL EVSET4 (9,0.0)
            DO 20 IHV=2,IXHRVP
            IF (IHVSTA(ISTND,IHV).EQ.3) CALL EVSET4 (9,1.0)
            IF (IHVSTA(ISTND,IHV).GE.2) IHVSTA(ISTND,IHV)=1
   20       CONTINUE
         ENDIF
C
      ELSE
C
C        LOOP OVER POLICY STATUS CODES.
C
         DO 40 IHV=1,IXHRVP
C
C        PROCESS STATUS CODES THAT ARE 2 OR GREATER
C
         IF (IHVSTA(ISTND,IHV).GE.2) THEN
C
C           IF THE POLICY STATUS CODE IS A 2, THEN SET THE
C           STAND TO "NOT SELECTED".
C
            IF (IHVSTA(ISTND,IHV).EQ.2) THEN
               CALL EVSET4 (9,0.0)
            ELSE
C
C              THE POLICY STATUS CODE IS A 3.  SET THE STAND
C              TO "SELECTED".
C
               CALL EVSET4 (9,1.0)
            ENDIF
C
C           RESET THE POLICY STATUS CODES FOR THIS AND ALL
C           APPLICABLE POLICIES TO 1 INDICATING THAT
C           THE STAND APPLIES TO THESE HARVEST POLICIES.
C
            DO 30 I=1,IXHRVP
            IF (IHVSTA(ISTND,I).GE.2) IHVSTA(ISTND,I)=1
   30       CONTINUE
C
C           BRANCH TO EXIT.
C
            GOTO 50
         ENDIF
   40    CONTINUE
C
C        ALL OF THE STATUS CODES MUST BE 0 (OR 1, WHICH WOULD BE AN ERROR)
C        SO SET THE VALUE OF "SELECTED" TO UNDEFINED.
C
         CALL EVUST4 (9)
   50    CONTINUE
      ENDIF
C
      IF (LHVDEB) WRITE (JOPPRT,'( '' IN HVSTSL.2 LHIER= '',L2,
     >     '' ISTND='',I5,'' IHVSTA(ISTND,1:15)='',15I2)') LHIER,ISTND,
     >    (IHVSTA(ISTND,I),I=1,IXHRVP)
C
C     CALL HVNEDN TO SET THE DENSITY EFFECTS OF NEIGHBORS.
C
      CALL HVNEDN
      RETURN
      END
