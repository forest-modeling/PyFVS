      SUBROUTINE PPDGF (XPPDDS,BAO,BALO,CCFO,PCCFO,DBH,CLBA,CDBAL,
     &                  CPCCF,BREL,BBA)
      IMPLICIT NONE
C----------
C  **PPDGF--BM  DATE OF LAST REVISION:  09/18/08
C----------
C
C     IF ARGUMENT LIST CHANGES, ALSO CHANGE EXPPNB ROUTINE
C
C     COMPUTE THE DDS MODIFIER THAT ACCOUNTS FOR THE DENSITY
C     OF NEIGHBORING STANDS.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--NOV 1992
C
C     MODIFIED FOR BLUE MTNS     BY  GARY DIXON -- FT COLLINS, CO
C                                    FOREST MANAGEMENT SERVICE CENTER
C     CALLED FROM: DGF.
C
COMMONS
C
C
      INCLUDE 'PPDNCM.F77'
C
C
COMMON
C----------
C     IF THE NEIGHBOR DENSITY EFFECTS ARE BEING INCLUDED, THEN
C     CALCULATE THEM, OTHERWISE, RETURN.
C
C     NOTE:  PDBAN IS IN SQ FT/ACRE
C            CCF IS IN IT'S NATURAL UNITS.
C     DON'T LET THE FUNCTION BE USED FOR DBH < 1.0 (IT'S TOO HARSH).
C
C  FIRST TERM IS CCF & CCF-MAI INTERACTION FOR SPECIES 1 & 11
C  SECOND TERM IS BAL/ALOG(D+1)
C    NOTE: ASSUME ADDITIONAL STAND BA WILL BE IN BIGGER TREES
C  THIRD TERM IS POINT CCF
C  FOURTH TERM IS STAND BA FOR SPECIES 2
C  FIFTH TERM IS ALOG(BA)
C
C
      REAL BBA,BREL,CPCCF,CDBAL,CLBA,DBH,PCCFO,CCFO,BALO,BAO,XPPDDS
      REAL D,ALB,B
C
C
C----------
      IF (LNEDNS) THEN
         D=AMAX1(1.0,DBH)+1
         ALB=0.
         B=AMAX1(0.0,PDBAN-BAO)
         IF(B .GT. 0.)ALB=ALOG(B)
         XPPDDS= BREL*(AMAX1(0.0,PDCCFN-CCFO))            +
     >           CDBAL*(AMAX1(0.0,PDBAN-BALO)/ALOG(D))     +
     >           CPCCF*(AMAX1(0.0,PDCCFN-PCCFO))            +
     >           BBA*(AMAX1(0.0,PDBAN-BALO))             +
     >           CLBA*ALB
      ENDIF
      RETURN
      END
