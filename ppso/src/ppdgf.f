      SUBROUTINE PPDGF (XPPDDS,BALO,CCFO,PCCFO,DBH,BREL,BBAL,BDBL,
     &                  BCCF,BLCF,BCF2)
      IMPLICIT NONE
C----------
C  **PPDGF--SO  DATE OF LAST REVISION:  09/18/08
C----------
C
C     IF ARGUMENT LIST CHANGES, ALSO CHANGE EXPPNB ROUTINE
C
C     COMPUTE THE DDS MODIFIER THAT ACCOUNTS FOR THE DENSITY
C     OF NEIGHBORING STANDS.
C
C     PART OF THE PARALLEL PROCESSING EXTENSION OF PROGNOSIS SYSTEM.
C     N.L. CROOKSTON--FORESTRY SCIENCES LAB, MOSCOW, ID--NOV 1992
C
C     MODIFIED FOR SORNEC        BY  GARY DIXON -- FT COLLINS, CO
C                                    FOREST MANAGEMENT SERVICE CENTER
C     CALLED FROM: DGF.
C
COMMONS
C
C
      INCLUDE 'PPDNCM.F77'
C
C
COMMON
C----------
C
C
      REAL XPPDDS,BALO,CCFO,PCCFO,DBH,BREL,BBAL,BDBL,BCCF,BLCF,BCF2
      REAL D,ALC,C
C
C
C     IF THE NEIGHBOR DENSITY EFFECTS ARE BEING INCLUDED, THEN
C     CALCULATE THEM, OTHERWISE, RETURN.
C
C     NOTE:  PDBAN IS IN SQ FT/ACRE
C            CCF IS IN IT'S NATURAL UNITS.
C     DON'T LET THE FUNCTION BE USED FOR DBH < 1.0 (IT'S TOO HARSH).
C
C  FIRST TERM IS CCF & CCF-MAI INTERACTION
C  SECOND TERM IS BAL
C    NOTE: ASSUME ADDITIONAL STAND BA WILL BE IN BIGGER TREES
C  THIRD TERM IS BAL/ALOG(D+1)
C  FOURTH TERM IS CCF
C  FIFTH TERM IS ALOG(CCF) FOR SPECIES 10
C  SIXTH TERM IS CCF SQUARED FOR SPECIES 10
C----------
      IF (LNEDNS) THEN
         D=AMAX1(1.0,DBH)+1
         ALC=0.
         C=AMAX1(0.0,PDCCFN-CCFO)
         IF(C .GT. 0.)ALC=ALOG(C)
         XPPDDS= BREL*(AMAX1(0.0,PDCCFN-CCFO))            +
     >           BDBL*(AMAX1(0.0,PDBAN-BALO)/ALOG(D))     +
     >           BCCF*(AMAX1(0.0,PDCCFN-PCCFO))            +
     >           BBAL*(AMAX1(0.0,PDBAN-BALO))             +
     >           BLCF*ALC                                 +
     >           BCF2*(AMAX1(0.0,PDCCFN-PCCFO) **2.)
      ENDIF
      RETURN
      END
