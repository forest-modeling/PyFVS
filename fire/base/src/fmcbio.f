      SUBROUTINE FMCBIO (IT, D, H, CRR, KSP, ABIO, MBIO, RBIO,IWHO)
      IMPLICIT NONE
C----------
C  $Id$
C----------
*     SINGLE-STAND VERSION
*     CALLED FROM: FMSCUT
*                  FMCRBOUT
*                  FMSADD
*     CALLS:       JENKINS
*                  CALCBIOMASS
*  PURPOSE:
*     CALL JENKINS TO ESTIMATE THE BIOMASS EQUATIONS FOR ABOVEGROUND
*     BIOMASS, MERCHANTABLE BIOMASS, AND BELOWGROUND BIOMASS, LOADS
*     ARRAYS FOR WRITING JENKINS BIOMASS ESTIMATES TO TREE LIST.CALLS
*     CALCBIOMASS TO ESTIMATE THE TREE COMPONENT BIOMASS WEIGHTS.
*     THE ENTRY BMEQNOS VALIDATES BIOMASS COMPONENT EQUATION NUMBER
*     AND IS CALLED FROM FMIN.
***********************************************************************
*
*  CALL LIST DEFINITIONS:
*     D:    DBH (IN)
*     KSP:  SPECIES CODE
*     ABIO: (RETURNED) ABOVEGROUND BIOMASS (KG)
*     MBIO: (RETURNED) MERCHANTABLE
*     RBIO: (RETURNED) ROOT BIOMASS
*     IWHO: =0 CALL IS FROM FMSADD (TREE RECORD IS PART SNAG)
*     IWHO: =1 CALL IS FROM FMCRBOUT (STANDING TREE)
*     IWHO: =2 CALL IS FROM FMSCUT (HARVESTED TREE)
*
***********************************************************************

C     PARAMETER INCLUDE FILES.

      INCLUDE 'PRGPRM.F77'
      INCLUDE 'FMPARM.F77'

C     COMMON INCLUDE FILES

      INCLUDE 'PLOT.F77'
      INCLUDE 'CONTRL.F77'
      INCLUDE 'ARRAYS.F77'
      INCLUDE 'FMCOM.F77'
      INCLUDE 'FMFCOM.F77'
      INCLUDE 'FMPROP.F77'
      INCLUDE 'METRIC.F77'
      INCLUDE 'BEQINFO.INC'
      INCLUDE 'DBSCOM.F77'

C     VARIABLE DECLARATIONS

      LOGICAL   DEBUG
      REAL      ABIO, MBIO, RBIO, D, DCM, KGtoTI
      INTEGER   KSP, IGRP, IT,IWHO
      REAL      DTMP,BIOMS(8),THT
      CHARACTER(LEN=12) BMCMPEQNO(47)
      INTEGER   I,IFLAGBM,J,JEN
      REAL H,CRR,BMS,P3,BMSTOT

C     CHECK FOR DEBUG.

      CALL DBCHK (DEBUG,'FMCBIO',6,ICYC)
      IF (DEBUG) WRITE(JOSTND,1) ICYC, IWHO
    1 FORMAT(' ENTERING FMCBIO CYCLE=',I2,'IWHO = ',I2)
C
      IGRP = BIOGRP(KSP)
      DCM    = D * INtoCM
      DTMP=D
      THT=H
      KGtoTI = TMtoTI / 1000.
      ABIO = 0.0
      MBIO = 0.0
      RBIO = 0.0
      DO I=1,8
      BIOMS(I)=0.
      ENDDO
C
C  1. ABOVEGROUND BIOMASS (ABM)
C  2. MERCH BIOMASS  (MBM) ("Stem wood" in Jenkin's paper -> defined as a proportion)
C                          (Note that merch is defined from a 12in stump height, so we
C                           will define it as being for trees above the merch limit.)
C  3. BELOWGROUND LIVE (BBM) ("Coarse roots" in Jenkin's paper -> defined as a proportion)
C  CALCULATE JENKINS EQUATIONS OF BIOMASS (GE 2.5 CM)
C      
      IF (DCM .GE. 2.5) THEN
        CALL JENKINS(IGRP,DTMP,BIOMS)
        DTMP=D
        ABIO=BIOMS(1)
        MBIO=BIOMS(2)
        RBIO=BIOMS(5)
C
C  LOAD TREE-LEVEL JENKINS BIOMASS COMPONENTS TO BE WRITTEN
C  TO THE TREE LIST DATABASE TABLE
C
        IF(IWHO.EQ.1)THEN
          DO JEN=1,8
          JENKTRE(IT,JEN)=BIOMS(JEN)*KGtoTI
          ENDDO
        ENDIF
      ELSE
C       IF TREE IS VERY SMALL, THEN CALCULATE THE VALUE AT 2.5CM, AND USE
C       ONLY A PROPORTION OF IT.
        DTMP=2.5/2.54
        CALL JENKINS(IGRP,DTMP,BIOMS)
        DTMP=D
        ABIO=BIOMS(1)
        MBIO=BIOMS(2)
        RBIO=BIOMS(5)
        ABIO = ABIO*(DCM/2.5)
        RBIO = RBIO*(DCM/2.5)
        MBIO = MBIO*(DCM/2.5)
C
C  LOAD TREE-LEVEL JENKINS BIOMASS COMPONENTS TO BE WRITTEN
C  TO TREE LIST AND CUTLIST DATABASE TABLES
C
        IF(IWHO.EQ.1)THEN
          DO JEN=1,8
          JENKTRE(IT,JEN)=BIOMS(JEN)*(DCM/2.5)*KGtoTI
          ENDDO
        ENDIF            
      ENDIF
C     
      DTMP=D
      IF (DTMP .LT. DBHMIN(KSP))MBIO=0.
      ABIO = ABIO * KGtoTI
      MBIO = MBIO * KGtoTI
      RBIO = RBIO * KGtoTI
C
C  IF SECOND CALL FROM FMSADD (SNAG KEYWORD) THEN SKIP CALCULATING
C  TREE BIOMASS CONPONENTS
C
      IF(IWHO.LE.0)GOTO 7000
C
C  IF NEITHER BIOMASS COMPONENET TABLE IS TO BE PRINTED SKIP
C  BIOMASS COMPONENT CALCULAIONS
C
      IF((IFMBMCMP.LE.0).AND.(IFMBMCMPCUT.LE.0))GOTO 7000
C
C   If Biomass Eq. entered by user with the
C   BIOMASEQ keyword evaluate it here and load arrays 
C   BMEQNO(I,J), I = SPECIES, J= 1,47 - EQUATION NO FOR COMPONENT
C   BMVALUE(I,J) I = SPECIES, J= 1,47 - CALCULATED BIOMASS VALUE FOR
C                                       SPECIES I, COMPONENT J=1, 47
C  THERE ARE 47 COMPONENT CATEGORIES
C
      IF(DEBUG)THEN
        IF(REMP(IT).GT.0.)WRITE(JOSTND,*)'IN FMCBIO-IWHO,D,H,',
     & 'REMP(IT)= ',IWHO,D,H,REMP(IT)
      ENDIF
      DO I=1,47
      IF(BMEQNO(KSP,I).NE.'')THEN
        BMS=0.
        P3=FLOAT(ICR(IT))
        IF(P3.LT.1.E-10)P3=0.
        CALL CALCBIOMASS(BMEQNO(KSP,I),DTMP,THT,P3,BMS)
        DTMP=D
        IF(BMS.LT.1.E-10)BMS=0.
        IF(IWHO.EQ.1)THEN
          BMCMPTRE(IT,I)=BMS*LBtoKG*KGtoTI
        ELSE
          BMCMPTRE(IT,I)=0.
        ENDIF
        IF((IWHO.EQ.2).AND.(REMP(IT).GT.0))THEN
          BMCMPCUT(IT,I)=BMS*LBtoKG*KGtoTI
        ELSE
          BMCMPCUT(IT,I)=0.
        ENDIF        
      ENDIF
      ENDDO
      IF(DEBUG)WRITE(JOSTND,*)'IT,(BMCMPCUT(IT,I),I=1,47)= ',
     &  IT,(BMCMPCUT(IT,I),I=1,47)
 7000 CONTINUE
      RETURN
C
      ENTRY BMEQNOS(BMCMPEQNO,IFLAGBM)
C
C  CALLED FROM FMIN - CHECK TO MAKE SURE THE USER INPUT BM EQ IS VALID
C
C  BEQN - ARRAY HOLDING BIOMASS EQUATIONS AVAILABLE IN BIOMASS LIBRARY
C  BMCMPEQNO - ARRAY 1-47 OF EQUATIONS FOR EACH BIOMASS COMPONENT
C  IN BIOMASS LIBRARY, CURRENTLY ONLY 47 COMPONENTS IDENTIFIED.
C
      DO J= 1,47
      DO I=1,TOTEQ
      IF((BMCMPEQNO(J).EQ.BEQN(I)).OR.
     &   (BMCMPEQNO(J).EQ.'            '))THEN
        IFLAGBM=J
        GOTO 8000
      ENDIF
      ENDDO
      IFLAGBM=8888
      IF(IFLAGBM.EQ.8888)GOTO 9000
 8000 CONTINUE
      ENDDO
 9000 CONTINUE
      RETURN
      END
