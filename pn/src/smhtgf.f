      SUBROUTINE SMHTGF (I,HHT,H,MODE,DTIME,ICYC,JOSTND,DEBUG)
      IMPLICIT NONE
C----------
C  **SMHTGF--PN  DATE OF LAST REVISION:  05/04/10
C----------
C  THIS ROUTINE CALCULATES THE HEIGHT GROWTH OF SMALL TREES (D<3.0 IN).
C  WHEN CALLED FROM ESSUBH (MODE=0), DTIME= TOTAL TREE AGE.
C  IF CALLED FROM REGENT (MODE=1), DTIME= FINT AND THE  
C  RETURN VARIABLE (HHT) IS THE HEIGHT INCREMENT.
C  CALLED FROM **ESSUBH
C  CALLED FROM **REGENT
C
C  INPUT VARIABLES
C  I      - SPECIES SEQUENCE NUMBER
C  H      - BEGINING HEIGHT
C  SI     - SITE INDEX FOR SPECIES
C  MODE   - = 0 IF CALL IS FROM ESSUBH, = 1 IF CALL IS FROM REGENT 
C  DTIME  - DTIME=TOTAL SEEDLING AGE (MODE= 0), DTIME=FINT (MODE= 1)
C
C  RETURN VARIABLES
C  HHT    - HEIGHT GROWTH OVER TIME INCREMENT DTIME (REGENT)
C  HHT    - HEIGHT 5 YEARS INTO CYCLE, OR END OF CYCLE FINT<5 (ESSUBH)
C
C
COMMONS
C
      INCLUDE 'PRGPRM.F77'
C
      INCLUDE 'PLOT.F77'
C
COMMONS
C
      LOGICAL DEBUG
      INTEGER I,ICYC,JOSTND,MODE
      REAL    DTIME,H,HHT,SI
C----------
C  IF NO GROWTH IS TO BE CALCULATED ON ESTAB TREES, I.E. PLANTING
C  IS MORE THAN 5 YEARS INTO THE SIMULATION, THEN RETURN AND
C  LEAVE THE SMALL TREE GROWTH, DURING THE PLANTING CYCLE, TO
C  THE REGENT ROUTINE.
C----------
      IF(DTIME .LE. 0.0) GO TO 170
C
      SI= SITEAR(I)
C             1   2   3   4   5   6   7   8   9  10  
      GO TO (10, 20, 20, 30, 40,150, 50, 60, 70, 30,
     &       80, 70, 90, 90, 70,140, 60,150,100,110,
     &       60,120, 60, 60, 60, 60, 60,160, 60,130,
     &       60, 60, 60, 60, 60, 60, 60, 60, 60),I
 
C----------
C  HEIGHT OF TALLEST SUBSEQUENT SPECIES 1 (SF) **********
C----------
   10 CONTINUE
C
      HHT = ((-1.05989 + 0.25820*SI)/(41.35-0.0129*SI))*DTIME
      GO TO 170
C----------
C  HEIGHT OF TALLEST SUBS. SPECIES 2 (WF) **********
C  ALSO SPECIES 3 (GF)
C----------
   20 CONTINUE
C
       HHT = ((4.2435 + 0.1510*SI)/(19.0184 - 0.0570*SI))*DTIME 
       GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 4 (AF) **********
C  ALSO SPECIES 10 (ES)
C----------
   30 CONTINUE
C
      HHT = ((0.09211 + 0.208517*SI)/(43.358 - 0.168166*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 5 (RF) **********
C----------
   40 CONTINUE
C
      HHT = ((-2.21784 + 0.38546*SI)/(34.81367 - 0.11174*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 7 (NF) ********** 
C---------- 
   50 CONTINUE
C
      HHT = ((11.26677 + 0.12027*SI)/(27.93806 - 0.02873*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 8 (YC) **********
C  ALSO SPECIES 17,21,23-29,31-39
C----------
   60 CONTINUE
C
      HHT = ((1.47043 + 0.23317*SI)/(31.56252 - 0.05586*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 9 (IC) **********
C  ALSO SPECIES 12 (JP) & 15 (PP) 
C----------
   70 CONTINUE
C
      HHT = ((-1.0 + 0.32857*SI)/(28.0-0.042857*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 11 (LP) **********
C----------
   80 CONTINUE
C
      HHT = (0.02008805*SI)*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 13 (SP) **********
C  ALSO SPECIES 14 (WP)
C----------
   90 CONTINUE
C
      HHT = ((-2.53561 + 0.24876*SI)/(41.02281 - 0.15988*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 19 (WH) **********
C----------
  100 CONTINUE
C
      HHT = ((-5.74874 + 0.54576*SI)/(26.15767 - 0.03596*SI))*DTIME
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 20 (MH) **********
C  IN METERS CONVERTED TO FEET
C---------- 
  110 CONTINUE
C
      HHT = ((0.965758 + 0.082969*SI)/(55.24961 - 1.28885*SI))*DTIME
     &      *3.280833
      GO TO 170
C----------
C  HT OF TALLEST SUBS. SPECIES 22 (RA) **********
C----------
  120 CONTINUE
C
      HHT = (-0.007025 + 0.056794*SI)*DTIME
      GO TO 170   
C----------
C  HT OF TALLEST SUBS. SPECIES 30 (LL) **********
C----------
  130 CONTINUE
C
      HHT = ((-3.97245 + 0.50995*SI)/(28.11668-0.05661*SI))*DTIME     
      GO TO 170   
C----------
C  HT OF TALLEST SUBS. SPECIES 16 (DF) **********
C----------
  140 CONTINUE
C
      HHT = ((1.78245 + 0.47506*SI)/(24.94144-0.02419*SI))*DTIME     
      GO TO 170   
C----------
C  HT OF TALLEST SUBS. SPECIES  6 (SS) **********
C  ALSO SPECIES 18 (RC)
C----------
  150 CONTINUE
C
      HHT = ((-3.01994 + 0.51633*SI)/(26.01954-0.03636*SI))*DTIME
      GO TO 170   
C----------
C  HT OF TALLEST SUBS. SPECIES 28 (WO) **********
C----------
  160 CONTINUE
C
      HHT = (((-37.60812*ALOG(1-(SI/114.24569)**.44444))*0.01)-0.1)
     &      *DTIME
  170 CONTINUE 
C----------    
      IF (DEBUG)
     & WRITE(JOSTND,*)' LEAVING SMHTGF, ICYC,I,HHT,DTIME,SI= ',
     &ICYC,I,HHT,DTIME,SI
C
      RETURN
      END
