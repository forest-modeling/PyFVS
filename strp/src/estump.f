      SUBROUTINE ESTUMP (JSSP,DBH,PREM,JPLOT,ISHAG)
      IMPLICIT NONE
C----------
C  **ESTUMP--STRP   DATE OF LAST REVISION:  10/30/09
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ESPARM.F77'
C
C
      INCLUDE 'ESHOOT.F77'
C
C
      INCLUDE 'ESHAP.F77'
C
C
      INCLUDE 'ESCOMN.F77'
C
C
COMMONS
C
C     STORE INFORMATION FOR STUMP SPROUT SUBROUTINE.
C
      INTEGER JPLOT,JSSP,ISSP,IDBH
      REAL PREM,DBH,DBHEND
      INTEGER*4 MDBH,IPLT,MSP,ISHI,I,MNAGDF,IBEST
      INTEGER ISHAG
      DATA MDBH/10000000/,MSP/10000/
      DO 10 I=1,NSPSPE
      IF(JSSP.EQ.ISPSPE(I)) GO TO 11
   10 CONTINUE
      GO TO 900
   11 CONTINUE
      ISSP=I
C
C     DETERMINE DIAMETER CLASS OF THE TREE BEING CUT.
C
      DO 100 I=1,NDBHCL-1
      DBHEND=(DBHMID(I)+DBHMID(I+1))/2.0
      IF(DBH.GT.DBHEND) GO TO 100
      IDBH=I
      GO TO 110
  100 CONTINUE
      IDBH=NDBHCL
  110 CONTINUE
      IPLT=JPLOT
      IF(IPLT.GT.9999) IPLT=9999
      ISHI = IDBH*MDBH + ISSP*MSP + IPLT

C     IF THERE IS ROOM IN THE LIST, THEN CREATE A NEW STUMP RECORD.
      ITRNRM=ITRNRM+1
C----------
C ISHOOT() IS A NUMBER COMPOSED OF DDDSSSPPPP, WHERE DDD = DBH CLASS,
C SSS = SPECIES, AND PPPP = PLOT.  DECODED IN **ESUCKR**.
C PRBREM() CONTAINS AMOUNT OF PROB REMOVED IN CUTTING TREE RECORD.
C----------
      IF (ITRNRM.LE.MAXTRE) THEN
         DSTUMP(ITRNRM) = DBH
         ISHOOT(ITRNRM) = ISHI
         PRBREM(ITRNRM) = PREM
         JSHAGE(ITRNRM) = ISHAG
      ELSE
         ITRNRM = MAXTRE

C        THE LIST IS FULL. FIND AN EXISTING RECORD THAT MATCHES AS BEST
C        IT CAN AND ADD THE PROB TO THAT RECORD. IF NO MATCH IS FOUND,
C        THEN IGNORE THE STUMP.

         MNAGDF = 9999
         IBEST  = 0

         DO I=1,ITRNRM
            IF (ISHI.EQ.ISHOOT(I)) THEN
               IF (ABS(ISHAG-JSHAGE(I)).LT.MNAGDF) THEN
                  IBEST = I
                  MNAGDF = ABS(ISHAG-JSHAGE(I))
               ENDIF
            ENDIF
            IF (MNAGDF.EQ.0) EXIT
         ENDDO

         IF (IBEST.GT.0) PRBREM(IBEST) = PRBREM(IBEST)+PREM
      ENDIF

  900 CONTINUE
      RETURN
      END
